# 4.自定义类型

第 2 章“数据类型和结构”讨论了内置类型，包括容器。在本章中，我们将学习如何创建用户定义的类型。

![img/477168_1_En_4_Figa_HTML.gif](img/477168_1_En_4_Figa_HTML.gif) *类型与结构*

让我们在 Julia 语言环境中澄清这两个术语。

一个对象的*类型*，说白了就是指描述该对象的一组特征。例如，一个类型为 *sheet* 的对象可以用它的尺寸、重量、大小或颜色来描述。

Julia 中的所有值都是属于给定类型的真实对象(它们是给定类型的单个“实例”)。

Julia 类型包括由固定数量的位组成的所谓的*原始类型*(像所有数字类型，如`Int64`、`Float64`和`Char`)和*复合类型*或*结构*，其中对象的特征使用多个字段和可变数量的位来描述。

*结构*和*原语类型*都可以是用户定义的，并且是分层组织的。*结构*大致对应于其他语言中所谓的*类*。

使用类型时，两个运算符特别重要:

*   `::`操作符用于约束一个给定类型的对象。例如，`a::B`表示“a 必须是 B 型”。

*   `<:`操作符有类似的意思，但是在对象可以是给定类型的任何子类型的意义上，它更宽松一些。例如，`A<:B`表示“A 一定是 B 的子类型”，即 B 是“父”类型，A 是它的“子”类型。

使用这些操作符的主要原因是为了确认程序按照预期的方式运行。在某些情况下，提供这些额外的信息也可以提高性能。

## 4.1 基本类型定义

用户定义的原语类型是用关键字`primitive type`及其名称和所需的位数定义的:

```py
primitive type [name] [bits] end

```

例如:

```py
primitive type My10KBBuffer 81920 end

```

Julia 的一个(当前)限制是位数必须是 8388608 以下的 8 的倍数。

或者，父类型可以指定如下:

```py
primitive type [name] <: [supertype] [bits] end

```

请注意，具有相同位数的两个用户定义类型的内部表示完全相同。唯一会改变的是它们的名称，但这是一个重要的区别:当这些类型的对象作为参数传递时，函数的行为方式发生了变化，即区别它们的是程序中命名类型的使用，而不是它们的实现。

## 4.2 结构定义

要定义一个结构，您可以使用关键字`mutable struct`，给该结构一个名称，指定字段，然后用关键字`end`结束定义:

```py
mutable struct MyOwnType      JULIA
  field1
  field2::String
  field3::Int64
end

```

请注意，虽然您可以选择将每个单独的字段定义为给定的类型(例如，`field3::Int64`)，但是您不能将字段定义为给定类型的子类型(例如，`field3<:Number`)。为此，您可以在结构定义中使用模板:

```py
mutable struct MyOwnType{T<:Number}      JULIA
 field1
 field2::String
 field3::T
end

```

使用模板，结构的定义是在第一次构造一个`field3`类型为`T`的对象时动态创建的。

用来注释单个字段的类型可以是基元类型(就像前面的例子)，也可以是对另一个结构的引用(参见后面的例子)。

还要注意，与其他高级语言(如 Python)不同，Julia 不能在定义结构后添加或删除字段。如果您需要这个功能，您必须使用字典来代替，但是请注意，您将会牺牲这个灵活性来换取更差的性能。

相反，为了获得性能(但又牺牲了灵活性)，可以省略`struct`前面的`mutable`关键字。这样，一旦创建了该类型的对象，它的字段就不能再被改变(即，默认情况下结构是不可变的)。

注意，可变对象——如数组——在不可变结构中也是可变的。

## 4.3 对象初始化和使用

定义自定义类型(基元或结构)后，可以从该类型初始化任意数量的对象:

```py
myObject = MyOwnType("something","something",10)      JULIA
a = myObject.field3 # 10
myObject.field3 = 20 # only if myObject is a mutable struct

```

请注意，您必须使用与结构定义中指定的顺序相同的值来初始化对象。您不能通过名称初始化对象(相反，考虑一下命名元组):

```py
MyOwnType(field3 = 10, field1 = "something", field2 = "something") # Error!

```

要访问对象字段或更改它们的值，可以像在大多数其他语言中一样使用`object.field`语法。

![img/477168_1_En_4_Figc_HTML.gif](img/477168_1_En_4_Figc_HTML.gif) *功能和结构*

请注意，处理对象的函数没有在类型定义中定义或声明，也就是说，它们不与特定的类型相关联。

这意味着在 Julia 中不是调用一个对象方法，比如`myobj.func(x,y)`，而是将对象作为一个参数传递，比如`func(myobj,x,y)`，不管它是作为第一个、第二个还是进一步的参数。

## 4.4 抽象类型和继承

您可以使用关键字`abstract type`创建抽象类型。抽象类型不允许保存任何字段，并且不能从它们实例化对象。相反，带有字段的具体类型可以被定义为它们的子类型以及从它们实例化的对象。一个允许抽象类实际持有字段的问题(见 [`https://github.com/JuliaLang/julia/issues/4935`](https://github.com/JuliaLang/julia/issues/4935) )目前还未解决，可能会在 Julia 的下一个主要版本中实现。

尽管目前不支持多重继承(当一个类型是多个类型的子类型时),但是您可以创建抽象类型的整个层次结构:

```py
abstract type MyOwnGenericAbstractType end      JULIA
abstract type MyOwnAbstractType1 <: MyOwnGenericAbstractType end
abstract type MyOwnAbstractType2 <: MyOwnGenericAbstractType end
mutable struct AConcreteType1 <: MyOwnAbstractType1
  f1::Int64
  f2::Int64
end
mutable struct AConcreteType2 <: MyOwnAbstractType1
  f1::Float64
end

mutable struct AConcreteType3 <: MyOwnAbstractType2
  f1::String
end

```

你不能定义具体类型的子类型，只能定义抽象类型的子类型。

如果抽象类型不能有字段，为什么还需要继承？这并不是为了节省一次定义跨多个类型的公共字段的时间(您可以使用 *composition* 来实现这一点，稍后讨论)。

更确切地说，正是在程序中类型的*用法*中，它们的层次结构的定义变得有用。

考虑以下对象:

```py
o1 = AConcreteType1(2,10)      JULIA
o2 = AConcreteType2(1.5)
o3 = AConcreteType3("aa")

```

由于它们都是`MyOwnGenericAbstractType`的子类型，您可以定义一个函数为它们提供默认实现:

```py
function foo(a :: MyOwnGenericAbstractType)      JULIA
  println("Default implementation: $(a.f1)")
end
foo(o1) # Default implementation: 2
foo(o2) # Default implementation: 1.5
foo(o3) # Default implementation: aa

```

然后，您可以决定提供一个函数，为类型是`MyOwnAbstractType1`的子类型的所有对象提供一个更专门化的实现:

```py
function foo(a :: MyOwnAbstractType1)      JULIA
  println("A more specialised implementation: $(a.f1*4)")
end
foo(o1) # A more specialised implementation: 8
foo(o2) # A more specialised implementation: 6.0
foo(o3) # Default implementation: aa

```

这是可能的，感谢在第 [3](03.html) 章中讨论的*多重分派*机制。当一个函数的多个*方法*可用于分派一个函数调用时，Julia 将选择更严格的一个，即在精确参数类型或其更直接的*超类型*(父类型)上定义的方法:

```py
function foo(a :: AConcreteType1)      JULIA
     println("A even more specialised implementation: $(a.f1 + a.f2)")
end
foo(o1) # An even more specialized implementation: 12
foo(o2) # A more specialized implementation: 6.0
foo(o3) # Default implementation: aa

```

### 4.4.1 在 Julia 中实现面向对象的范例

Julia 允许继承和组合模型，尽管支持程度不同。

*继承*就是你刚刚看到的，当一个类型的层次结构被获得，声明一个类型为另一个类型的子类型。您还看到了在 Julia 中使用继承的局限性，只有行为(而不是结构)可以被继承。

那么，如何在 Julia 中实现一个面向对象的模型呢？Julia 社区的偏好是使用复合而不是继承。

*复合*是指将一个给定类型的字段声明为另一个复合类型的对象。通过这个(被引用的)对象，您可以访问其他类型的字段。

考虑下面的例子，首先定义一个通用的`Person`结构，然后定义两个更具体的`Student`和`Employee`结构:

```py
struct Shoes      JULIA
   shoesType::String
   colour::String
end

struct Person
  myname::String
  age::Int64
end

struct Student
   p::Person
   school::String
   shoes::Shoes
end

struct Employee
   p::Person
   monthlyIncomes::Float64
   company::String
   shoes::Shoes
end

```

不使用继承来声明`Student`和`Employee`作为`Person`的子类型，而是使用组合来为它们分配一个`Person`类型的字段`p`。正是因为有了这个字段，才不需要重复两者共有的字段。

![img/477168_1_En_4_Fige_HTML.gif](img/477168_1_En_4_Fige_HTML.gif)类型必须先定义，然后才能用于引用对象。因此，`Shoes`定义必须在`Students`和`Employee`定义之前。

然后，您可以创建专用类型的实例，方法是首先创建被引用的对象，或者在专用类型的构造函数中内联完成该操作:

```py
gymShoes = Shoes("gym","white")      JULIA
proShoes = Shoes("classical","brown")

Marc = Student(Person("Marc",15),"Divine School",gymShoes)
MrBrown = Employee(Person("Brown",45),3200.0,"ABC Corporation Inc.", proShoes)

```

最后，您可以使用多重分派来为专用类型提供定制的实现，并通过点(`.`)操作符的链式使用来访问被引用的对象和/或通用字段:

```py
function printMyActivity(self::Student)      JULIA
println("Hi! I am $(self.p.myname), I study at $(self.school) school, and I wear $(self.shoes.colour) shoes")
end

function printMyActivity(self::Employee)
println("Good day. My name is $(self.p.myname), I work at $(self.company) company and I wear
$(self.shoes.colour) shoes")
end

printMyActivity(Marc)      # Hi! I am Marc, ...
printMyActivity(MrBrown)   # Good day. My name is MrBrown, ...

```

虽然明智地使用继承在实践中可以满足您可能遇到的许多建模设计情况，但 Julia 核心语言仍然缺少一些表达能力，因为您无法直接考虑和/或区分对象之间关系的不同概念，如专门化(例如，人→学生)、组合(例如，人→手臂)和弱关系(例如，人→鞋子)。

为了解决这一不足，已经发布了几个第三方包(本书中没有讨论)来提高 Julia 在这一领域的灵活性，如 SimpleTraits ( [`https://github.com/mauro3/SimpleTraits.jl`](https://github.com/mauro3/SimpleTraits.jl) )包来模仿多重继承，或`OOPMacro.jl` ( [`https://github.com/ipod825/OOPMacro.jl`](https://github.com/ipod825/OOPMacro.jl) )包来自动将字段声明从父类型复制到子类型。

## 4.5 一些与类型相关的有用函数

为了完成关于用户定义类型的讨论，您可能会发现以下与类型相关的函数很有用:

*   `supertype(MyType)`:返回一个类型的父类型

*   `subtypes(MyType)`:列出一个类型的所有子类型

*   `fieldnames(MyType)`:查询一个结构的所有字段

*   `isa(obj,MyType)`:检查`obj`是否属于`MyType`类型

*   `typeof(obj)`:返回`obj`的类型

*   `eltype(obj)`:返回集合的内部类型，类似于数组的内部元素