# 5.输入/输出

本章包括对以下第三方软件包的讨论:

<colgroup><col class="tcol1 align-left"> <col class="tcol2 align-left"> <col class="tcol3 align-left"></colgroup> 
| 

包名

 | 

统一资源定位器

 | 

版本

 |
| --- | --- | --- |
| `CSV.jl` | [T2`https://github.com/JuliaData/CSV.jl`](https://github.com/JuliaData/CSV.jl) | `v0.5.11` |
| `HTTP.jl` | [T2`https://github.com/JuliaWeb/HTTP.jl`](https://github.com/JuliaWeb/HTTP.jl) | `v0.8.4` |
| `XLSX.jl` | [T2`https://github.com/felipenoris/XLSX.jl`](https://github.com/felipenoris/XLSX.jl) | `v0.5.4` |
| `JSON2.jl` | [T2`https://github.com/quinnj/JSON2.jl`](https://github.com/quinnj/JSON2.jl) | `v0.3.1` |

Julia 中的输入/输出是这样实现的:首先选择合适的`IOStream`对象——一个文件、用户终端或网络对象——然后在该流上应用所需的函数。总体思路是打开流，执行所需的操作，然后关闭流。

关于用户终端，Julia 通过内置的`stdin`和`stdout`(最终还有`stderr` ) IO 流提供与它的交互。这些流已经打开，是输入和输出操作的默认流。

关于 web 资源，您目前仍然需要使用第三方软件包。我们将在本章中考虑`HTTP.jl`(参见 [`https://github.com/JuliaWeb/HTTP.jl`](https://github.com/JuliaWeb/HTTP.jl) )。

## 5.1 阅读(输入)

### 从终端读取

*   `aString = readline()`:读取用户在终端中键入的任何内容，直到按下![../images/477168_1_En_5_Chapter/477168_1_En_5_Figa_HTML.gif](../images/477168_1_En_5_Chapter/477168_1_En_5_Figa_HTML.gif)。

*   `anInteger = parse(Int64, readline())`:读取用户在终端输入的数字。

请注意，您很少会希望这样。Julia 是交互式的，您可以运行部分代码，或者直接从终端或正在使用的编辑器中更改变量。

不过，您仍然可以创建一个 Julia 脚本(如下例所示)并运行它(在 Julia 提示符中使用`julia myScript.jl`或`include("myScript.jl")`)。这将模仿经典的“脚本”行为，即用户运行一个脚本，但不能完全控制它，只能输入脚本请求的用户输入:

```
println("Welcome to a Julia script\n\n")      JULIA

function getUserInput(T=String,msg="")
  print("$msg ")
  if T == String
      return readline()
  else
    try
      return parse(T,readline())
    catch
     println("Sorry, I could not interpret your answer. Please try again")
     getUserInput(T,msg)
    end
  end
end

sentence = getUserInput(String,"Which sentence do you want to be repeated?");
n = getUserInput(Int64,"How many times do you want it to be repeated?");
[println(sentence) for i in 1:n]
println("Done!")

```

请注意，在这段代码中，function 使用运行时异常来确保用户的响应与您想要的类型兼容。你将在第 8 章中了解更多关于运行时异常的内容。

### 从文件中读取

在 Julia 中，文件读取(以及，您将看到，写入)与其他语言相似。您打开文件，指定模态(`r`，代表“阅读”)，将文件绑定到一个对象，对对象进行操作，并在完成后使用`close()`。

一个更好的替代方法是将文件操作封装在一个`do`块中，当块结束时自动关闭文件。

*   一次读取整个文件:

    ```
    open("afile.txt", "r") do f    # "r" for reading      JULIA
      filecontent = read(f,String) # Note that it can be used only once. If used a second time, without
    reopening the file, read() would return an empty string
      # ... do what you want with the file content ...
    end

    ```

*   Reading a file line by line:

    ```
    open("afile.txt", "r") do f      JULIA
      for ln in eachline(f)
        # ... do what you want with the line ...
      end
    end

    ```

    这种方法甚至可以用于不适合内存的非常大的文件。

*   逐行读取文件并跟踪行号:

    ```
    open("afile.txt", "r") do f      JULIA
       for (i,ln) in enumerate(eachline(f))
         println("$i $ln")
       end
    end

    ```

#### 为矩阵导入数据

您可以使用`readdlm()`(在标准库包`DelimitedFiles`中)将数据从文件导入矩阵。

您可以使用 slice 运算符跳过行和/或列，然后转换为所需的类型:

```
myData = convert(Array{Float64,2},readdlm("myinputfile.csv",'\t')[2:end,4:end]); # Skip the first 1
row and the first 3 columns

```

#### 解析逗号分隔值(CSV)文件

您可以使用`CSV.jl`包的`read`功能:

```
myData = CSV.read(file; delim=';', missingstring="NA", decimal=',')

```

`read`支持一长串(大部分是不言自明的)选项:`delim`(对于制表符分隔的文件使用`\t`)`quotechar`、`openquotechar`、`closequotechar`、`escapechar`、`missingstring`、`dateformat`、`append`、`writeheader`、`header`、`newline`、`quotestrings`、`decimal`、`header`、`normalizenames`、`datarow`、`skipto`、`footerskip`、`limit`、`transpose`、`comment`、`comment`

结果是一个*数据框架*(一种广泛使用的数据表格格式，你将在第 [10 章](10.html)中广泛了解)。除非使用了选项`header=false`，否则第一行将被解释为标题。

使用大量的初始行自动识别每个字段的类型，但有时这还不够。例如，在某些数据集中，您可能有一个所有值都缺失的字段，然后突然非缺失值很晚才出现在数据集中。当您试图用一些值填充字段时，这种情况会触发一个`TypeError`，因为它会被识别为类型`Missing`。

诀窍是用`types`参数手动指定列类型(一个向量或者一个字典，比如`types=Dict("myFieldFoo" => Union{Missing,Int64})`)。

虽然您在这里使用了`CSV.jl`包，但是您可能会对另一个更新的包感兴趣，这个包叫做`CSVFiles.jl`(参见 [`https://github.com/queryverse/CSVFiles.jl`](https://github.com/queryverse/CSVFiles.jl) )，它允许您通过 URL 直接访问文件，或者如果文件是 gzipped 的话。

### 5.1.3 从 Excel 导入数据

使用 XLSX 软件包，您可以:

*   获取工作表名称列表:`XLSX.sheetnames(XLSX.readxlsx("myExcelFile.xlsx"))`

*   从给定的工作表中导入所有数据:`m = XLSX.readxlsx("myExcelFile.xlsx")["mySheet"][:]`

*   导入特定时间间隔的所有数据:`m = XLSX.readxlsx("myExcelFile.xlsx")["mySheet"]["B3:D6"]`或`m = XLSX.readdata("myExcelFile.xlsx", "mySheet", "B3:D6")`

*   将数据导入数据帧:`df = DataFrame(XLSX.readtable("myExcelFile.xlsx", "mySheet")...)`

最后一种方法值得解释一下。XLSX 通常以标准矩阵格式(`Array{T,2}`)导出数据。为了便于以 DataFrame 格式导出数据，提供了另一个函数`XLSX.readtable()`，它返回一个元组，其中第一个元素是列数据的向量(即`Array{Array{T,1},1}`)，元组的第二个元素是标题名称的向量。这种格式使用 splat 构造函数将元组分成两个独立的参数，允许您匹配 DataFrames 构造函数方法之一，即`DataFrame(data, headers)`。

如果您只想将工作表的一个范围导入到一个数据帧中，您可以在`readtable()`中指定您想导入的列作为第三个位置参数(如`B:D`)和从其导入的`first_row`。然而，在这一点上，你可能最好使用一个包，比如`ExcelFiles.jl`(见 [`https://github.com/queryverse/ExcelFiles.jl`](https://github.com/queryverse/ExcelFiles.jl) )，它允许直接导入到`DataFrame`格式。

### 5.1.4 从 JSON 导入数据

JavaScript Object Notation (JSON)是一种通用的开放标准语言无关数据格式，它使用人类可读的文本(由属性-值对组成)来序列化和传输数据对象。

这里我呈现`JSON2.jl`包( [`https://github.com/quinnj/JSON2.jl`](https://github.com/quinnj/JSON2.jl) )。将数据读取或导出为 JSON 格式的其他常见选项有`JSON.jl` ( [`https://github.com/JuliaIO/JSON.jl`](https://github.com/JuliaIO/JSON.jl) )和`LazyJSON.jl` ( [`https://github.com/JuliaCloud/LazyJSON.jl`](https://github.com/JuliaCloud/LazyJSON.jl) )(特别是后者仅在需要时解析 JSON 文件，而不是一次解析所有文件。).

假设您在 Julia 中有以下 JSON 数据作为字符串，表示诺丁汉森林的摘录:

```
jsonString="""      JULIA
{
     "species": "Oak",
    "latitude": 53.204199,
   "longitude": -1.072787,
       "trees": [
                 {
                    "vol": 23.54,
                    "id": 1
                 },
                 {
                    "vol": 12.25,
                     "id": 2
                 }
                ]
}
"""

```

有两种方法可以在程序中导入这些数据。第一种方法是使用`JSON2.read(jsonString, T)`，也就是将数据作为特定类型导入(必须已经定义好):

```
struct ForestStand      JULIA
    sp::String
    lat::Float64
    long::Float64
    trees::Array{Dict{String,Float64},1}
end
nottFor = JSON2.read(jsonString, ForestStand)

```

`nottFor`将成为一个`ForestStand`对象。注意，在结构和导入的 JSON 数据中定义的字段名称不需要匹配。重要的是字段的顺序和类型。

第二种方法是不为导入的对象指定任何类型，如下所示:

```
nottFor2 = JSON2.read(jsonString)

```

`nottFor2`将是一个`NamedTuple`，它的字段可以用普通的点符号访问，例如`nottFor2.trees[1].vol`。

### 5.1.5 访问网络资源(HTTP)

可以使用名为`HTTP.jl`([`https://github.com/JuliaWeb/HTTP.jl`](https://github.com/JuliaWeb/HTTP.jl))<sup>[1](#Fn1)</sup>的第三方包，通过超文本传输协议(HTTP)访问 Web 资源。`HTTP.jl`包相当完整，包括服务器端。在这里，您只是看到了访问(读取)web 资源的基本方式。

首先，您可以使用`HTTP.jl`来提供一个流并对其进行操作，就像您处理本地文件一样:

```
HTTP.open("GET", "https://julialang.org/") do io
# Note the SSLsupport      JULIA
    while !eof(io)
        println(String(readavailable(io)))
    end
end

```

或者，您可以对`get`方法使用更简洁的语法:

```
res = HTTP.get("https://julialang.org/")      JULIA
println(String(res.body))

```

如果您需要做的只是从 web 资源中读取，那么第二种方法可能更简单。第一种方法的优点是，您可以同时打开其他流，并将一个流的内容重定向到另一个流。例如，您可以将流从远程视频重定向到本地播放器。

最后，您可以一起使用`CSV.jl`和`HTTP.jl`来访问和解析远程 CSV 文件:

```
using HTTP, CSV      JULIA
resp = HTTP.request("GET", "https://cohesiondata.ec.europa.eu/resource/2q3n-nr7n.csv")
df = CSV.read(IOBuffer(String(resp.body)))

```

## 5.2 书写(输出)

### 向终端写入

要写入终端，可以使用`write(IO,T)`函数或`print(IO,T)`，使用内置的`stdout`(默认为`print`)或`stderr`流。

*   `write(stdout, "Hello World");`:将`"Hello World"`字符串打印到终端。注意结尾的分号`;`。如果没有它，`write`方法也会输出字符串的大小。

*   `print("Hello World") ;`

*   `println("Hello World")`:调用`print("Hello World")`，加一个换行符。

![../images/477168_1_En_5_Chapter/477168_1_En_5_Figb_HTML.gif](../images/477168_1_En_5_Chapter/477168_1_En_5_Figb_HTML.gif) *书写 vs 打印*

这两个函数的主要区别在于，`write`输出对象的原始字节，而`print`输出对象的文本表示。已经为所有内置类型定义了这种表示，并且为自定义类型提供了默认值(与抛出`MethodError`的`write()`相反)。该默认值可通过覆盖`print()`进行修改:

```
import Base.print # Needed when we want tooverride the print function      JULIA
struct aCustomType
    x::Int64
    y::Float64
    z::String
end
foo = aCustomType(1,2,"MyObj")

print(foo) # Output: aCustomType(1, 2.0, "MyObj")
println(foo) # Output: aCustomType(1, 2.0, "MyObj") + NewLine
write(stdout,foo) # Output: MethodError
function print(io::IO, c::aCustomType)
    print("$(c.z): x: $(c.x), y: $(c.y)")
end
print(foo) # Output: MyObj: x: 1, y: 2.0
println(foo) # Output: MyObj: x: 1, y: 2.0 + NewLine (no need to override also `println()``)

```

### 写入文件

写入文件类似于读取文件，但是在`open()`函数调用中使用了模态`w`(覆盖文件)或`a`(附加到现有数据)而不是`r`。当然，您还必须在 IO 对象上使用一个输出函数，如`write`或`print`:

```
open("afile.txt", "w") do f # "w" for writing      JULIA
  write(f, "First line\n") # \n for newline
  println(f, "Second line") # Newline automatically added by println
end

```

如果您喜欢使用模式 open > work on file > close，而不是使用`do`块，请注意，只有当 IOStream 关闭或达到某个缓冲区限制时，操作才会刷新磁盘上的文件。

### 5.2.3 导出到 CSV

导出到 CSV 文件无疑比读取 CSV 文件简单，因为您不需要考虑无数可能的 CSV 选项，并且可以控制文件的格式。

您可以简单地循环遍历数据的行/列，并手动记下数据和分隔符。或者，您可以使用`CSV.write()`函数，它会为您完成这项工作:

```
CSV.write("myOutputFile.csv", myData, delim=';', decimal='.', missingstring="NA")

```

当要导出的数据是一个数据帧时，这是现成的，但是对于一个标准矩阵(`Array{T,2}`)，您需要首先使用`Tables`包中的函数`Tables.table(Matrix)`将其转换成一个所谓的`MatrixTable`(参见 [`https://github.com/JuliaData/Tables.jl`](https://github.com/JuliaData/Tables.jl) ):

```
CSV.write("myOutputFile.csv", Tables.table(myMatrix), delim=';', decimal='.', missingstring="NA", header=["field1","field2","field3"])

```

`write`支持以下选项:`delim`、`quotechar`、`openquotechar`、`closequotechar`、`escapechar`、`missingstring`、`dateformat`、`append`、`writeheader`、`header`、`newline`、`quotestrings`、`decimal`。

### 将数据导出到 Excel

写入 Excel 遵循在`do`块中打开文件并对其进行操作的常见模式。

```
XLSX.openxlsx("myExcelFile.xlsx", mode="w") do xf# w to write (or overwrite) the file      JULIA
    sheet1 = xf[1] # One sheet is created by default
    XLSX.rename!(sheet1, "new sheet 1")
    sheet2 = XLSX.addsheet!(xf, "new sheet 2") # We can add further sheets if needed
    sheet1["A1"] = "Hello world!"
    sheet2["B2"] = [ 1 2 3 ; 4 5 6 ; 7 8 9 ] # Top-right cell to anchor the matrix
end

```

这段代码创建一个新文件(如果同名文件已经存在，它将被覆盖)。为了在不重写数据的情况下将数据“附加”到现有文件中，请使用`mode="rw"`:

```
XLSX.openxlsx("myExcelFile.xlsx", mode="rw") do xf# rw to append to an existing file instead      JULIA
    sheet1 = xf[1] # One sheet is created by default
    sheet2 = xf[2]
    sheet3 = XLSX.addsheet!(xf, "new sheet 3") # We can add further sheets if needed
    sheet1["A2"] = "Hello world again!"
    sheet3["B2"] = [ 10 20 30 ; 40 50 60 ; 70 80 90 ] # Top-right cell to anchor the matrix
end

```

单个单元格可以接受`Missing`、`Bool`、`Float64`、`Int64`、`Date`、`DateTime`、`Time`和`String`的值。

如本例的最后一行所示，数组(`Array{T,1}`和`Array{T,2}`)被自动广播到各个单元格。对于其他数据结构，如数据帧，这是不正确的。正如 XLSX 提供了一个特定的函数来帮助导入数据帧一样，它也提供了一个函数来帮助导出数据帧。这个函数是`writetable`，其中要导出的数据由一个元组`(Array{Array{T,1},1}, Array{String})`给出，即数据列和字段名称。这种格式可以很容易地从数据帧中获得:

```
XLSX.writetable("myNewExcelFile.xlsx", sheet1=( [ [1, 2, 3], [4,5,6], [7,8,9]], ["f1","f2","f3"] ), sheet2= (collect(DataFrames.eachcol(myDf)), DataFrames.names(myDf) ))

```

请注意，可以一次写入多个工作表，但是目标文件必须是新的(否则将会出现错误)。

### 5.2.5 将数据导出到 JSON

再次使用`JSON2.jl`包( [`https://github.com/quinnj/JSON2.jl`](https://github.com/quinnj/JSON2.jl) )并重用“从 JSON 导入数据”一节中定义的`nottFor`对象，只需使用`jsonString = JSON2.write(nottFor)`就可以将任何对象导出到 JSON。

返回的字符串是一个有效的 JSON 字符串，但是它被写成一行，并且用引号括起来。

要显示它的人类可读版本，可以使用由同一个包:`@pretty jsonString`提供的`@pretty`宏。

## 5.3 其他专业 IO

所有主要的数据存储格式都至少有一个 Julia 包允许你与它们交互。以下是部分列表(首先列出我的建议):

*   XML: `EzXML.jl`(见 [`https://github.com/bicycle1885/EzXML.jl`](https://github.com/bicycle1885/EzXML.jl) )和`LightXML.jl`(见 [`https://github.com/JuliaIO/LightXML.jl`](https://github.com/JuliaIO/LightXML.jl) )

*   HTML(网页抓取):`Gumbo.jl`(见 [`https://github.com/JuliaWeb/Gumbo.jl`](https://github.com/JuliaWeb/Gumbo.jl) )和`Cascadia.jl`(见 [`https://github.com/Algocircle/Cascadia.jl`](https://github.com/Algocircle/Cascadia.jl) )

*   OpenDocument 电子表格格式(ODS): `OdsIO.jl`(参见 [`https://github.com/sylvaticus/OdsIO.jl`](https://github.com/sylvaticus/OdsIO.jl) )

*   HDF5: `HDF5.jl`(参见 [`https://github.com/JuliaIO/HDF5.jl`](https://github.com/JuliaIO/HDF5.jl) )

<aside class="FootnoteSection" epub:type="footnotes">Footnotes [1](#Fn1_source)

Julia 社区正在考虑将 HTTP.jl 迁移到 Julia 标准库。

 </aside>