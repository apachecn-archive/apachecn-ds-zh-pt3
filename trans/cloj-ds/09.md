<title>Chapter 9. Time Series</title><link href="epub.css" rel="stylesheet" type="text/css"> 

# 第九章。时间序列

|   | 时间又一次流逝了 |   |
|   | - *卡洛琳·基恩，旧钟的秘密* |

在前面的几章中，我们看到了如何应用迭代算法来确定复杂方程的解。我们第一次遇到这种情况是在梯度下降中——批处理和随机——但最近我们在使用图形并行计算模型的图形社区检测中看到了这种情况。

本章是关于时间序列数据的。时间序列是由根据测量时间排列的某个量的常规观测值组成的任何数据序列。为了使本章中的许多技术起作用，我们要求连续观测之间的时间间隔都相等。对于销售数据，测量间隔时间可以是每月一次；对于降雨或股市波动，测量间隔时间可以是每天一次；对于高流量网站的点击率，测量间隔时间可以是每分钟一次。

为了能够预测时间序列的未来值，我们要求未来值在某种程度上是基于之前的值。因此这一章也是关于递归的:我们如何建立一个序列，其中每个新值都是前一个值的函数。通过将一个真实的时间序列建模为一个以这种方式产生新值的过程，我们希望能够及时模拟该序列并产生一个预测。

不过，在我们开始递归之前，我们将学习如何采用我们已经遇到过的技术——线性回归——来拟合时间序列数据的曲线。

# 关于数据

本章将利用预装在 Incanter 中的两个数据集:一个是 **Longley 数据集**，其中包含了 1947 年至 1962 年间在美国测量的七个经济变量的数据，另一个是**航空公司数据集**，其中包含了 1949 年 1 月至 1960 年 12 月的月度航空乘客总数。

### 注意

你可以从 https://github.com/clojuredatascience/ch9-time-series 下载本章的源代码。

航空数据集是我们在本章中花费大部分时间的地方，但首先让我们看看 Longley 数据集。它包含的列包括国内生产总值(GDP)、就业和失业人数、人口和武装力量的规模。这是用于分析多重共线性的经典数据集，因为许多预测值本身是相关的。这不会影响我们正在进行的分析，因为我们一次只会使用其中一个预测值。

## 加载 Longley 数据

由于 Incanter 将 Longley 数据集作为其样本数据集库的一部分，因此加载数据是一件简单的事情，只需调用`incanter.datasets/get-dataset`并将`:longley`作为唯一参数。加载后，我们将使用`incanter.core/view`查看数据集:

```
(defn ex-9-1 []

  (-> (d/get-dataset :longley)

      (i/view)))
```

数据应该是这样的:

![Loading the Longley data](graphics/7180OS_09_100.jpg)

这些数据最初是由美国国家标准与技术研究所作为统计参考数据集发布的，列说明列在他们的网站上:http://www . ITL . NIST . gov/div 898/strd/lls/data/LINKS/I-long ley . shtml。我们将考虑最后三栏 **x4** :军队的规模，**X5**:14 岁及以上的“非机构”人口，以及 **x6** :年份。

首先，让我们看看人口是如何随时间变化的:

```
(defn ex-9-2 []

  (let [data (d/get-dataset :longley)]

    (-> (c/scatter-plot (i/$ :x6 data)

                        (i/$ :x5 data)

                        :x-label "Year"

                        :y-label "Population")

        (i/view))))
```

前面的代码生成了下面的图表:

![Loading the Longley data](graphics/7180OS_09_110.jpg)

人口与年份的关系图显示了一个非常清晰的非线性关系。轻微的曲线表明人口增长率随着人口的增加而增加。

### 注意

回想一下第三章、*相关性*中的吉布拉特定律，企业的增长率与其规模成正比。在分析适用吉布拉特定律的人口时，通常会看到与上一条曲线相似的增长曲线:增长率会随着时间的推移而增加。

我们已经看到了如何用 Incanter 的线性模型拟合穿过数据的直线。也许令人惊讶的是，用`linear-model`函数拟合曲线也是可能的。

<title>Fitting curves with a linear model</title><link href="epub.css" rel="stylesheet" type="text/css"> 

# 用线性模型拟合曲线

首先，让我们提醒自己如何使用 Incanter 的`linear-model` 函数拟合直线。我们希望从数据集中提取出`x5`和`x6`列，并将它们应用到`incanter.stats/linear-model`函数中(按此顺序:`x6`，年份，是我们的预测变量)。

```
(defn ex-9-3 []

  (let [data  (d/get-dataset :longley)

        model (s/linear-model (i/$ :x5 data)

                              (i/$ :x6 data))]

    (println "R-square" (:r-square model))

    (-> (c/scatter-plot (i/$ :x6 data)

                        (i/$ :x5 data)

                        :x-label "Year"

                        :y-label "Population")

        (c/add-lines (i/$ :x6 data)

                     (:fitted model))

        (i/view))))

;; R-square 0.9879
```

前面的代码生成了下面的图表:

![Fitting curves with a linear model](graphics/7180OS_09_120.jpg)

虽然直线与数据非常接近，产生了超过 0.98 的 *R* ² ，但它没有捕捉到曲线。特别是，我们可以看到图表两端和中间的点偏离了直线。我们的直接模型有很高的偏差，并且根据年份系统地低估和高估了人口。残差图将清楚地显示误差不是等方差正态分布的。

所谓的`linear-model`函数是因为它生成的模型与其参数有线性关系。然而，也许令人惊讶的是，它能够产生非线性预测，只要我们给它提供非线性特征。例如，除了年份之外，我们可以添加年的平方作为参数。在下面的代码中，我们使用 Incanter 的`bind-columns`函数来创建这两个特性的矩阵:

```
(defn ex-9-4 []

  (let [data  (d/get-dataset :longley)

        x     (i/$ :x6 data)

        xs    (i/bind-columns x (i/sq x))

        model (s/linear-model (i/$ :x5 data) xs)]

    (println "R-square" (:r-square model))

    (-> (c/scatter-plot (i/$ :x6 data)

                        (i/$ :x5 data)

                        :x-label "Year"

                        :y-label "Population")

        (c/add-lines (i/$ :x6 data)

                     (:fitted model))

        (i/view))))

;; 0.9983
```

我们的 *R* ² 增加了，我们得到如下图表:

![Fitting curves with a linear model](graphics/7180OS_09_130.jpg)

这在看来更符合数据。我们可以通过创建一个`forecast`函数来使用我们的模型进行预测，该函数获取模型的系数并返回一个函数`x`，即年份，然后将它们乘以我们定义的特征:

```
(defn forecast [coefs]

  (fn [x]

    (first

     (i/mmult (i/trans coefs)

              (i/matrix [1.0 x (i/sq x)])))))
```

系数包括偏置项的参数，因此我们将系数乘以 *1.0* 、 *x* 和*x*2。

```
(defn ex-9-5 []

  (let [data  (d/get-dataset :longley)

        x     (i/$ :x6 data)

        xs    (i/bind-columns x (i/sq x))

        model (s/linear-model (i/$ :x5 data) xs)]

    (-> (c/scatter-plot (i/$ :x6 data)

                        (i/$ :x5 data)

                        :x-label "Year"

                        :y-label "Population")

        (c/add-function (forecast (:coefs model))

                        1947 1970)

        (i/view))))
```

接下来，我们将的函数图一直延伸到 1970 年，以便更清楚地看到拟合模型的曲线，如下所示:

![Fitting curves with a linear model](graphics/7180OS_09_140.jpg)

当然，我们的推断超出了我们数据的范围。正如在第三章、*相关性*中讨论的那样，推断得太远通常是不明智的。为了更清楚地说明原因，让我们将注意力转向 Longley 数据集中的另一列，武装部队的规模:`x6`。

我们可以用和以前一样的方法来绘制它:

```
(defn ex-9-6 []

  (let [data (d/get-dataset :longley)]

    (-> (c/scatter-plot (i/$ :x6 data)

                        (i/$ :x4 data)

                        :x-label "Year"

                        :y-label "Size of Armed Forces")

        (i/view))))
```

这会生成以下图表:

![Fitting curves with a linear model](graphics/7180OS_09_150.jpg)

这显然是一个更加复杂的系列。我们可以看到，在 1950 年至 1952 年期间，武装力量的规模急剧增加，随后略有下降。1950 年 6 月 27 日，T2，杜鲁门总统命令空军和海军在后来被称为朝鲜战争中援助韩国。

为了拟合这些数据的曲线，我们需要生成高阶多项式。首先，让我们构造一个`polynomial-forecast`函数，它将根据单个`x`和要创建的最高次多项式自动为我们创建高阶特征:

```
(defn polynomial-forecast [coefs degree]

  (fn [x]

    (first

     (i/mmult (i/trans coefs)

              (for [i (range (inc degree))]

                (i/pow x i))))))
```

例如，我们可以使用下面的代码训练一个模型一直到 *x* 11:

```
(defn ex-9-7 []

  (let [data (d/get-dataset :longley)

        degree 11

        x  (s/sweep (i/$ :x6 data))

        xs (reduce i/bind-columns

                   (for [i (range (inc degree))]

                     (i/pow x i)))

        model (s/linear-model (i/$ :x4 data) xs

                              :intercept false)]

    (println "R-square" (:r-square model))

    (-> (c/scatter-plot (i/$ 1 xs) (i/$ :x4 data)

                        :x-label "Distance from Mean (Years)"

                        :y-label "Size of Armed Forces")

        (c/add-function (polynomial-forecast (:coefs model)

                                             degree)

                        -7.5 7.5)

        (i/view))))

;; R-square 0.9755
```

前面的代码生成了下面的图表:

![Fitting curves with a linear model](graphics/7180OS_09_160.jpg)

该曲线与数据拟合得相当好，R*²超过 0.97。然而，你现在发现我们过度拟合数据应该不会感到惊讶。我们建立的模型不太可能有很强的预测能力。事实上，如果我们将图表的范围向右扩展，就像我们用`ex-9-8`来显示对未来的预测一样，我们会得到以下结果:*

*![Fitting curves with a linear model](graphics/7180OS_09_170.jpg)*

*距离上一次测量数据点仅两年半，我们的模型预测军队人数将增长 500%以上，达到 175，000 人以上。*

*<title>Time series decomposition</title><link href="epub.css" rel="stylesheet" type="text/css"> 

# 时间序列分解

我们对军事时间序列建模的一个问题是，根本没有足够的数据来产生一个产生该序列的过程的通用模型。对时间序列建模的一种常用方法是将序列分解成许多独立的部分:

*   **趋势**:数列一般是随时间增加还是减少？这种趋势是我们在人口中看到的指数曲线吗？
*   **季节性**:该系列是否以设定的时间间隔呈现周期性的上升和下降？对于月度数据，通常观察 12 个月的周期。
*   **周期**:数据集中是否存在跨越多个季节的长期周期？例如，在金融数据中，我们可能会观察到与扩张和衰退时期相对应的多年周期。

用军事数据说明问题的另一种方式是，没有足够的信息来确定是否有趋势，以及观察到的峰值是否是季节性或周期性模式的一部分。尽管数据似乎呈上升趋势，但我们可能正在密切关注一个最终将回落至起点的周期。

我们将在本章研究的数据集之一是一个经典的时间序列，观察 1949-1960 年间每月的航班乘客人数。该数据集更大，并且清楚地展示了趋势和季节成分。

## 检查航空公司数据

与 Longley 数据集一样，航空公司数据集也是 Incanter 数据集库的一部分。我们将`incanter.datasets`库加载为`d`，将`incanter.code`加载为`i`。

```
(defn ex-9-9 []

  (-> (d/get-dataset :airline-passengers)

      (i/view)))
```

前几行应该如下所示:

![Inspecting the airline data](graphics/7180OS_09_180.jpg)

当分析时间序列时，重要的是数据按时间顺序排列。该数据按年份和月份排序。所有一月份的数据后面都是所有二月份的数据，依此类推。为了更进一步，我们需要将年和月列转换成列，这样我们就可以排序了。为此，我们将再次使用`clj-time`库([https://github.com/clj-time/clj-time](https://github.com/clj-time/clj-time))。

### 可视化航空公司数据

之前在第 3 章、*关联*中解析时间时，我们能够利用时间的字符串表示是 clj-time 理解的默认格式这一事实。当然，Clj-time 不能自动推断所有的时间表示。特别成问题的是美国的 *mm/dd/yyyy* 格式和世界上大多数其他国家喜欢的 *dd/mm/yyyy* 格式之间的差异。`clj-time.format` 名称空间提供了一个`parse`函数，允许我们传递一个格式字符串，指示库应该如何解释该字符串。我们在下面的代码中包含了名称空间`format`作为`tf`，并指定我们的时间将以格式`"MMM YYYY"`表示。

### 注意

clj-time 使用的格式化程序字符串列表可在[http://www.joda.org/joda-time/key_format.html](http://www.joda.org/joda-time/key_format.html)获得。

也就是说，三个“月”字后跟四个“年”字。

```
(def time-format

  (tf/formatter "MMM YYYY"))

(defn to-time [month year]

  (tf/parse time-format (str month " " year)))
```

有了前面的函数，我们可以将 year 和 month 列解析为一个时间，按顺序对它们进行排序，并提取乘客编号:

```
(defn airline-passengers []

  (->> (d/get-dataset :airline-passengers)

       (i/add-derived-column :time [:month :year] to-time)

       (i/$order :time :asc)

       (i/$ :passengers)))
```

结果是一个数字序列，按时间升序表示乘客数量。现在让我们把它想象成一个折线图:

```
(defn timeseries-plot [series]

  (-> (c/xy-plot (range (count series)) series

               :x-label "Time"

               :y-label "Value")

      (i/view)))

(defn ex-9-10 []

  (-> (airline-passengers)

      (timeseries-plot)))
```

前面的代码生成了下面的图表:

![Visualizing the airline data](graphics/7180OS_09_190.jpg)

您可以看到数据具有明显的季节性模式(每 12 个月重复一次)、上升趋势和平缓的增长曲线。

图表右边的方差大于左边的方差，所以我们说数据呈现出某种 **异方差**。我们希望从数据集中移除方差的增加以及上升趋势。这将产生一个 **平稳**的时间序列。

## 平稳性

平稳时间序列是统计特性在时间上保持不变的时间序列。大多数统计预测方法都假设序列已经转化为平稳序列。对于平稳的时间序列来说，预测要容易得多:我们假设时间序列的统计特性在未来会和过去一样。为了从航空公司数据中去除增加的方差和增长曲线，我们可以简单地取乘客数量的对数:

```
(defn ex-9-11 []

  (-> (airline-passengers)

      (i/log)

      (timeseries-plot)))
```

这会生成以下图表:

![Stationarity](graphics/7180OS_09_200.jpg)

取对数的效果是双重的。首先，初始图表中明显的异方差已经被消除。其次，指数增长曲线已经缩减为一条直线。

这使得数据更容易处理，但我们仍然有一个趋势，在这个系列中也被称为**漂移**。为了得到一个真正平稳的时间序列，我们还需要稳定平均值。有几种方法可以做到这一点。

## 去趋势化和差异化

第一种方法是去除序列的趋势。取对数后，航空公司数据集包含一个非常强的线性趋势。我们可以用线性模型拟合该数据，然后绘制残差图:

```
(defn ex-9-12 []

  (let [data (i/log (airline-passengers))

        xs   (range (count data))

        model (s/linear-model data xs)]

    (-> (c/xy-plot xs (:residuals model)

                   :x-label "Time"

                   :y-label "Residual")

        (i/view))))
```

这会生成以下图表:

![De-trending and differencing](graphics/7180OS_09_210.jpg)

残差图显示一个序列，其平均值比原始序列稳定得多，并且上升趋势已完全消除。然而不幸的是，残差在新的平均值附近似乎不是正态分布的。特别是在图表中间似乎有一个“驼峰”。这表明我们的线性模型在航空公司数据上的表现并不理想。我们可以像本章开始时那样用曲线拟合数据，但让我们看看另一种使时间序列平稳的方法。

第二种方法是差分。如果我们从时间序列中的每个点减去前一个点的值，我们将获得一个新的时间序列(短一个数据点),它只包含连续点之间的差异。

```
(defn difference [series]

  (map - (drop 1 series) series))

(defn ex-9-13 []

  (-> (airline-passengers)

      (i/log)

      (difference)

      (timeseries-plot)))
```

我们可以在下面的图表中看到效果:

![De-trending and differencing](graphics/7180OS_09_220.jpg)

请注意，上升趋势是如何被围绕一个恒定平均值的一系列波动所取代的。平均值略高于零，对应于差异为正的倾向增加，并导致我们观察到的上升趋势。

这两种技术都旨在产生一个均值恒定的序列。在某些情况下，可能需要对序列进行多次差分，或者在去趋势后进行差分，以获得一个真正稳定均值的序列。例如，在去趋势化之后，一些漂移在序列中仍然是明显的，所以我们将在本章的剩余部分使用差异数据。

在继续讨论如何对这种时间序列进行建模以进行预测之前，让我们绕道来考虑什么是时间序列，以及我们如何将时间序列建模为递归过程。

<title>Discrete time models</title><link href="epub.css" rel="stylesheet" type="text/css"> 

# 离散时间模型

离散时间模型，比如我们目前所看到的，以规则的时间间隔将时间分成片段。为了能够预测时间片的未来值，我们假设它们依赖于过去的时间片。

### 注意

时间序列也可以根据频率而不是时间来分析。我们不会在本章讨论频域分析，但该书在[http://wiki.clojuredatascience.com](http://wiki.clojuredatascience.com)的 wiki 包含更多资源的链接。

在下文中，让 *y* [t] 表示在时间 *t* 的观察值。最简单的时间序列可能是每个时间片的值与前一个时间片的值相同。此类系列的预测值为:

![Discrete time models](graphics/7180OS_09_01.jpg)

也就是说给定 *t* 的时间 *t + 1* 的预测值等于时间 *t* 的观测值。注意，这个定义是递归的:时间 *t* 的值取决于时间 *t - 1* 的值。 *t - 1* 处的值取决于 *t - 2* 处的值，依此类推。

我们可以在 Clojure 中将这个“常量”时间序列建模为一个惰性序列，其中序列中的每个值都是一个常量值:

```
(defn constant-series [y]

  (cons y (lazy-seq (constant-series y))))

(defn ex-9-14 []

  (take 5 (constant-series 42)))

;; (42 42 42 42 42)
```

注意`constant-series`的定义是如何包含对自身的引用的。这是一个递归函数定义，它创建了一个我们可以从中消费值的无限惰性序列。

下一个时间片，在时间 *t + 1* ，观察到实际值为 *y* [t+1] 。如果这个值和我们的预测值![Discrete time models](graphics/7180OS_09_02.jpg)不同，那么我们可以计算这个差值作为我们预测的误差:

![Discrete time models](graphics/7180OS_09_03.jpg)

通过结合前面的两个方程，我们得到了时间序列的随机模型。

![Discrete time models](graphics/7180OS_09_04.jpg)

换句话说，当前时间片的值是前一个时间片的值加上一些误差。

## 随机漫步

最简单的随机过程之一是随机行走。让我们将`constant-series`扩展成一个`random-walk`过程。我们希望误差正态分布，均值为零，方差不变。让我们通过调用 Incanter 的`stats/sample-normal`函数来模拟随机噪声。

```
(defn random-walk [y]

  (let [e (s/sample-normal 1)

        y (+ y e)]

    (cons y (lazy-seq (random-walk y)))))

(defn ex-9-15 []

  (->> (random-walk 0)

       (take 50)

       (timeseries-plot)))
```

当然，您会得到不同的结果，但它应该类似于下图:

![Random walks](graphics/7180OS_09_230.jpg)

随机漫步模型在金融和计量经济学中非常常见。

### 注意

随机漫步这个术语是由卡尔·皮尔逊在 1905 年首次提出的。许多过程——从波动的股票价格到分子在气体中运动时的轨迹——都可以用简单的随机行走来建模。1973 年，普林斯顿大学的经济学家伯顿·戈登·马尔基尔在他的书《T2:漫步华尔街》中指出，股票价格也是按照随机漫步的方式演变的。

随机漫步并非完全不可预测。虽然每个点和下一个点之间的差异由随机过程控制，但该差异的方差是常数。这个意味着我们可以估计每一步幅度的置信区间。然而，由于平均值为零，我们不能有把握地说该差值相对于当前值是正还是负。

## 自回归模型

在本章中，我们已经了解了如何使用线性模型根据预测值的线性组合进行预测。在自回归模型中，我们通过使用变量过去值的线性组合来预测感兴趣的变量。

自回归模型对预测值进行回归。为了了解这在实践中是如何工作的，让我们看看下面的代码:

```
(defn ar [ys coefs sigma]

  (let [e (s/sample-normal 1 :sd sigma)

        y (apply + e (map * ys coefs))]

    (cons y (lazy-seq

             (ar (cons y ys) coefs sigma)))))
```

这与我们前几页遇到的随机游走递归定义有许多共同之处。然而，这一次，我们生成的每一个新的`y`都是之前的`ys`和`coefs`的产物。

我们可以通过调用新的`ar`函数生成一个自回归序列，传递之前的`ys`和自回归模型的系数:

```
(defn ex-9-16 []

  (->> (ar [1] [2] 0)

       (take 10)

       (timeseries-plot)))
```

这会生成以下图表:

![Autoregressive models](graphics/7180OS_09_240.jpg)

通过将初始值设为 1.0，系数设为 2.0，噪声为零，我们创建了一条指数增长曲线。系列中的每个时间步长都是 2 的幂。

自回归序列被称为*自相关*。换句话说，每个点与其前面的点线性相关。在前面的例子中，这只是前面值的两倍。系数的数量被称为自相关模型的阶，并且通常由字母 *p* 表示。因此，前面的例子是一个具有 *p=1* 的自回归过程，或者是一个 *AR(1)* 过程。

通过增加 *p* 可以生成更复杂的自回归序列。

```
(defn ex-9-17 []

  (let [init (s/sample-normal 5)]

    (->> (ar init [0 0 0 0 1] 0)

         (take 30)

         (timeseries-plot))))
```

例如，前面的代码生成一个 5 阶的自回归时间序列，或一个 *AR(5)* 序列。效应是在系列中可见的，是一个周期为 5 点的规则循环。

![Autoregressive models](graphics/7180OS_09_250.jpg)

我们可以将自回归模型与一些噪声结合起来，引入我们之前看到的随机游走的一个成分。让我们将 sigma 增加到 0.2:

```
(defn ex-9-18 []

  (let [init (s/sample-normal 5)]

    (->> (ar init [0 0 0 0 1] 0.2)

         (take 30)

         (timeseries-plot))))
```

这会生成以下图表:

![Autoregressive models](graphics/7180OS_09_260.jpg)

请注意每五个点的特征“季节性”周期是如何保留的，但也与噪声的元素相结合。虽然这是模拟数据，但这个简单的模型开始接近时间序列分析中经常出现的序列。

一个滞后的 AR 模型的一般方程由下式给出:

![Autoregressive models](graphics/7180OS_09_05.jpg)

其中 *c* 是某个常数，*ε*t 是误差，*y*t-1 是序列在前一时间步的值，*φ*1 是希腊符号*φ*表示的系数。更一般地，达到 *p* 滞后的自回归模型的方程由下式给出:

![Autoregressive models](graphics/7180OS_09_06.jpg)

由于我们的系列是稳定的，我们在代码中省略了常数项 *c* 。

## 确定 AR 模型中的自相关性

正如线性回归可以在多个独立变量之间建立(线性)相关性一样，自回归可以在不同的时间点建立变量与其自身之间的相关性。

正如在线性回归中我们试图建立预测因子和响应变量之间的相关性一样，在时间序列分析中，我们试图在一定数量的滞后上建立与时间序列及其自身的自相关。知道了自相关存在的滞后次数，我们就可以计算自回归模型的阶数。

接下来我们要研究不同滞后的序列的自相关性。例如，滞后为零意味着我们将每个点与其自身进行比较(自相关为 1.0)。滞后值为 1 意味着我们将每个点与前一个点进行比较。 **自相关函数** ( **ACF** )是一个数据集与自身之间的线性依赖，具有给定的滞后。因此，ACF 由滞后 *k* 参数化。

![Determining autocorrelation in AR models](graphics/7180OS_09_07.jpg)

Incanter 包含一个`auto-correlation`函数，它将返回给定序列和滞后的自相关。然而，我们正在定义自己的`autocorrelation`函数，该函数将返回一系列滞后的`autocorrelation`:

```
(defn autocorrelation* [series variance n k]

  (let [lag-product (->> (drop k series)

                         (map * series)

                         (i/sum))]

    (cons (/ lag-product variance n)

          (lazy-seq

           (autocorrelation* series variance n (inc k))))))

(defn autocorrelation [series]

  (autocorrelation* (s/sweep series)

                    (s/variance series)

                    (dec (count series)) 0))
```

在计算自相关之前，我们使用`incanter.stats`的`sweep`函数从序列中移除平均值。这意味着我们可以简单地将序列的值与滞后 *k* 处的值相乘，以确定它们是否有一起变化的趋势。如果他们这样做，他们的产品将是积极的；如果没有，他们的产品将是负面的。

该函数返回对应于滞后 *0 的自相关的自相关值的无限延迟序列...k* 。让我们定义一个函数，将这些值绘制成一个条形图。与`timeseries-plot`一样，该函数将接受一个有序的值序列:

```
(defn bar-plot [ys]

  (let [xs (range (count ys))]

    (-> (c/bar-chart xs ys

                     :x-label "Lag"

                     :y-label "Value")

        (i/view))))

(defn ex-9-19 []

  (let [init (s/sample-normal 5)

        coefs [0 0 0 0 1]]

    (->> (ar init coefs 0.2)

         (take 100)

         (autocorrelation)

         (take 15)

         (bar-plot))))
```

这会生成以下图表:

![Determining autocorrelation in AR models](graphics/7180OS_09_270.jpg)

每 5 个滞后的峰值与我们的 *AR(5)* 系列发电机一致。随着噪声干扰信号并降低测量的自相关性，它们随时间减少。

## 移动平均模型

AR 模型的一个假设是噪声是随机的，具有恒定的均值和方差。我们的递归 AR 函数从正态分布中采样值，以生成满足这些假设的噪声。因此，在 AR 过程中，噪声项彼此*不相关*。

然而，在某些过程中，噪声项本身并非不相关。作为这个的一个例子，考虑一个报告每天售出的烤肉数量的时间序列。我们可能每 7 天观察到一次高峰，这与顾客在周末购买烧烤的可能性增加相对应。偶尔，我们可能会观察到总销售额在几周内下降，而在其他几周内销售额相应上升。我们可能会认为这是由天气造成的，销售不佳对应于寒冷或多雨的天气，而销售良好对应于天气良好的时期。不管是什么原因，它都会在我们的数据中表现为系列平均值的明显变化。表现出这种行为的系列被称为**移动平均线** ( **马**)，模型。

由 *MA(1)* 表示的一阶移动平均模型为:

![Moving-average models](graphics/7180OS_09_08.jpg)

其中 *μ* 是系列的平均值，*ε[t]是噪声值， *θ [1]* 是模型的参数。更一般地，对于 *q* 项，MA 模型由下式给出:*

![Moving-average models](graphics/7180OS_09_09.jpg)

因此，MA 模型在概念上是序列的当前值相对于当前和先前(未观察到的)白噪声误差项或随机冲击的线性回归。假设每个点的误差项是相互独立的，并且来自相同的(通常是正态)分布，具有零均值和恒定方差。

在 MA 模型中，我们假设噪声值本身是自相关的。我们可以这样建模:

```
(defn ma [es coefs sigma]

  (let [e (s/sample-normal 1 :sd sigma)

        y (apply + e (map * es coefs))]

    (cons y (lazy-seq

             (ma (cons e es) coefs sigma)))))
```

这里，`es`是以前的误差，`coefs`是 MA 模型的参数，`sigma`是误差的标准差。

请注意该函数与之前定义的`ar`函数有何不同。我们保留先前的 *es* 的序列，而不是保留先前的 *ys* 的序列。让我们看看 MA 模型生成了什么样的序列:

```
(defn ex-9-20 []

  (let [init (s/sample-normal 5)

        coefs [0 0 0 0 1]]

    (->> (ma init coefs 0.5)

         (take 100)

         (timeseries-plot))))
```

这将生成类似于下图的图形:

![Moving-average models](graphics/7180OS_09_280.jpg)

你可以看到均线图缺少 AR 模型的明显重复。然而，从更长的一系列角度来看，你可以看到它是如何将*漂移*重新引入模型的，因为一次随机冲击的回响以一种新的临时方式延续。

## 确定 MA 模型中的自相关性

你可能想知道自相关图是否有助于识别 MA 过程。让我们现在画出来。MA 模型可能更难识别，所以我们将在绘制自相关之前生成更多的点。

```
(defn ex-9-21 []

  (let [init (s/sample-normal 5)

        coefs [0 0 0 0 1]]

    (->> (ma init coefs 0.2)

         (take 5000)

         (autocorrelation)

         (take 15)

         (bar-plot))))
```

这会生成以下图表:

![Determining autocorrelation in MA models](graphics/7180OS_09_290.jpg)

您可以在前面的图表中看到，它清楚地显示了 MA 过程的顺序，在滞后 5 处有一个明显的峰值。不过请注意，与自回归过程不同，每 5 个滞后期没有重复出现的峰值。MA 过程的一个特点是，由于该过程将漂移引入均值，其他滞后的自相关性大大降低。

## 组合 AR 和 MA 模型

本章到目前为止，我们一直在考虑的 AR 和 MA 模型是两种不同但密切相关的生成自相关时间序列的方法。但是，它们并不相互排斥，当试图对实时序列建模时，您经常会遇到序列看起来是两者混合的情况。

![Combining the AR and MA models](graphics/7180OS_09_10.jpg)

我们可以将 AR 和 MA 过程合并到一个 ARMA 模型中，使用两组系数:自回归模型的系数和移动平均模型的系数。每个模型的系数的数量不需要相同，按照惯例，AR 模型的阶由 *p* 标识，MA 模型的阶由 *q* 标识。

```
(defn arma [ys es ps qs sigma]

  (let [e  (s/sample-normal 1 :sd sigma)

        ar (apply + (map * ys ps))

        ma (apply + (map * es qs))

        y  (+ ar ma e)]

    (cons y (lazy-seq

                (arma (cons y ys)

                      (cons e es)

                      ps qs sigma)))))
```

让我们绘制一系列更长的点，看看会出现什么样的结构:

```
(defn ex-9-22 []

  (let [ys (s/sample-normal 10 :sd 1.0)

        es (s/sample-normal 10 :sd 0.2)

        ps [0 0 0 0.3 0.5]

        qs [0.2 0.8]]

    (->> (arma ys es ps qs 0.2)

         (take 500)

         (timeseries-plot))))
```

请注意我们是如何为模型的 AR 和 MA 部分指定不同数量的参数的:AR 模型有 5 个参数，MA 模型有 2 个参数。这被称为 *ARMA(5，2)* 模型。

![Combining the AR and MA models](graphics/7180OS_09_300.jpg)

早期的 ARMA 模型在较长的一系列点上的图允许 MA 项的影响变得可见。在这个尺度上，我们看不到 AR 分量的影响，所以让我们像以前一样通过自相关图来运行这个系列:

```
(defn ex-9-23 []

  (let [ys (s/sample-normal 10 :sd 1.0)

        es (s/sample-normal 10 :sd 0.2)

        ps [0 0 0 0.3 0.5]

        qs [0.2 0.8]]

    (->> (arma ys es ps qs 0.2)

         (take 500)

         (autocorrelation)

         (take 15)

         (bar-plot))))
```

您应该会看到类似于下图的图表:

![Combining the AR and MA models](graphics/7180OS_09_310.jpg)

ACF 图远没有使序列的顺序变得更清晰，随着更多的数据和序列中的 ar 和 MA 分量，ACF 图并不是非常有用，并且与我们到目前为止看到的非常清晰的自相关图非常不同。自相关慢慢衰减到零，使得不可能确定 AR 和 MA 过程的顺序，即使我们已经向它提供了大量的数据。

这是因为模型的 MA 部分压倒了模型的 AR 部分。我们无法确定数据中的循环模式，因为它隐藏在移动平均线之后，移动平均线使彼此接近的所有点看起来相关。解决这个问题的最好方法是绘制偏相关图。

## 计算偏自相关

**偏自相关函数** ( **PACF** )旨在解决在混合 ARMA 模型中发现循环成分的问题。它被定义为给定所有中间观测值的*y*t 和*y*t+k 之间的相关系数。换句话说，滞后 1 到 *k-1* 尚未考虑滞后 *k* 处的自相关。

一阶滞后 1 部分自相关被定义为等于一阶自相关。二阶滞后 2 偏自相关等于:

![Calculating partial autocorrelation](graphics/7180OS_09_11.jpg)

这是相隔两个时间段的值之间的， *y* [t] 和*y*t-2，以*y*t-1 的知识为条件。在平稳的时间序列中，分母中的两个方差是相等的。

第三阶，滞后 3 偏自相关等于:

![Calculating partial autocorrelation](graphics/7180OS_09_12.jpg)

对于任何滞后，以此类推。

### 自协方差

偏相关方程要求我们计算数据在时的协方差。这被称为**自协方差**。我们已经在前面的章节中了解了如何测量两个数据系列之间的协方差，即两个或多个属性一起变化的趋势。该函数与我们在本章前面定义的自相关函数非常相似，它计算从零开始的一系列滞后的自协方差:

```
(defn autocovariance* [series n k]

  (let [lag-product (->> (drop k series)

                         (map * series)

                         (i/sum))]

    (cons (/ lag-product n)

          (lazy-seq

           (autocovariance* series n (inc k))))))

(defn autocovariance [series]

  (autocovariance* (s/sweep series) (count series) 0))
```

和以前一样，返回值将是一个延迟序列，所以我们将确保只取我们需要的值。

### PACF 与德宾-莱文森递归

因为需要考虑之前解释的变化，所以计算部分自相关比计算自相关要复杂得多。Durbin-Levinson 算法提供了一种递归计算的方法。

### 注意

Durbin-Levinson 递归，或简称为 Levinson 递归，是一种用于计算涉及对角线上具有常数值的矩阵(称为**托普利兹矩阵**)的方程的解的方法。更多信息可在[https://en.wikipedia.org/wiki/Levinson_recursion](https://en.wikipedia.org/wiki/Levinson_recursion)获得。

Levinson 递归的实现如下所示。数学已经超出了本书的范围，但是递归函数的一般形式你现在应该很熟悉了。在每次迭代中，我们用先前的部分自相关和自协方差的函数来计算部分自相关。

```
(defn pac* [pacs sigma prev next]

  (let [acv (first next)

        sum (i/sum (i/mult pacs (reverse prev)))

        pac (/ (- acv sum) sigma)]

    (cons pac

          (lazy-seq

           (pac* (->> (i/mult pacs pac)

                      (reverse)

                      (i/minus pacs)

                      (cons pac))

                 (* (- 1 (i/pow pac 2)) sigma)

                 (cons acv prev)

                 (rest next))))))

(defn partial-autocorrelation [series]

  (let [acvs (autocovariance series)

        acv1 (first  acvs)

        acv2 (second acvs)

        pac  (/ acv2 acv1)]

    (concat [1.0 pac]

            (pac* (vector pac)

                  (- acv1 (* pac acv2))

                  (vector acv2)

                  (drop 2 acvs)))))
```

和以前一样，这个函数将创建一个无限的部分自相关的惰性序列，所以我们必须只从它那里获取我们真正想要的数字。

### 绘制偏自相关图

既然我们已经实现了一个函数来计算一个时间序列的部分自相关，让我们来绘制它们。我们将使用与之前相同的 ARMA 系数，这样我们就可以比较不同之处。

```
(defn ex-9-24 []

  (let [ys (s/sample-normal 10 :sd 1.0)

        es (s/sample-normal 10 :sd 0.2)

        ps   [0 0 0 0.3 0.5]

        qs   [0.2 0.8]]

    (->> (arma ys es ps qs 0.2)

         (take 500)

         (partial-autocorrelation)

         (take 15)

         (bar-plot))))
```

这将生成一个如下所示的条形图:

![Plotting partial autocorrelation](graphics/7180OS_09_320.jpg)

幸运的是，这与我们之前创建的 ACF 图非常不同。在滞后 1 和 2 处有很高的部分自相关。这表明一个 *MA(2)* 过程在起作用。然后，在滞后 5 之前存在低偏自相关。这表明也有一个 T2 AR(5)T3 模型在起作用。

### 用 ACF 和 PACF 确定 ARMA 模型的阶次

ACF 和 PACF 图之间的差异有助于为时间序列选择最合适的模型。下表描述了理想化 AR 和 MA 系列的 ACF 和 PACF 图的外观。

| 

模型

 | 

advancedcommunicationsfunction 高级通信功能

 | 

PACF

 |
| --- | --- | --- |
| *AR(p)* | 逐渐衰减 | 在 *p* 滞后后切断 |
| *马(q)* | 在 *q* 滞后后切断 | 逐渐衰减 |
| *ARMA(p，q)* | 逐渐衰减 | 逐渐衰减 |

然而，我们经常没有数据来证实这些想法。给定一个真实的时间序列，尤其是一个没有大量点的时间序列，并不总是很明显哪一个是最合适的模型。最佳做法通常是选择能够描述您的数据的最简单的模型(阶数最低的模型)。

![Determining ARMA model order with ACF and PACF](graphics/7180OS_09_330.jpg)

上图显示了理想化的 *AR(1)* 序列的样本 ACF 和 PACF 图。接下来是理想化的 *MA(1)* 系列的样本 ACF 和 PACF 图。

![Determining ARMA model order with ACF and PACF](graphics/7180OS_09_340.jpg)

图形上的虚线表示重要性的界限。一般来说，我们无法建立一个模型来完美地捕捉时间序列中的所有自相关，而显著性阈值有助于我们区分最重要的部分。确定具有 5%的 *α* 的显著性阈值的简单公式为:

![Determining ARMA model order with ACF and PACF](graphics/7180OS_09_13.jpg)

这里， *n* 是时间序列中的点数。如果 ACF 和 PACF 中的所有点都接近于零，则数据基本上是随机的。

## 航空公司数据的 ACF 和 PACF

让我们将返回到我们之前开始考虑的航空公司数据，并绘制前 25 个滞后的数据的 ACF。

```
(defn ex-9-25 []

  (->> (airline-passengers)

       (difference)

       (autocorrelation)

       (take 25)

       (bar-plot)))
```

此代码生成以下图表:

![ACF and PACF of airline data](graphics/7180OS_09_350.jpg)

你可以看到数据中有规律的波峰和波谷。第一个峰值在滞后 12；第二次是在第 24 天。由于数据是每月的，这些峰值对应于一个年度、季度周期。由于我们的时间序列中有 144 个点，重要性的阈值大约是![ACF and PACF of airline data](graphics/7180OS_09_14.jpg)或 0.17。

接下来，让我们看看航空公司数据的偏相关图:

```
(defn ex-9-26 []

  (->> (airline-passengers)

       (difference)

       (partial-autocorrelation)

       (take 25)

       (bar-plot)))
```

此代码生成以下图表:

![ACF and PACF of airline data](graphics/7180OS_09_360.jpg)

部分自相关图在滞后 12 时也有一个峰值。与自相关图不同，它在滞后 24 处没有峰值，因为在滞后 12 处已经考虑了周期性自相关。

虽然这似乎表明 AR(12)模型将是合适的，但这将产生大量要学习的系数，尤其是在相对少量的数据上。由于周期循环是季节性的，我们应该用第二个差分阶段来消除它。

## 用差异消除季节性

我们已经对数据进行了一次差分，这意味着我们的模型被称为一个自回归综合移动平均 ( **ARIMA** )模型。在给定参数 *d* 的情况下，差异的级别是，因此完整的模型阶数可以指定为 *ARIMA(p，d，q)* 。

我们可以对数据进行第二次差分，以消除数据中强烈的季节性。让我们接下来这样做:

```
(defn ex-9-27 []

  (->> (airline-passengers)

       (difference)

       (difference 12)

       (autocorrelation)

       (take 15)

       (bar-plot)))
```

首先，我们绘制自相关图:

![Removing seasonality with differencing](graphics/7180OS_09_362.jpg)

接下来，偏自相关:

```
(defn ex-9-28 []

  (->> (airline-passengers)

       (difference)

       (difference 12)

       (partial-autocorrelation)

       (take 15)

       (bar-plot)))
```

这会生成以下图表:

![Removing seasonality with differencing](graphics/7180OS_09_364.jpg)

图表中的大部分意义是由强烈的季节性周期造成的。我们在两个图表的滞后 1 处都有负自相关，在 ACF 的滞后 9 处几乎没有显著的自相关。一般的经验法则是，正自相关最好通过向模型添加一个 *AR* 项来处理，而负自相关通常最好通过向模型添加一个 *MA* 项来处理。

根据前面的图表，合理模型似乎是一个 *MA(1)* 模型。对于这种情况，这可能是一个足够好的模型，但让我们以此为契机，通过尝试捕捉 *AR(9)* 自相关来演示如何将大量参数拟合到一个模型中。

我们将考虑成本函数的替代方法，即可能性，它衡量给定模型与数据的拟合程度。模型拟合得越好，可能性就越大。因此，我们将希望最大化可能性，这一目标也被称为**最大可能性估计**。

<title>Maximum likelihood estimation</title><link href="epub.css" rel="stylesheet" type="text/css"> 

# 最大似然估计

在本书中的几个场合，我们已经用一个最小化的成本函数表达了优化问题。例如，在[第 4 章](ch04.html "Chapter 4. Classification")、*分类*中，我们在构建逻辑回归分类器时使用了 Incanter 来最小化逻辑成本函数，而在[第 5 章](ch05.html "Chapter 5. Big Data")、*大数据*中，我们在执行批处理和随机梯度下降时使用梯度下降来最小化最小二乘成本函数。

优化也可以表达为利益最大化，有时从这些角度考虑更自然。最大似然估计旨在通过最大化似然函数找到模型的最佳参数。

假设给定模型参数 *β* ，观察值的概率 *x* 写为:

![Maximum likelihood estimation](graphics/7180OS_09_15.jpg)

那么，可能性可以表示为:

![Maximum likelihood estimation](graphics/7180OS_09_16.jpg)

在给定数据的情况下，可能性是参数的*概率的度量。最大似然估计的目的是找到使观测数据最有可能的参数值。*

## 计算可能性

在计算时间序列的可能性之前，我们将通过一个简单的例子来说明这个过程。假设我们投掷硬币 100 次，观察到 56 个正面， *h* ，44 个反面， *t* 。与其假设我们有一枚 *P(h)=0.5* 的公平硬币(因此略微不相等的总数是随机变化的结果)，不如我们可以问观察到的值是否与 0.5 有显著差异。我们可以通过询问 *P(h)* 的什么值使观察到的数据最有可能。

```
(defn ex-9-29 []

  (let [data 56

        f (fn [p]

            (s/pdf-binomial data :size 100 :prob p))]

    (-> (c/function-plot f 0.3 0.8

                         :x-label "P"

                         :y-label "Likelihood")

        (i/view))))
```

在前面的代码中，我们使用二项式分布来模拟掷硬币的顺序(回想一下[第 4 章](ch04.html "Chapter 4. Classification")、*分类*，二项式分布用于模拟二进制结果预期出现的次数)。关键的一点是数据是固定的，我们在给二项分布不同的参数的情况下，绘制观察到数据的不同概率。下图显示了可能性表面:

![Calculating the likelihood](graphics/7180OS_09_366.jpg)

正如我们所料，二项式分布最可能的参数是 *p=0.56* 。这个人为的例子可以更容易地用手工计算，但是最大似然估计的原理能够处理更复杂的模型。

事实上，我们的 ARMA 模型就是这样一个复杂的模型。计算时间序列参数可能性的数学方法超出了本书的范围。我们将利用 Clojure 库继承([https://github.com/henrygarner/succession](https://github.com/henrygarner/succession))来计算我们的时间序列的可能性。

通常情况下，我们使用对数似然法，而不是似然法。这只是为了数学上的方便，因为对数似然性:

![Calculating the likelihood](graphics/7180OS_09_17.jpg)

可以重写为:

![Calculating the likelihood](graphics/7180OS_09_18.jpg)

这里， *k* 是模型参数的数。取大量参数的和比取乘积在计算上更方便，因此第二个公式通常是首选。让我们通过对一个简单的 *AR(2)* 时间序列绘制不同参数的对数似然来感受一下似然函数在一些测试数据上的表现。

```
(defn ex-9-30 []

  (let [init  (s/sample-normal 2)

        coefs [0 0.5]

        data  (take 100 (ar init coefs 0.2))

        f     (fn [coef]

                (log-likelihood [0 coef] 2 0 data))]

    (-> (c/function-plot f -1 1

                         :x-label "Coefficient"

                         :y-label "Log-Likelihood")

        (i/view))))
```

前面的代码生成了下面的图表:

![Calculating the likelihood](graphics/7180OS_09_370.jpg)

在给定数据的情况下，曲线的峰值对应于参数的最佳估计值。请注意前面图中的峰值比 0.5 稍高:我们添加到模型中的噪声意味着最佳估计值不是 0.5。

## 估计最大似然

我们的 ARMA 模型的参数数量很大，因此为了确定最大可能性，我们将使用一种在高维空间中表现良好的优化方法。方法被称为**内尔德-米德**或**单纯形**方法。在 *n* 维空间中，单形是由 *n+1* 个顶点组成的*多面体*。

### 注意

多面体是一种具有平坦侧面的几何物体，可以存在于任意数量的维度中。二维多边形是二维多面体，三维多面体是三维多面体。

单纯形优化的优点是，它不需要为了下降(或上升)到更优的位置而计算每个点的梯度。Nelder-Mead 方法外推在单纯形上每个测试点测量的目标函数的行为。最差点被替换为通过反射其余点的质心而创建的点。如果新点比当前最佳点更好，那么我们沿着这条线指数地延伸单纯形。如果新的点不比之前好多少，我们可能会跨过一个山谷，所以我们将单纯形收缩到一个可能更好的点。

下图显示了用三角形表示的单纯形如何反射和收缩以找到最佳参数的示例。

![Estimating the maximum likelihood](graphics/7180OS_09_380.jpg)

单纯形总是表示为顶点数比维数多一的形状。如前图所示，二维优化的单纯形由三角形表示。对于任意的 *n* 维空间，单纯形将被表示为一个由 *n+1* 个顶点组成的多边形。

### 注意

单纯形法也被称为阿米巴变形虫法，因为它似乎是朝着一个更理想的位置爬行。

优化的单纯形法没有在 Incanter 中实现，但是它在 Apache Commons 数学库(【http://commons.apache.org/proper/commons-math/】T2)中可用。要使用它，我们需要将我们的目标函数，对数似然性，包装成本库能够理解的表示形式。

### 使用 Apache Commons Math 优化 Nelder-Mead

Apache Commons Math 是一个大型复杂的库。我们在这里只能介绍最基本的要素。下一个例子只是为了说明如何将 Clojure 代码与库提供的 Java 接口集成在一起。

### 注意

关于 Apache Commons Math 的广泛优化功能的概述，请访问[http://Commons . Apache . org/proper/Commons-Math/user guide/optimization . html](http://commons.apache.org/proper/commons-math/userguide/optimization.html)。

Apache Commons 数学库希望我们能提供一个`ObjectiveFunction`进行优化。接下来，我们通过具体化一个`MultivariateFunction`来创建一个，因为我们的目标函数需要提供多个参数。我们的回答将是一个单一的值:对数可能性。

```
(defn objective-function [f]

  (ObjectiveFunction. (reify MultivariateFunction

                        (value [_ v]

                          (f (vec v))))))
```

前面的代码将返回一个任意函数`f`的`ObjectiveFunction`表示。一个`MultivariateFunction`期望接收一个参数向量`v`，我们将它直接传递给我们的`f`。

有了这些，我们使用一些 Java 互操作来调用带有一些合理默认值的`SimplexOptimizer`上的`optimize`。我们的参数的`InitialGuess`只是一个零数组。`NelderMeadSimplex`必须用每个维度的默认步长进行初始化，步长可以是除零以外的任何值。我们为每个参数选择 0.2 的值。

```
(defn arma-model [p q ys]

  (let [m (+ p q)

        f (fn [params]

            (sc/log-likelihood params p q ys))

        optimal (.optimize (SimplexOptimizer. 1e-10 1e-10)

                           (into-array

                            OptimizationData

                            [(MaxEval. 100000)

                             (objective-function f)

                             GoalType/MAXIMIZE

                             (->> (repeat 0.0)

                                  (take m)

                                  (double-array)

                                  (InitialGuess.))

                             (->> (repeat 0.1)

                                  (take m)

                                  (double-array)

                                  (NelderMeadSimplex.))]))

        point (-> optimal .getPoint vec)

        value (-> optimal .getValue)]

    {:ar (take p point)

     :ma (drop p point)

     :ll value}))

(defn ex-9-31 []

  (->> (airline-passengers)

       (i/log)

       (difference)

       (difference 12)

       (arma-model 9 1)))
```

我们的模型是一个有许多参数的大模型，所以优化需要一段时间才能收敛。如果您运行前面的例子中的,您最终应该会看到类似于下面所示的返回参数:

```
;; {:ar (-0.23769808471685377 -0.012617164166298971 ...),

;;  :ma (-0.14754455658280236),

;;  :ll 232.97813750669314}
```

这些是我们模型的最大似然估计。响应中还包括具有最大似然参数的模型的对数似然。

## 用赤池信息准则识别更好的模型

评估多个模型时，最佳模型可能是具有最大似然估计值的模型。毕竟，估计已经确定了模型是生成观察数据的最佳候选。然而，最大似然估计没有考虑模型的复杂性，一般来说，更简单的模型是首选。回想一下本章的开头，我们的高阶多项式模型有很高的 *R* 2，但是没有提供预测能力。

**Akaike 信息标准** ( **AIC** )是一种比较模型的方法，该方法奖励的拟合优度，由似然函数评估，但包括作为参数数量函数的惩罚。这种惩罚阻止了过度拟合，因为增加模型的参数数量几乎总是会提高拟合优度。

AIC 可以通过以下公式计算:

![Identifying better models with Akaike Information Criterion](graphics/7180OS_09_19.jpg)

这里， *k* 是模型参数的数量，而 *L* 是似然函数。我们可以在 Clojure 中使用参数 count*p*和 *q* 通过以下方式计算 AIC。

```
(defn aic [coefs p q ys]

  (- (* 2 (+ p q 1))

     (* 2 (log-likelihood coefs p q ys))))
```

如果我们要生产多种型号并选择最佳型号，我们会选择 AIC 最低的型号。

<title>Time series forecasting</title><link href="epub.css" rel="stylesheet" type="text/css"> 

# 时间序列预测

定义了参数估计值后，我们终于可以使用我们的模型进行预测了。我们实际上已经编写了完成这项工作所需的大部分代码:我们有一个`arma`函数，它能够基于一些种子数据和模型参数 *p* 和 *q* 生成一个自回归移动平均序列。种子数据将是我们从航空公司数据中测量的 *y* 的值，并且 *p* 和 *q* 的值将是我们使用 Nelder-Mead 方法计算的参数。

让我们将这些数字代入 ARMA 模型，生成一系列对 *y* 的预测:

```
(defn ex-9-32 []

  (let [data (i/log (airline-passengers))

        diff-1  (difference 1 data)

        diff-12 (difference 12 diff-1)

        forecast (->> (arma (take 9 (reverse diff-12))

                       []

                       (:ar params)

                       (:ma params) 0)

                      (take 100)

                      (undifference 12 diff-1)

                      (undifference 1 data))]

    (->> (concat data forecast)

         (i/exp)

         (timeseries-plot))))
```

前面的代码生成了下面的图表:

![Time series forecasting](graphics/7180OS_09_390.jpg)

直到时间片 144 的线是原始序列。这一点之后的一行是我们的预测系列。预测看起来很像我们可能希望的那样:指数增长趋势继续，正如有规律的季节性高峰和低谷模式一样。

事实上，预测几乎太有规律了。与点 1 到点 144 的序列不同，我们的预测不包含噪声。让我们添加一些噪音，使我们的预测更加现实。为了确定多少噪音是合理的，我们可以看看在我们过去的预测中有什么误差。为了避免我们的误差复合，我们应该提前一个时间步进行预测，并观察预测值和实际值之间的差异。

让我们运行 sigma 为 0.02 的 ARMA 函数:

```
(defn ex-9-33 []

  (let [data (i/log (airline-passengers))

        diff-1  (difference 1 data)

        diff-12 (difference 12 diff-1)

        forecast (->> (arma (take 9 (reverse diff-12))

                       []

                       (:ar params)

                       (:ma params) 0.02)

                      (take 10)

                      (undifference 12 diff-1)

                      (undifference 1 data))]

    (->> (concat data forecast)

         (i/exp)

         (timeseries-plot))))
```

上述代码可能会生成如下所示的图表:

![Time series forecasting](graphics/7180OS_09_395.jpg)

现在我们对预测的波动性有了一个的感觉。通过多次运行模拟，我们可以了解各种不同的可能结果。如果我们能够确定我们预测的置信区间，那将是非常有用的:包括噪声在内的所有未来序列的上下期望值。

## 用蒙特卡罗模拟进行预测

虽然分析方法确实存在用于计算时间序列的预期未来值，以及置信区间，但我们将使用最后一节通过模拟得出这些值。通过研究许多预测之间的差异，我们可以得出模型预测的置信区间。

例如，如果我们运行大量的模拟，我们可以根据值在 95%的时间内下降的范围来计算未来预测的 95%置信区间。这是蒙特卡罗模拟的本质，蒙特卡罗模拟是一种常用的统计工具，用于分析难以解决的问题。

### 注意

蒙特卡洛方法是在曼哈顿计划期间系统地开发和使用的，曼哈顿计划是美国在第二次世界大战期间开发核武器的努力。约翰·冯·诺依曼和斯坦尼斯劳·乌拉姆建议用它来研究中子穿过辐射屏蔽的特性，并以摩纳哥的蒙特卡洛赌场命名这种方法。

我们已经为时间序列预测的蒙特卡洛模拟奠定了所有的基础。我们只需要运行模拟数百次并收集结果。在下面的代码中，我们运行了 1，000 次模拟，并收集了每个未来时间段所有预测的平均值和标准差。通过创建两个新系列(上限加上标准偏差乘以 1.96，下限减去标准偏差乘以 1.96)，我们能够可视化该系列未来值的 95%置信区间。

```
(defn ex-9-34 []

  (let [data (difference (i/log (airline-passengers)))

        init (take 12 (reverse data))

        forecasts (for [n (range 1000)]

                    (take 20

                          (arma init [0.0028 0.0028]

                                (:ar params1)

                                (:ma params1)

                                0.0449)))

        forecast-mean (map s/mean (i/trans forecasts))

        forecast-sd (-> (map s/sd (i/trans forecasts))

                        (i/div 2)

                        (i/mult 1.96))

        upper (->> (map + forecast-mean forecast-sd)

                   (concat data)

                   (undifference 0)

                   (i/exp))

        lower (->> (map - forecast-mean forecast-sd)

                   (concat data)

                   (undifference 0)

                   (i/exp))

        n (count upper)]

    (-> (c/xy-plot   (range n) upper

                     :x-label "Time"

                     :y-label "Value"

                     :series-label "Upper Bound"

                     :legend true)

        (c/add-lines (range n) lower

                     :series-label "Lower Bound")

        (i/view))))
```

这个产生了下面的图:

![Forecasting with Monte Carlo simulation](graphics/7180OS_09_400.jpg)

上限和下限为我们未来的时间序列预测提供了置信区间。

<title>Summary</title><link href="epub.css" rel="stylesheet" type="text/css"> 

# 总结

在这一章中，我们已经考虑了分析离散时间序列的任务:在固定的时间间隔内进行的连续观察。我们已经看到了如何通过将这样的序列分解成一组组件来简化建模的挑战:趋势组件、季节组件和周期组件。

我们已经看到 ARMA 模型如何将一个序列进一步分解为自回归和移动平均分量，每个分量都以某种方式由序列的过去值决定。这种级数的概念本质上是递归的，我们已经看到了 Clojure 定义递归函数和惰性序列的自然能力是如何帮助算法生成这种级数的。通过将序列的每个值确定为先前值的函数，我们实现了一个递归 ARMA 生成器，它能够模拟测量的序列并及时预测它。

我们还学习了期望最大化:一种在给定数据的情况下，将优化问题的解决方案重新构建为产生最大可能性的方法。我们还看到了 Apache Commons 数学库如何使用 Nelder-Mead 方法来估计最大似然参数。最后，我们看到了如何通过向前播放时间序列来完成预测，以及如何使用蒙特卡罗模拟来估计序列的未来误差。

在最后一章，我们将把注意力从数据分析转向数据可视化。在某些方面，数据科学家面临的最大挑战是沟通，我们将看到 Clojure 如何支持我们以最有效的方式呈现数据。*