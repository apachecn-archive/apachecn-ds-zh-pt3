

# 一、统计学

|   | 投票的人什么也决定不了。计票的人决定一切。” |   |
|   | ——*约瑟夫·斯大林* |

在 *Clojure for Data Science* 接下来的十章中，我们将尝试在数据科学领域发现一条大致线性的道路。事实上，我们会发现这条道路并不完全是线性的，细心的读者应该会注意到许多重复出现的主题。

描述统计学关注的是对数字序列的总结，在某种程度上，它们会出现在本书的每一章中。在这一章中，我们将通过在 Clojure 中实现计算数字序列的平均值、中值、方差和标准差的函数来为即将到来的事情打下基础。在这样做的同时，我们将试图消除对解释数学公式的恐惧。

一旦我们有一个以上的数字要分析，询问这些数字是如何分布的就变得有意义了。你可能已经听过“长尾”和“80/20 法则”这样的表达。它们关注的是数字在一个范围内的分布。在这一章中，我们将展示分布的价值，并介绍其中最有用的:正态分布。

可视化极大地帮助了对分布的研究，为此我们将使用 Clojure 库咒语。我们将展示如何使用 Incanter 来加载、转换和可视化真实数据。我们将比较两次全国大选的结果——2010 年英国大选和 2011 年俄罗斯总统大选——看看即使是最基本的分析也能提供潜在欺诈活动的证据。

# 下载示例代码

这本书的所有样本代码都可以在位于 http://www.packtpub.com/support[的 Packt Publishing](http://www.packtpub.com/support)网站或者位于 http://github.com/clojuredatascience[的 GitHub](http://github.com/clojuredatascience)上获得。每章的示例代码都可以在它自己的库中找到。

### 注意

第一章[，*统计*的示例代码可以从](ch01.html "Chapter 1. Statistics")[https://github.com/clojuredatascience/ch1-statistics](https://github.com/clojuredatascience/ch1-statistics)下载。

在所有章节中定期提供可执行的例子，或者演示刚刚解释的代码的效果，或者演示已经介绍的统计原理。所有示例函数名称都以`ex-`开头，并在每章中按顺序编号。于是，[第一章](ch01.html "Chapter 1. Statistics")、*统计*的第一个可运行的例子被命名为`ex-1-1`，第二个被命名为`ex-1-2`，以此类推。



# 运行示例

每个例子都是在`cljds.ch1.examples`名称空间中的一个函数，它可以以两种方式运行——要么从 **REPL** 运行，要么在命令行上用 **Leiningen** 运行。如果您想在 REPL 运行这些示例，您可以执行:

```

lein repl

```

在命令行上。默认情况下，REPL 将在`examples`命名空间中打开。或者，要运行特定编号的示例，您可以执行:

```

lein run –-example 1.1

```

或者传递等效的单个字母:

```

lein run –e 1.1

```

在本书中，我们只假设读者熟悉基本的命令行。运行 Leiningen 和 shell 脚本的能力就是所需要的。

### Tip

如果你在某一点上卡住了，请参考这本书在 http://wiki.clojuredatascience.com T2 的维基。wiki 将提供已知问题的故障排除提示，包括在各种平台上运行示例的建议。

事实上，shell 脚本仅用于从远程位置自动获取数据。该书的 wiki 还将为那些不希望或不能执行 shell 脚本的人提供替代说明。



# 下载数据

维也纳医科大学的复杂系统研究小组已经提供了本章的数据集。我们将进行的分析密切反映了他们的研究，以确定世界各国国家选举中系统性选举欺诈的信号。

### 注意

有关这项研究的更多信息，以及下载其他数据集的链接，请访问该书的维基或研究小组的网站，网址为[http://www . complex-systems . meduniwien . AC . at/elections/election . html](http://www.complex-systems.meduniwien.ac.at/elections/election.html)。

在本书中，我们将会用到大量的数据集。在可能的情况下，我们在示例代码中包含了数据。由于数据的大小或许可的限制，这是不可能的，我们已经包含了一个脚本来下载数据。

[第 1 章](ch01.html "Chapter 1. Statistics")，*统计*正是这样一章。如果您已经克隆了这一章的代码，并打算按照示例进行操作，现在可以通过在命令行上从项目目录中执行以下命令来下载数据:

```

script/download-data.sh

```

该脚本将样本数据下载并解压缩到项目的数据目录中。

### Tip

如果你在运行下载脚本时有任何困难，或者想要遵循手动指导，请访问该书的维基，网址为[http://wiki.clojuredatascience.com](http://wiki.clojuredatascience.com)寻求帮助。

我们将在下一节开始研究数据。



# 检查数据

在这一章以及本书的许多其他章节中，我们将使用咒语库(【http://incanter.org/】T2)来加载、操作和显示数据。

Incanter 是 Clojure 库的一个模块化套件，提供统计计算和可视化功能。模仿非常流行的数据分析 R 环境，它集合了 Clojure(一个交互式 REPL)的强大功能和一组强大的数据处理抽象。

Incanter 的每个模块都专注于一个特定的功能领域。例如，`incanter-stats`包含一套相关的函数，用于分析数据和生成汇总统计数据，而`incanter-charts`提供了大量的可视化功能。`incanter-core`提供了最基本和最有用的数据转换函数。

每个模块都可以单独包含在您自己的代码中。要访问统计数据、图表和 Excel 功能，您可以在您的`project.clj`中包含以下内容:

```
  :dependencies [[incanter/incanter-core "1.5.5"]

                 [incanter/incanter-stats "1.5.5"]

                 [incanter/incanter-charts "1.5.5"]

                 [incanter/incanter-excel "1.5.5"]

                 ...]
```

如果您不介意包含比您需要的更多的库，您可以简单地包含完整的咒语发行版:

```
:dependencies [[incanter/incanter "1.5.5"]

               ...]
```

Incanter 的核心是数据集的概念——由行和列组成的结构。如果您有使用关系数据库的经验，您可以将数据集视为一个表。数据集中的每一列都有名称，数据集中的每一行都有相同数量的列。有几种方法可以将数据加载到 Incanter 数据集中，我们使用哪种方法将取决于我们的数据是如何存储的:

*   如果我们的数据是文本文件(CSV 或制表符分隔的文件)，我们可以使用来自`incanter-io`的`read-dataset`函数
*   如果我们的数据是一个 Excel 文件(例如，`.xls`或`.xlsx`文件)，我们可以使用`incanter-excel`中的`read-xls`函数
*   对于任何其他数据源(外部数据库、网站等等)，只要我们可以将数据放入 Clojure 数据结构中，我们就可以用`incanter-core`中的`dataset`函数创建一个数据集

本章使用 Excel 数据源，所以我们将使用`read-xls`。该函数采用一个必需的参数(要加载的文件)和一个指定图纸编号或名称的可选关键字参数。我们所有的例子都只有一张表，所以我们只提供字符串形式的文件参数:

```
(ns cljds.ch1.data

  (:require [clojure.java.io :as io]

            [incanter.core :as i]

            [incanter.excel :as xls]))
```

一般来说，我们不会从示例代码中复制名称空间声明。这既是为了简洁，也是因为所需的名称空间通常可以通过引用它们的符号来推断。例如，在本书中，我们总是将`clojure.java.io`称为`io`，将`incanter.core`称为`I`，将`incanter.excel`称为`xls`。

在本章中，我们将加载几个数据源，所以我们在`cljds.ch1.data`名称空间中创建了一个名为`load-data`的多方法:

```
(defmulti load-data identity)

(defmethod load-data :uk [_]

  (-> (io/resource "UK2010.xls")

      (str)

      (xls/read-xls)))
```

在前面的代码中，我们定义了在第一个参数的`identity`上调度的`load-data`多方法。我们还定义了当第一个参数是`:uk`时将被调用的实现。因此，调用`(load-data :uk)`将返回一个包含英国数据的 Incanter 数据集。在本章的后面，我们将为其他数据集定义额外的`load-data`实现。

`UK2010.xls`电子表格的第一行包含列名。Incanter 的`read-xls`函数会将这些作为返回数据集的列名保留下来。现在让我们通过检查数据开始我们的探索——`incanter.core`中的`col-names`函数将列名作为向量返回。在下面的代码中(在整本书中，我们使用来自`incanter.core`命名空间的函数)，我们要求它为`i`:

```
(defn ex-1-1 []

  (i/col-names (load-data :uk)))
```

如前面运行示例中所述，以`ex-`开头的函数可以在命令行上用 Leiningen 运行，如下所示:

```

lein run –e 1.1

```

前面命令的输出应该是下面的 Clojure 向量:

```
["Press Association Reference" "Constituency Name" "Region" "Election Year" "Electorate" "Votes" "AC" "AD" "AGS" "APNI" "APP" "AWL" "AWP" "BB" "BCP" "Bean" "Best" "BGPV" "BIB" "BIC" "Blue" "BNP" "BP Elvis" "C28" "Cam Soc" "CG" "Ch M" "Ch P" "CIP" "CITY" "CNPG" "Comm" "Comm L" "Con" "Cor D" "CPA" "CSP" "CTDP" "CURE" "D Lab" "D Nat" "DDP" "DUP" "ED" "EIP" "EPA" "FAWG" "FDP" "FFR" "Grn" "GSOT" "Hum" "ICHC" "IEAC" "IFED" "ILEU" "Impact" "Ind1" "Ind2" "Ind3" "Ind4" "Ind5" "IPT" "ISGB" "ISQM" "IUK" "IVH" "IZB" "JAC" "Joy" "JP" "Lab" "Land" "LD" "Lib" "Libert" "LIND" "LLPB" "LTT" "MACI" "MCP" "MEDI" "MEP" "MIF" "MK" "MPEA" "MRLP" "MRP" "Nat Lib" "NCDV" "ND" "New" "NF" "NFP" "NICF" "Nobody" "NSPS" "PBP" "PC" "Pirate" "PNDP" "Poet" "PPBF" "PPE" "PPNV" "Reform" "Respect" "Rest" "RRG" "RTBP" "SACL" "Sci" "SDLP" "SEP" "SF" "SIG" "SJP" "SKGP" "SMA" "SMRA" "SNP" "Soc" "Soc Alt" "Soc Dem" "Soc Lab" "South" "Speaker" "SSP" "TF" "TOC" "Trust" "TUSC" "TUV" "UCUNF" "UKIP" "UPS" "UV" "VCCA" "Vote" "Wessex Reg" "WRP" "You" "Youth" "YRDPL"]
```

这是一个非常广泛的数据集。数据文件中的前六列描述如下:随后的栏目按党派细分了投票数:

*   **记协参考**:这是识别选区(投票区，由一名议员代表)的号码
*   **选区名称**:这是投票区的通用名称
*   **Region** :这是选区所在的英国地理区域
*   **选举年**:这是举行选举的年份
*   **选民**:这是在选区中有资格投票的总人数
*   **票数**:这是投票总数

每当我们面对新数据时，花时间去理解它是很重要的。在没有详细数据定义的情况下，我们可以这样做的一个方法是从验证我们对数据的假设开始。例如，我们期望这个数据集包含关于 2010 年选举的信息，所以让我们回顾一下`Election Year`列的内容。

Incanter 提供了用于从数据集中选择列的`i/$`函数(`i`，和以前一样，表示`incanter.core`名称空间)。在本章中，我们会经常遇到这个函数——它是 Incanter 从各种数据表示中选择列的主要方法，它提供了几种不同的算法。现在，我们将只提供我们想要提取的列的名称以及从中提取列的数据集:

```
(defn ex-1-2 []

  (i/$ "Election Year" (load-data :uk)))

;; (2010.0 2010.0 2010.0 2010.0 2010.0 ... 2010.0 2010.0 nil)
```

年份作为单个值序列返回。输出可能很难解释，因为数据集包含这么多行。因为我们想知道该列包含哪些唯一值，所以我们可以使用 Clojure 核心函数`distinct`。使用 Incanter 的一个优点是，它有用的数据操作功能增强了 Clojure 已经提供的功能，如下例所示:

```
(defn ex-1-3 []

  (->> (load-data :uk)

       (i/$ "Election Year")

       (distinct)))

;; (2010 nil)
```

`2010`年大大证实了我们的预期，即这些数据来自`2010`。然而，`nil`值是出乎意料的，可能表明我们的数据有问题。

我们还不知道数据集中有多少 nil，确定这一点可以帮助我们决定下一步做什么。像这样计算数值的一个简单方法是使用核心库函数`frequencies`，它返回一个数值到计数的映射:

```
(defn ex-1-4 [ ]

  (->> (load-data :uk)

       (i/$ "Election Year")

       (frequencies)))

;; {2010.0 650 nil 1}
```

在前面的例子中，我们使用 Clojure 的 thread-last 宏`->>`将几个函数链接在一起，以便于阅读。

### Tip

除了 Clojure 庞大的数据操作核心函数库之外，前面讨论的宏(包括 thread-last 宏`->>`)也是使用 Clojure 分析数据的重要原因。在本书中，我们将看到 Clojure 如何使复杂的分析变得简洁易懂。

我们不需要很长时间就能确认 2010 年英国有 650 个选区，即所谓的选区。当对新数据进行健全检查时，像这样的领域知识是非常宝贵的。因此，`nil`值很可能是无关的，可以被删除。我们将在下一节看到如何做到这一点。



# 数据清理

数据科学家至少 80%的工作是数据清理，这是一个经常重复的统计数据。这是一个过程，检测潜在的损坏或不正确的数据，并对其进行纠正或过滤。

### 注意

数据清理是处理数据最重要(也是最耗时)的方面之一。这是确保对有效、准确和一致的数据执行后续分析的关键步骤。

选举年列的末端的`nil`值可以指示应该被移除的脏数据。我们已经看到，过滤数据的*列*可以用 Incanter 的`i/$`函数来完成。为了过滤*行*的数据，我们可以使用 Incanter 的`i/query-dataset`函数。

我们通过传递列名和谓词的 Clojure 映射，让 Incanter 知道我们希望它过滤哪些行。只有所有谓词都返回 true 的行才会被保留。例如，要从我们的数据集中只选择`nil`值:

```
(-> (load-data :uk)

    (i/query-dataset {"Election Year" {:$eq nil}}))
```

如果你了解 SQL，你会注意到这非常类似于一个`WHERE`子句。事实上，Incanter 还提供了`i/$where`函数，它是`i/query-dataset`的别名，可以颠倒参数的顺序。

查询是列名到谓词的映射，每个谓词本身是操作符到操作数的映射。可以通过同时指定多个列和多个运算符来构造复杂的查询。查询运算符包括:

*   `:$gt`大于
*   `:$lt`小于
*   `:$gte`大于或等于
*   `:$lte`小于或等于
*   `:$eq`等于
*   `:$ne`不等于
*   测试集合的成员资格
*   `:$nin`测试集合的非成员资格
*   `:$fn`应为要保留的行返回真实响应的谓词函数

如果没有一个内置操作符可以满足要求，那么最后一个操作符可以提供传递自定义函数的能力。

我们将继续使用 Clojure 的 thread-last 宏使代码意图更加清晰，并使用`i/to-map` 函数将行作为键和值的映射返回:

```
(defn ex-1-5 []

  (->> (load-data :uk)

       (i/$where {"Election Year" {:$eq nil}})

       (i/to-map)))

;; {:ILEU nil, :TUSC nil, :Vote nil ... :IVH nil, :FFR nil}
```

仔细查看结果，很明显这一行中的所有列(除了一列)都是`nil`。事实上，进一步的研究证实非零行是一个汇总总数，应该从数据中删除。我们可以通过使用`:$ne`操作符更新谓词映射来删除有问题的行，只返回选举年份不等于`nil`的行:

```
(->> (load-data :uk)

      (i/$where {"Election Year" {:$ne nil}}))
```

前面的函数是我们几乎总是想确保在使用数据之前调用的函数。一种方法是添加我们的`load-data` multimethod 的另一个实现，它也包括这个过滤步骤:

```
(defmethod load-data :uk-scrubbed [_]

  (->> (load-data :uk)

       (i/$where {"Election Year" {:$ne nil}})))
```

现在我们写的任何代码，都可以选择是引用`:uk` 还是`:uk-scrubbed`数据集。

通过总是加载源文件并在上面执行清理，我们保留了我们所应用的转换的审计线索。这让我们——以及我们代码的未来读者——清楚地知道对源代码做了哪些调整。这也意味着，如果我们需要使用新的源数据重新运行我们的分析，我们可能能够只加载新文件来代替现有文件。



# 描述性统计

描述性统计数据是用于总结和描述数据的数字。在下一章中，我们将把注意力转向更复杂的分析，所谓的**推断统计**，但是现在我们将把自己限制在简单地描述我们可以观察到的关于文件中包含的数据的。

为了说明我们所说的是什么意思，让我们看看数据的`Electorate`列。这一栏列出了每个选区的登记选民总数:

```
(defn ex-1-6 []

  (->> (load-data :uk-scrubbed)

       (i/$ "Electorate")

       (count)))

;; 650
```

我们已经从数据集中筛选出了`nil`字段；前面的代码应该返回一个与每个英国选区的选民相对应的`650`号码列表。

描述性统计，也称为**汇总统计**，是衡量数字序列属性的方法。它们有助于描述序列的特征，并可以作为进一步分析的指南。让我们从计算一个数列的两个最基本的统计数据开始——它的均值和方差。

## 中庸

测量数据集平均值的最常见方法是使用平均值。这实际上是测量数据的集中趋势(T2)的几种方法之一。平均值，或者更准确地说，算术平均值，是一种简单的计算方法——简单地将数值相加，然后除以计数——但尽管如此，它还是有一个有点吓人的数学符号:

![The mean](graphics/7180OS_01_01.jpg)

其中![The mean](graphics/7180OS_01_02.jpg)读作 *x-bar* ，这个数学符号通常用来表示平均值。

对于从数学或科学之外的领域进入数据科学的程序员来说，这种符号可能非常令人困惑和疏远。其他人可能完全适应这种符号，他们可以放心地跳过下一节。

## 解读数学符号

虽然数学符号可能看起来晦涩难懂，令人心烦意乱，但实际上只有少数几个符号会在本书的公式中频繁出现。

*σ*读作*西格玛*，意为*总和*。当你在数学符号中看到它时，它意味着一个序列正在被累加。sigma*上方和下方的符号*表示我们将求和的范围。它们更像是一个 C 型循环，在前面的公式中，我们将从 *i=1* 开始求和，直到 *i=n* 。按照惯例， *n* 是序列的长度，数学符号中的序列是一个索引的，而不是零索引的，所以从 *1* 到 *n* 的求和意味着我们对序列的整个长度求和。

紧跟在 sigma 后面的表达式是要求和的序列。在我们前面的平均值公式中，*x*T2 I 紧跟在 sigma 之后。由于 *i* 将表示从 *1* 到 *n* 的每个索引，*x*I 表示 *xs* 序列中的每个元素。

最后，![Interpreting mathematical notation](graphics/7180OS_01_03.jpg)正好出现在 sigma 之前，表示整个表达式应该乘以 *1* 除以 *n* (也称为 n 的**倒数)。这可以简化为仅仅除以 *n* 。**

| 

名字

 | 

数学符号

 | 

Clojure 等价

 |
| --- | --- | --- |
|   | *n* | `(count xs)` |
| 适马符号 | ![Interpreting mathematical notation](graphics/7180OS_01_04.jpg) | `(reduce + xs)` |
| 圆周率符号 | ![Interpreting mathematical notation](graphics/7180OS_01_05.jpg) | `(reduce * xs)` |

将所有这些放在一起，我们得到“从第一个到最后一个将序列中的元素相加，然后除以计数”。在 Clojure 中，这可以写成:

```
(defn mean [xs]

  (/ (reduce + xs)

     (count xs)))
```

其中`xs`代表“ *xs* 的顺序”。我们可以使用新的`mean`函数来计算英国选民的平均值:

```
(defn ex-1-7 []

  (->> (load-data :uk-scrubbed)

       (i/$ "Electorate")

       (mean)))

;; 70149.94
```

事实上，Incanter 已经包含了一个函数`mean`，可以在`incanter.stats`名称空间中非常高效地计算一个序列的平均值。在这一章(以及整本书)中，无论在哪里使用`incanter.stats`名称空间都需要作为`s`。

## 中位数

中位数是另一种常见的描述性统计，用于衡量一个序列的集中趋势。如果你把所有数据从最低到最高排序，中间值就是中间值。如果序列中有偶数个数据点，中值通常定义为中间两个值的平均值。

中位数通常在公式中用![The median](graphics/7180OS_01_06.jpg)表示，读作 *x-tilde* 。数学符号的缺陷之一是没有特别标准的方式来表达中值的公式,但尽管如此，在 Clojure 中还是相当简单的:

```
(defn median [xs]

  (let [n   (count xs)

        mid (int (/ n 2))]

    (if (odd? n)

      (nth (sort xs) mid)

      (->> (sort xs)

           (drop (dec mid))

           (take 2)

           (mean)))))
```

英国选民的中位数是:

```
(defn ex-1-8 []

  (->> (load-data :uk-scrubbed)

       (i/$ "Electorate")

       (median)))

;; 70813.5
```

Incanter 还有一个功能，就是把中值计算为`s/median`。



# 方差

均值和中值是描述序列中间值的两种可选方式，但它们本身并不能告诉你序列中包含的值。例如，如果我们知道一个由 99 个值组成的序列的平均值是 50，我们仍然很难知道这个序列包含什么值。

它可能包含从 1 到 99 的所有整数，或者 49 个 0 和 599 个。也许它包含负一九十八次和单个五千零四十八。或者所有的值都正好是 50。

序列的方差是其关于平均值的“分布”,前面的每个例子都有不同的方差。在数学符号中，方差表示为:

![Variance](graphics/7180OS_01_07.jpg)

其中 *s* ² 是常用来表示方差的数学符号。

这个公式与前面计算的平均值公式有许多相似之处。我们不是对单个值*x*T2 I 求和，而是对一个函数![Variance](graphics/7180OS_01_08.jpg)求和。回想一下，符号![Variance](graphics/7180OS_01_02.jpg)代表平均值，因此该函数计算 *xi* 与所有*x*的平均值的平方偏差。

我们可以将表达式![Variance](graphics/7180OS_01_08.jpg)转换成一个函数`square-deviation`，映射到序列`xs`上。我们还可以利用已经创建的`mean`函数对序列中的值求和，然后除以计数。

```
(defn variance [xs]

  (let [x-bar (mean xs)

        n     (count xs)

        square-deviation (fn [x]

                           (i/sq (- x x-bar)))]

    (mean (map square-deviation xs))))
```

我们使用 Incanter 的`i/sq`函数来计算我们表达式的平方。

因为我们在取平均值之前已经对偏差进行了平方，所以方差的单位也是平方，所以英国选民的方差单位是“人民平方”。这有点不合常理。我们可以通过求方差的平方根使单位更自然，因此单位又是“人”，结果被称为**标准差**:

```
(defn standard-deviation [xs]

  (i/sqrt (variance xs)))

(defn ex-1-9 []

  (->> (load-data :uk-scrubbed)

       (i/$ "Electorate")

       (standard-deviation)))

;; 7672.77
```

Incanter 实现了函数，分别计算方差和标准差为`s/variance`和`s/sd`。



# 分位数

中位数是从列表中计算中间值*的一种方法，而方差提供了一种测量中点周围数据分布的方法。如果整个数据范围用 0 到 1 来表示，中值将是 0.5。*

例如，考虑以下数字序列:

```
[10 11 15 21 22.5 28 30]
```

数列中有七个数字，所以中位数是第四个，即 21。这也被称为 0.5 分位数。通过查看 0、0.25、0.5、0.7 和 1.0 分位数，我们可以更全面地了解数字序列。总的来说，这些数字不仅会显示中位数，还会总结数据的范围以及数字在其中的分布情况。他们有时被称为*五位数摘要*。

计算英国选民数据的五位数汇总的一种方法如下所示:

```
(defn quantile [q xs]

  (let [n (dec (count xs))

        i (-> (* n q)

              (+ 1/2)

              (int))]

    (nth (sort xs) i)))

(defn ex-1-10 []

  (let [xs (->> (load-data :uk-scrubbed)

                (i/$ "Electorate"))

        f (fn [q]

            (quantile q xs))]

    (map f [0 1/4 1/2 3/4 1])))

;; (21780.0 66219.0 70991.0 75115.0 109922.0)
```

分位数也可以用`s/quantile`函数直接在 Incanter 中计算。所需分位数的序列作为关键字参数`:probs`传递。

### 注意

Incanter 的`quantile`函数使用了之前显示的算法的一种变体，称为 **phi-quantile** ，它在特定情况下执行连续数字之间的线性插值。计算分位数的有很多替代方法——咨询[https://en.wikipedia.org/wiki/Quantile](https://en.wikipedia.org/wiki/Quantile)了解差异讨论。

如前所述，分位数将范围分成四个相等的范围，它们被称为**四分位数**。上下四分位数之间的差异被称为**四分位数间** **范围**，也常常简称为 **IQR** 。像关于平均值的方差一样，IQR 给出了关于中位数的数据分布的度量。



# 宁滨数据

为了发展对这些不同的方差计算所测量的东西的直觉，我们可以使用一种叫做 T2 宁滨 T3 的技术 T1。在数据连续的情况下，使用`frequencies`(就像我们用选举数据计算 nil 一样)是不实际的，因为没有两个值是相同的。但是，通过将数据分组到离散的区间中，可以获得数据结构的大致含义。

宁滨的过程是将值的范围分成多个连续的、大小相等的较小的容器。原始序列中的每个值恰好属于一个容器。通过计算落入每个条柱的点数，我们可以了解数据的分布情况:

![Binning data](graphics/7180OS_01_100.jpg)

上图显示了十五个 *x* 的值被分成五个大小相等的容器。通过计算落入每个箱中的点的数量，我们可以清楚地看到，大多数点落入中间的箱中，较少的点落入朝向边缘的箱中。我们可以用下面的`bin`函数在 Clojure 中实现同样的功能:

```
(defn bin [n-bins xs]

  (let [min-x    (apply min xs)

        max-x    (apply max xs)

        range-x  (- max-x min-x)

        bin-fn   (fn [x]

                   (-> x

                       (- min-x)

                       (/ range-x)

                       (* n-bins)

                       (int)

                       (min (dec n-bins))))]

    (map bin-fn xs)))
```

例如，我们可以将范围 0-14 装入`5`容器，如下所示:

```
(bin 5 (range 15))

;; (0 0 0 1 1 1 2 2 2 3 3 3 4 4 4)
```

一旦我们将这些值分入箱中，我们就可以再次使用`frequencies`函数来计算每个箱中的点数。在下面的代码中，我们使用函数将英国选民数据分成五个部分:

```
(defn ex-1-11 []

  (->> (load-data :uk-scrubbed)

       (i/$ "Electorate")

       (bin 10)

       (frequencies)))

;; {1 26, 2 450, 3 171, 4 1, 0 2}
```

极端条柱(0 和 4)中的点数比中间条柱中的点数低得多，计数似乎朝着中间值上升，然后又下降。在下一节中，我们将可视化这些计数的形状。



# 直方图

直方图是可视化单个值序列分布的一种方式。直方图简单地取一个连续分布，对其进行分类，并将落入每个分类的点的频率绘制成一个条形。直方图中每个条形的高度表示该条柱中包含的数据点数。

我们已经看到了如何自己绑定数据，但是`incanter.charts`包含了一个`histogram`函数，它将绑定数据并分两步将其可视化为直方图。在本章(以及整本书)中，我们要求`incanter.charts`为`c`。

```
(defn ex-1-12 []

  (-> (load-data :uk-scrubbed)

      (i/$ "Electorate")

      (c/histogram)

      (i/view)))
```

前面的代码生成了下面的图表:

![Histograms](graphics/7180OS_01_110.jpg)

我们可以通过将关键字参数`:nbins`作为第二个参数传递给直方图函数来配置数据被分割成多少个条块:

```
(defn ex-1-13 []

  (-> (uk-electorate)

      (c/histogram :nbins 200)

      (i/view)))
```

前面的图表显示了一个单独的高峰，但表达了数据的形状相当粗糙。下图显示了详细信息，但条形的体积掩盖了分布的形状，尤其是在尾部:

![Histograms](graphics/7180OS_01_120.jpg)

选择表示数据的条柱数量是一个很好的平衡——太少的条柱和数据的形状只能被粗略地表示，太多的和嘈杂的特征可能会模糊基础结构。

```
(defn ex-1-14 []

  (-> (i/$ "Electorate" (load-data :uk-scrubbed))

      (c/histogram :x-label "UK electorate"

                   :nbins 20)

      (i/view)))
```

下面显示的是`20`条的直方图:

![Histograms](graphics/7180OS_01_130.jpg)

这张包含`20`条块的最终图表似乎是迄今为止该数据的最佳代表。

除了平均值和中值，*模式*是测量序列平均值的另一种方式——它被定义为序列中出现频率最高的值。该模式严格地仅被定义用于具有至少一个重复值的序列；对于许多发行版来说，情况并非如此，模式也未定义。尽管如此，直方图的峰值通常被称为众数，因为它对应于最受欢迎的仓。

我们可以清楚地看到，该分布关于该模式是非常对称的，值沿着浅的尾部在一侧急剧下降。这是遵循近似正态分布的数据。



# 正态分布

直方图将告诉您数据在其范围内大致是如何分布的，并提供一种可视化的方法将您的数据分类到少数几种常见分布中的一种。许多分布在数据分析中经常出现，但没有一个像正态分布那样多，也称为**高斯分布**。

### 注意

这种分布被称为正态分布，因为它在自然界中经常出现。伽利略注意到他的天文测量中的误差遵循一种分布，在这种分布中，与平均值的小偏差比大偏差出现得更频繁。伟大的数学家高斯在描述这些误差的数学形态方面做出了贡献，这使得该分布也以他的名字命名为高斯分布。

分布类似于压缩算法:它允许非常有效地汇总潜在的大量数据。正态分布只需要两个参数，其余的数据可以从这两个参数中近似得到——平均值和标准差。

## 中心极限定理

中心极限定理部分解释了正态分布无处不在的原因。在某些情况下，从不同分布生成的值将趋向于收敛到正态分布，我们将在下面展示。

在编程中常见的分布是*均匀分布*。这是 Clojure 的`rand`函数生成的数的分布:对于一个公平的随机数生成器，所有的数都有均等的机会被生成。我们可以通过多次生成 0 到 1 之间的随机数并绘制结果，在直方图上直观地显示出来。

```
(defn ex-1-15 []

  (let [xs (->> (repeatedly rand)

                (take 10000))]

    (-> (c/histogram xs

                     :x-label "Uniform distribution"

                     :nbins 20)

        (i/view))))
```

前面的代码将生成如下直方图:

![The central limit theorem](graphics/7180OS_01_140.jpg)

直方图的每个条大约是相同的高度，对应于生成落入每个箱中的数的相等概率。由于均匀分布描述了我们的随机采样无法精确反映的理论输出，所以条形的高度并不完全相同。在接下来的几章中，我们将学习如何精确量化理论和实践之间的差异，以确定这些差异是否大到足以引起关注。在这种情况下，他们不是。

相反，如果我们生成个数字序列平均值的直方图，我们将得到一个看起来相当不同的分布。

```
(defn ex-1-16 []

  (let [xs (->> (repeatedly rand)

                (partition 10)

                (map mean)

                (take 10000))]

    (-> (c/histogram xs

                     :x-label "Distribution of means"

                     :nbins 20)

        (i/view))))
```

前面的代码将提供类似于下面的直方图的输出:

![The central limit theorem](graphics/7180OS_01_150.jpg)

虽然平均值接近零或一不是不可能的，但这种可能性极小，而且随着平均数和样本平均数的增加，这种可能性越来越小。事实上，产量非常接近正态分布。

这个结果——许多小的随机波动的平均效应导致正态分布——被称为的**中心极限定理**，有时被缩写为 **CLT** ，并在很大程度上解释了为什么正态分布在自然现象中如此频繁地出现。

尽管早在 1733 年，法国数学家亚伯拉罕·德莫佛就记录了中心极限定理的影响，他使用正态分布来估计投掷一枚公平硬币所产生的人头数，但是直到 20 世纪^(T2 才命名中心极限定理。掷硬币的结果最好用二项式分布来建模，我们将在[第 4 章](ch04.html "Chapter 4. Classification")、*分类*中介绍。虽然中心极限定理提供了一种从近似正态分布生成样本的方法，但 Incanter 的`distributions`命名空间提供了从各种分布有效生成样本的函数，包括正态分布:)

```
(defn ex-1-17 []

  (let [distribution (d/normal-distribution)

        xs (->> (repeatedly #(d/draw distribution))

                (take 10000))]

    (-> (c/histogram xs

                     :x-label "Normal distribution"

                     :nbins 20)

        (i/view))))
```

上述代码生成以下直方图:

![The central limit theorem](graphics/7180OS_01_155.jpg)

`d/draw`函数将从提供的分布中返回一个样本。与`d/normal-distribution`的默认均值和标准偏差分别为零和一。



# 庞加莱的面包师

有一个故事，虽然几乎肯定是虚构的，但它让我们更详细地了解了中心极限定理是如何让我们推理分布是如何形成的。它与 19 世纪著名的法国学者亨利·庞加莱有关，据说他连续一年每天称面包的重量。

烘焙是一个受监管的职业，庞加莱发现，虽然面包的重量遵循正态分布，但峰值是 950 克，而不是广告中的 1 公斤。他向当局告发了他的面包师，因此面包师被罚款。

第二年，庞加莱继续称同一面包师的面包。他发现现在的平均值是 1 公斤，但分布不再围绕平均值对称。分布向右倾斜，与面包师只给庞加莱最重的面包相一致。庞加莱再次向当局举报他的面包师，他的面包师第二次被罚款。

这个故事是否真实与我们无关；提供它只是为了说明一个关键点——一个数字序列的分布可以告诉我们一些关于生成它的过程的重要信息。

## 生成分配

为了开发我们对正态分布和方差的直觉，让我们用 Incanter 的分布函数来模拟一个诚实和不诚实的面包师。我们可以将诚实的面包师建模为平均值为 1000 的正态分布，对应于 1 公斤的公平面包。我们假设烘焙过程中的方差导致标准差为 30 克。

```
(defn honest-baker [mean sd]

  (let [distribution (d/normal-distribution mean sd)]

    (repeatedly #(d/draw distribution))))

(defn ex-1-18 []

  (-> (take 10000 (honest-baker 1000 30))

      (c/histogram :x-label "Honest baker"

                   :nbins 25)

      (i/view)))
```

前面的代码将提供类似于下面的直方图的输出:

![Generating distributions](graphics/7180OS_01_160.jpg)

现在，让我们来模拟一个面包师，他只卖最重的面包。我们将序列分成 13 个一组(一个“面包师一打”)，并选择最大值:

```
(defn dishonest-baker [mean sd]

  (let [distribution (d/normal-distribution mean sd)]

    (->> (repeatedly #(d/draw distribution))

         (partition 13)

         (map (partial apply max)))))

(defn ex-1-19 []

  (-> (take 10000 (dishonest-baker 950 30))

      (c/histogram :x-label "Dishonest baker"

                   :nbins 25)

      (i/view)))
```

前面的代码将生成如下直方图:

![Generating distributions](graphics/7180OS_01_170.jpg)

很明显，这张直方图与我们看到的其他直方图不太一样。平均值仍然是 1kg，但平均值周围的值分布不再对称。我们说这个直方图表示一个**偏斜的正态分布**。



# 偏斜度

偏斜度是分布关于其众数的不对称性的名称。**负偏斜**，或**左偏斜**，表示图形下方区域在模式左侧较大。**正偏斜**，或**右偏斜**，表示图形下方的区域在模式右侧较大。

![Skewness](graphics/7180OS_01_180.jpg)

在`stats`名称空间中，Incanter 有一个用于测量偏斜度的内置函数:

```
(defn ex-1-20 []

  (let [weights (take 10000 (dishonest-baker 950 30))]

    {:mean (mean weights)

     :median (median weights)

     :skewness (s/skewness weights)}))
```

前面的例子表明，不诚实的面包师的输出的偏斜度约为 0.4，量化了直方图中明显的偏斜度。

## 分位数-分位数图

我们在本章前面遇到了用分位数来描述 T2 数据分布的方法。回想一下，`quantile`函数接受一个介于 0 和 1 之间的数字，并返回序列在该点的值。0.5 对应于中间值。

将数据的分位数与正态分布的分位数相对照，可以让我们看到测量数据与理论分布的对比情况。像这样的图被称为 **Q-Q 图**，它们提供了一种快速直观的方法来确定常态。对于接近正态分布的数据，Q-Q 图是一条直线。偏离直线表示数据偏离理想化正态分布的方式。

让我们一起为诚实和不诚实的面包师绘制 Q-Q 图。Incanter 的`c/qq-plot`函数接受数据点列表，并生成样本分位数的散点图，相对于理论上的正态分布的分位数绘制:

```
(defn ex-1-21 []

  (->> (honest-baker 1000 30)

       (take 10000)

       (c/qq-plot)

       (i/view))

  (->> (dishonest-baker 950 30)

       (take 10000)

       (c/qq-plot)

       (i/view)))
```

前面的代码将生成以下图形:

![Quantile-quantile plots](graphics/7180OS_01_190.jpg)

诚实的面包师的 Q-Q 曲线已经在前面展示过了。不诚实的接下来是贝克的阴谋:

![Quantile-quantile plots](graphics/7180OS_01_200.jpg)

线是弯曲的这一事实表明数据是正偏斜的；另一个方向的曲线表示负偏斜。事实上，Q-Q 图更容易辨别各种各样的与标准正态分布的偏差，如下图所示:

![Quantile-quantile plots](graphics/7180OS_01_210.jpg)

Q-Q 图将诚实和不诚实面包师的分布与理论正态分布进行比较。在下一节中，我们将比较几种直观比较两个(或更多)测量值序列的替代方法。



# 对比可视化

Q-Q 图提供了一个很好的方法来比较测量的、经验的分布和理论的正态分布。如果我们想要比较两个或更多的经验分布，我们不能使用 Incanter 的 Q-Q 曲线图。尽管如此，我们还有各种其他的选择，如下两节所示。

## 箱形图

箱形图，或**箱形图和须状图**，是一种直观显示中位数和方差的描述性统计的方法。我们可以使用下面的代码生成:

```
(defn ex-1-22 []

  (-> (c/box-plot (->> (honest-baker 1000 30)

                       (take 10000))

                  :legend true

                  :y-label "Loaf weight (g)"

                  :series-label "Honest baker")

      (c/add-box-plot (->> (dishonest-baker 950 30)

                           (take 10000))

                      :series-label "Dishonest baker")

      (i/view)))
```

这将创建以下情节:

![Box plots](graphics/7180OS_01_220.jpg)

图中心的方框代表**四分位间距**。中间值是穿过盒子中间的线，平均值是大黑点。对于诚实的面包师来说，中间值穿过圆的中心，表明平均值和中间值大致相同。对于不诚实的面包师来说，平均值偏离了中间值，表明存在偏差。

触须表示数据的范围，异常值用空心圆圈表示。在一张图表中，我们可以更清楚地看到两种分布之间的差异，而不是单独在直方图或 Q-Q 图上。

## 累积分布函数

累积分布函数，也称为 **CDFs** ，描述了从分布中提取的值小于 *x* 的概率。像所有概率分布一样，它们的值在 *0* 和 *1* 之间，其中 *0* 代表不可能， *1* 代表确定。举个例子，想象一下我要扔一个六面骰子。我掷出小于 6 的概率有多大？

为了公平起见，我掷出 5 或更低的概率是![Cumulative distribution functions](graphics/7180OS_01_09.jpg)。相反，我掷出 1 的概率只有![Cumulative distribution functions](graphics/7180OS_01_10.jpg)。3 或更低相当于偶数——概率为 50%。

掷骰子的 CDF 遵循与所有 CDF 相同的模式——对于范围低端的数字，CDF 接近于零，对应于选择该范围或更低的数字的低概率。在范围的高端，CDF 接近于 1，因为从序列中提取的大多数值将会更低。

### 注意

CDF 和分位数彼此密切相关，CDF 是分位数函数的反函数。如果 0.5 分位数对应于值 1，000，则 1，000 的 CDF 为 0.5。

正如 Incanter 的`s/quantile`函数允许我们在特定点从分布中采样值一样，`s/cdf-empirical`函数允许我们从序列中输入一个值，并返回一个介于 0 和 1 之间的值。这是一个高阶函数，它将接受值(在本例中是一个值序列)并返回一个函数。然后可以根据需要用不同的输入值调用返回的函数，为每个输入值返回 CDF。

### 注意

高阶函数是接受或返回函数的函数。

让我们并排绘制诚实和不诚实面包师的 CDF。我们可以通过绘制源数据——来自诚实和不诚实的面包师的样本——与根据经验 CDF 计算的概率对比，使用 Incanter 的`c/xy-plot`来可视化 CDF。`c/xy-plot`函数期望将 *x* 值和 *y* 值作为两个独立的值序列提供。

为了在同一个图表上绘制两种分布，我们需要能够向我们的`xy-plot`提供多个系列。Incanter 为其许多图表提供了添加额外系列的功能。在一个`xy-plot`的情况下，我们可以使用函数`c/add-lines`，它接受图表作为第一个参数，分别接受 *x* 系列和 *y* 系列数据作为接下来的两个参数。你也可以通过一个可选的系列标签。我们在下面的代码中这样做，这样我们可以在完成的图表上区分这两个系列:

```
(defn ex-1-23 []

  (let [sample-honest    (->> (honest-baker 1000 30)

                              (take 1000))

        sample-dishonest (->> (dishonest-baker 950 30)

                              (take 1000))

        ecdf-honest    (s/cdf-empirical sample-honest)

        ecdf-dishonest (s/cdf-empirical sample-dishonest)]

    (-> (c/xy-plot sample-honest (map ecdf-honest sample-honest)

                   :x-label "Loaf Weight"

                   :y-label "Probability"

                   :legend true

                   :series-label "Honest baker")

        (c/add-lines sample-dishonest

                     (map ecdf-dishonest sample-dishonest)

                     :series-label "Dishonest baker")

        (i/view))))
```

前面的代码生成了下面的图表:

![Cumulative distribution functions](graphics/7180OS_01_230.jpg)

虽然看起来很不一样，但该图显示的信息与方框图和须状图基本相同。我们可以看到两条线大约在中间值 0.5 处相交，相当于 1000 克。不诚实的线在较低的尾部被截断，在较高的尾部变长，对应于一个偏斜的分布。



# 可视化的重要性

像早期那些简单的可视化是传达大量信息的简洁方式。它们是对我们在本章前面计算的汇总统计数据的补充，使用它们很重要。均值和标准差等统计数据必然隐藏了大量信息，因为它们将一个序列简化为一个数字。

统计学家 Francis Anscombe 设计了四个散点图的集合，被称为 **Anscombe 的四重奏**，它们的具有几乎相同的统计特性(包括均值、方差和标准差)。尽管如此，视觉上很明显的是 *xs* 和 *ys* 的分布都非常不同:

![The importance of visualizations](graphics/7180OS_01_240.jpg)

数据集不一定要被人为地绘制成图表来揭示有价值的见解。以 2013 年波兰国家数学考试考生的分数直方图为例:

![The importance of visualizations](graphics/7180OS_01_250.jpg)

我们可能会认为学生的能力是正态分布的，事实上——除了 30%左右的峰值——确实如此。我们可以清楚地看到，考官将学生的分数推过及格线是非常人性化的。

事实上，从大量样本中提取的序列分布非常可靠，任何偏离都可能是非法活动的证据。本福德定律，也称为第一位数定律，是在大范围内产生的随机数的一个奇怪特征。一个出现在大约 30%的时间里作为前导数字，而较大的数字出现的频率越来越低。例如，9 作为前导数字出现的时间不到 5%。

### 注意

本福德定律是以物理学家弗兰克·本福德的名字命名的，他在 1938 年提出了这一定律，并证明了它在各种数据来源中的一致性。早在 50 多年前，西蒙·纽康就已经观察到了这一现象，他注意到他的对数表的书页上，以数字 1 开头的数字更容易破损。

本福德表明，该定律适用于各种数据，如电费、街道地址、股票价格、人口数量、死亡率和河流长度。对于涵盖大范围数值的数据集来说，该法律是如此的一致，以至于在金融欺诈的审判中，偏离该法律已经被接受为证据。

## 可视化选民数据

让我们回到选举数据，将我们之前创建的选民序列与理论上的正态分布 CDF 进行比较。我们可以使用 Incanter 的`s/cdf-normal`函数从一系列值中生成一个普通的 CDF。默认均值为 0，标准差为 1，因此我们需要提供选民数据的测量均值和标准差。我们的选民数据的这些值分别是 70，150 和 7，679。

我们在本章前面已经生成了一个经验 CDF。以下示例简单地生成两个 CDF 中的每一个，并将其绘制在单个`c/xy-plot`上:

```
(defn ex-1-24 []

  (let [electorate (->> (load-data :uk-scrubbed)

                        (i/$ "Electorate"))

        ecdf   (s/cdf-empirical electorate)

        fitted (s/cdf-normal electorate

                             :mean (s/mean electorate)

                             :sd   (s/sd electorate))]

    (-> (c/xy-plot electorate fitted

                   :x-label "Electorate"

                   :y-label "Probability"

                   :series-label "Fitted"

                   :legend true)

        (c/add-lines electorate (map ecdf electorate)

                     :series-label "Empirical")

        (i/view))))
```

前面的示例生成了以下图:

![Visualizing electorate data](graphics/7180OS_01_260.jpg)

从两条线的接近程度可以看出，这个数据非常接近正态分布，尽管有一点点倾斜是明显的。偏斜的方向与我们之前绘制的不诚实的 baker CDF 相反，所以我们的选民数据稍微偏左。

当我们将我们的分布与理论正态分布进行比较时，让我们使用 Q-Q 图，默认情况下会这样做:

```
(defn ex-1-25 []

  (->> (load-data :uk-scrubbed)

       (i/$ "Electorate")

       (c/qq-plot)

       (i/view)))
```

下面的 Q-Q 图更好地突出了数据中明显的左偏斜:

![Visualizing electorate data](graphics/7180OS_01_270.jpg)

正如我们所料，曲线弯曲的方向与本章前面不诚实的贝克 Q-Q 曲线相反。这表明，如果数据更接近正态分布，有更多的选区比我们预期的要小。



# 添加列

本章到目前为止，我们已经通过过滤行和列减小了数据集的大小。通常我们会想要向数据集添加行，而 Incanter 以几种方式支持这一点。

首先，我们可以选择是替换数据集内的现有列，还是向数据集追加一个额外的列。其次，我们可以选择是否提供新的列值来直接替换现有的列值，或者是否通过对数据的每一行应用函数来计算新值。

下表列出了我们的选项和相应的咒语功能:

|   | 

替换数据

 | 

追加数据

 |
| --- | --- | --- |
| 通过提供序列 | `i/replace-column` | `i/add-column` |
| 通过应用一个函数 | `i/transform-column` | `i/add-derived-column` |

当基于函数转换或派生列时，我们传递要创建的新列的名称、应用于每一行的函数以及一系列现有的列名。这些现有列中包含的值将构成我们函数的参数。

让我们参考一个真实的例子来说明如何使用`i/add-derived-column`函数。2010 年英国大选的结果是一个无多数的议会，没有一个政党获得全部多数席位。保守党和自由民主党组成了联盟。在下一节中，我们将找出有多少人投票给任何一方，以及这占总投票的百分比。

## 添加派生列

为了找出投票给保守党或自由民主党的选民的百分比，我们需要计算任一政党的总票数。因为我们正在基于现有数据的函数创建一个新的数据字段，所以我们希望使用`i/add-derived-column`函数。

```
(defn ex-1-26 []

  (->> (load-data :uk-scrubbed)

       (i/add-derived-column :victors [:Con :LD] +)))
```

但是，如果我们现在运行它，将会生成一个异常:

```
ClassCastException java.lang.String cannot be cast to java.lang.Number  clojure.lang.Numbers.add (Numbers.java:126)
```

不幸的是，Clojure 抱怨我们试图添加一个`java.lang.String`。显然，`Con`或`LD`列中的一个(或两个)包含字符串值，但是是哪个呢？我们可以再次使用频率来查看问题的程度:

```
(->> (load-data :uk-scrubbed)

     ($ "Con")

     (map type)

     (frequencies))

;; {java.lang.Double 631, java.lang.String 19}

(->> (load-data :uk-scrubbed)

     ($ "LD")

     (map type)

     (frequencies))

;; {java.lang.Double 631, java.lang.String 19}
```

让我们使用本章前面遇到的`i/$where`函数来检查这些行:

```
(defn ex-1-27 []

  (->> (load-data :uk-scrubbed)

       (i/$where #(not-any? number? [(% "Con") (% "LD")]))

       (i/$ [:Region :Electorate :Con :LD])))

;; |           Region | Electorate | Con | LD |

;; |------------------+------------+-----+----|

;; | Northern Ireland |    60204.0 |     |    |

;; | Northern Ireland |    73338.0 |     |    |

;; | Northern Ireland |    63054.0 |     |    |

;; ...
```

这一点探索应该足以让我们相信，这些字段空白的原因是候选人没有在相应的选区中提出。它们应该被过滤掉还是假设为零？这是一个有趣的问题。让我们把他们过滤掉，因为选民甚至不可能在这些选区选择自由民主党或保守党候选人。相反，如果我们假设一个零，我们将人为地降低平均人数——如果有选择的话——投票给这两个政党中的任何一个。

现在我们知道了如何过滤有问题的行，让我们添加胜利者和胜利者的投票份额以及选举投票率的派生列。我们过滤这些行，只显示保守党和自由民主党候选人都被提名的行:

```
(defmethod load-data :uk-victors [_]

  (->> (load-data :uk-scrubbed)

       (i/$where {:Con {:$fn number?} :LD {:$fn number?}})

       (i/add-derived-column :victors [:Con :LD] +)

       (i/add-derived-column :victors-share [:victors :Votes] /)

       (i/add-derived-column :turnout [:Votes :Electorate] /)))
```

结果，我们的数据集中现在有了三个额外的列:`:victors`、`:victors-share`和`:turnout`。让我们将获胜者的投票份额绘制成 Q-Q 图，看看它与理论正态分布相比如何:

```
(defn ex-1-28 []

  (->> (load-data :uk-victors)

       (i/$ :victors-share)

       (c/qq-plot)

       (i/view)))
```

上述代码生成了以下图形:

![Adding derived columns](graphics/7180OS_01_275.jpg)

回顾本章前面的各种 Q-Q 曲线图，可以发现胜利者的投票份额与正态分布相比具有“轻尾”。这意味着更多的数据比我们从真正的正态分布数据中预期的更接近平均值。



# 选民数据的对比可视化

现在让我们看看另一次大选的数据集，这次是 2011 年的俄罗斯。俄罗斯是一个大得多的国家，它的选举数据也大得多。我们将把两个大的 Excel 文件加载到内存中，这可能会超过默认的 JVM 堆大小。

为了扩展 Incanter 可用的内存量，我们可以在项目的`profile.clj`中调整 JVM 设置。JVM 的配置标志的向量可以用键`:jvm-opts`提供。这里我们使用 Java 的`Xmx`标志将堆大小增加到 1GB。这应该绰绰有余了。

```
  :jvm-opts ["-Xmx1G"]
```

俄罗斯的数据存在于两个数据文件中。幸运的是，每一列都是相同的，所以它们可以首尾相连。咒语的功能`i/conj-rows`正是为了这个目的而存在:

```
(defmethod load-data :ru [_]

  (i/conj-rows (-> (io/resource "Russia2011_1of2.xls")

                   (str)

                   (xls/read-xls))

               (-> (io/resource "Russia2011_2of2.xls")

                   (str)

                   (xls/read-xls))))
```

在前面的代码中，我们定义了`load-data` multimethod 的第三个实现来加载和合并两个俄罗斯文件。

### 注意

除了`conj-rows`之外，Incanter-core 还定义了`conj-columns`，它将合并数据集的列，前提是它们具有相同的行数。

让我们看看俄罗斯数据列的名称是什么:

```
(defn ex-1-29 []

  (-> (load-data :ru)

      (i/col-names)))

;; ["Code for district"

;; "Number of the polling district (unique to state, not overall)"

;; "Name of district" "Number of voters included in voters list"

;; "The number of ballots received by the precinct election

;; commission" ...]
```

俄罗斯数据集中的列名非常具有描述性，但是可能比我们想要输入的要长。此外，如果表示与我们在英国选举数据中已经看到的属性相同的列(例如，胜利者的份额和投票率)在两个数据集中被标记为相同，这将是很方便的。让我们相应地重新命名它们。

除了数据集，`i/rename-cols`函数还希望接收一个映射，该映射的键是当前的列名，其值对应于所需的新列名。如果我们将此与我们已经看到的`i/add-derived-column`数据相结合，我们会得出以下结论:

```
(defmethod load-data :ru-victors [_]

  (->> (load-data :ru)

       (i/rename-cols

        {"Number of voters included in voters list" :electorate

         "Number of valid ballots" :valid-ballots

         "United Russia" :victors})

       (i/add-derived-column :victors-share

                             [:victors :valid-ballots] i/safe-div)

       (i/add-derived-column :turnout

                             [:valid-ballots :electorate] /)))
```

`i/safe-div`功能与`/`相同，但会防止被零除。它没有抛出异常，而是返回值`Infinity`，这将被 Incanter 的统计和图表功能忽略。



# 可视化俄罗斯选举数据

我们之前看到英国选举投票率的直方图大致正常(尽管有轻微的拖尾)。现在，我们已经加载并转换了俄罗斯选举数据，让我们看看它是如何比较的:

```
(defn ex-1-30 []

  (-> (i/$ :turnout (load-data :ru-victors))

      (c/histogram :x-label "Russia turnout"

                   :nbins 20)

      (i/view)))
```

前面的示例生成以下直方图:

![Visualizing the Russian election data](graphics/7180OS_01_320.jpg)

这个直方图看起来一点也不像我们目前看到的经典钟形曲线。有一个明显的正偏态，选民投票率实际上从 80%增加到 100%——与我们从正态分布数据中预期的相反。

鉴于英国数据和中心极限定理设定的预期，这是一个奇怪的结果。让我们用 Q-Q 图来显示数据:

```
(defn ex-1-31 []

  (->> (load-data :ru-victors)

       (i/$ :turnout)

       (c/qq-plot)

       (i/view)))
```

这将返回以下图:

![Visualizing the Russian election data](graphics/7180OS_01_330.jpg)

这个 Q-Q 图既不是一条直线，也不是一条特别 S 形的曲线。事实上，Q-Q 图表明在分布的顶端有一个轻的尾部，在底部有一个重的尾部。这几乎与我们在直方图上看到的相反，它清楚地表明了一个极其沉重的右尾巴。

事实上，正是因为尾部太重，所以 Q-Q 图具有误导性:直方图上 0.5 和 1.0 之间的点密度表明峰值应该在 0.7 左右，右尾部继续超过 1.0。我们会有超过 100%的百分比，这显然是不合逻辑的，但 Q-Q 图没有考虑到这一点(它不知道我们正在绘制百分比)，因此，超过 1.0 的数据的突然缺失被解释为右尾被修剪。

鉴于中心极限定理，以及我们对英国选举数据的观察，100%投票率的趋势很奇怪。让我们并排比较一下英国和俄罗斯的数据集。



# 对比可视化

假设我们想比较英国和俄罗斯的选民数据分布。在本章中我们已经看到了如何利用 CDF 和箱线图，所以让我们研究一种类似于直方图的替代方法。

我们可以尝试在直方图上绘制两个数据集，但这不是一个好主意。我们无法解释这些结果有两个原因:

*   投票区的大小，因此分配的手段，是非常不同的
*   总的来说，投票区的数量差别很大，所以直方图的高度也不同

解决这两个问题的直方图的替代方法是**概率质量函数** ( **PMF** )。

## 概率质量函数

概率质量函数，或 PMF，在中与直方图有很多共同之处。然而，它不是绘制落入箱中的值的计数，而是绘制从分布中抽取的数字将正好等于给定值的概率。由于该函数为分布可能返回的每个值分配了一个概率，并且概率是在从 0 到 1 的范围内测量的(1 对应于确定性)，因此概率质量函数下的面积等于 1。

因此，PMF 可确保我们的地块下的面积在数据集之间具有可比性。然而，我们仍然有一个问题，那就是投票区的大小——以及分布的平均值——无法比较。这可以通过一种独立的技术来解决——标准化。

### 注意

标准化数据与正态分布无关。它是将一个或多个值序列对齐的一般任务的名称。根据上下文，这可能意味着简单地调整值，使它们落入相同的范围，或者更复杂的过程，以确保数据的分布是相同的。一般来说，标准化的目的是为了便于比较两个或多个系列的数据。

标准化数据的方法有无数种，但最基本的方法之一是确保每个系列都在 0 到 1 的范围内。我们的值都没有降到零以下，因此我们可以通过简单地除以最大值来完成这种标准化:

```
(defn as-pmf [bins]

  (let [histogram (frequencies bins)

        total     (reduce + (vals histogram))]

    (->> histogram

         (map (fn [[k v]]

                [k (/ v total)]))

         (into {}))))
```

有了前面的函数，我们可以标准化英国和俄罗斯的数据，并在相同的轴上并排绘制:

```
(defn ex-1-32 []

  (let [n-bins 40

        uk (->> (load-data :uk-victors)

                (i/$ :turnout)

                (bin n-bins)

                (as-pmf))

        ru (->> (load-data :ru-victors)

                (i/$ :turnout)

                (bin n-bins)

                (as-pmf))]

    (-> (c/xy-plot (keys uk) (vals uk)

                   :series-label "UK"

                   :legend true

                   :x-label "Turnout Bins"

                   :y-label "Probability")

        (c/add-lines (keys ru) (vals ru)

                     :series-label "Russia")

        (i/view))))
```

前面的示例生成了下面的图表:

![Probability mass functions](graphics/7180OS_01_340.jpg)

归一化后，这两种分布可以更容易地进行比较。显而易见的是，尽管平均投票率比英国低，但俄罗斯大选的投票率却大幅提升至 100%。只要它代表了许多独立选择的综合效应，我们就会期望选举结果符合中心极限定理，并且近似呈正态分布。事实上，世界各地的选举结果普遍符合这一预期。

虽然不像分布中心的众数峰值那么高(对应于大约 50%的投票率)，但俄罗斯选举数据显示了一个非常反常的结果。维也纳医科大学的研究员彼得·克里梅克和他的同事甚至认为这是一个明显的选举舞弊的信号。

## 散点图

我们观察了俄罗斯选举投票率的奇怪结果，发现它与英国选举有着不同的特征。接下来，让我们看看获胜候选人的投票比例与投票率之间的关系。毕竟，如果出乎意料的高投票率真的是现任政府违规操作的迹象，我们预计他们会投票给自己，而不是其他任何人。因此，我们预计大部分(如果不是全部的话)额外的选票将会投给最终的选举获胜者。

第 3 章，*关联*，将更详细地介绍关联两个变量背后的统计数据，但现在，简单地将投票率和获胜方的选票比例之间的关系可视化会更有趣。

本章我们将介绍的最后一个可视化是散点图。散点图对于可视化两个变量之间的相关性非常有用:当存在线性相关性时，散点图中的对角线趋势会很明显。Incanter 包含用于这种图表的`c/scatter-plot`函数，其参数与`c/xy-plot`函数的参数相同。

```
(defn ex-1-33 []

  (let [data (load-data :uk-victors)]

    (-> (c/scatter-plot (i/$ :turnout data)

                        (i/$ :victors-share data)

                        :x-label "Turnout"

                        :y-label "Victor's Share")

        (i/view))))
```

前面的代码生成了下面的图表:

![Scatter plots](graphics/7180OS_01_350.jpg)

尽管这些点大致排列在一个模糊的椭圆中，但散点图右上角的对角线趋势非常明显。这表明一个有趣的结果——投票率与最终选举获胜者的投票比例相关。我们可能已经预料到了相反的情况:选民的自满导致了投票率的下降，而在选举中有明显的胜利者。

### 注意

如前所述，2010 年的英国大选很不寻常，产生了一个无多数派议会和一个联合政府。事实上，这种情况下的“赢家”代表了两个在选举日之前一直是对手的政党。对任何一方的投票都算作对获胜者的投票。

接下来，我们将为俄罗斯选举创建相同的散点图:

```
(defn ex-1-34 []

  (let [data (load-data :ru-victors)]

    (-> (c/scatter-plot (i/$ :turnout data)

                        (i/$ :victors-share data)

                        :x-label "Turnout"

                        :y-label "Victor's Share")

        (i/view))))
```

这产生了如下图:

![Scatter plots](graphics/7180OS_01_360.jpg)

尽管从这些点的轮廓可以清楚地看出俄罗斯数据的对角线趋势，但庞大的数据量掩盖了内部结构。在本章的最后一节，我们将向展示一个从图表中提取结构的简单技术，比如前面那个使用不透明度的图表。

## 散射透明度

在和前面的一样，散点图被大量的点淹没的情况下，透明度有助于可视化数据的结构。因为重叠的半透明点会更不透明，而点较少的区域会更透明，所以具有半透明点的散点图可以比实心点更好地显示数据的密度。

我们可以用`c/set-alpha`函数设置绘制在咒语图上的点的 alpha 透明度。它接受两个参数:图表和一个介于 0 和 1 之间的数字。一表示完全不透明，零表示完全透明。

```
(defn ex-1-35 []

  (let [data (-> (load-data :ru-victors)

                 (s/sample :size 10000))]

    (-> (c/scatter-plot (i/$ :turnout data)

                        (i/$ :victors-share data)

                        :x-label "Turnout"

                        :y-label "Victor Share")

        (c/set-alpha 0.05)

        (i/view))))
```

前面的示例生成了以下图表:

![Scatter transparency](graphics/7180OS_01_370.jpg)

前面的散点图显示了胜利者的份额和投票率一起变化的一般趋势。我们可以看到这两个值之间的相关性，在图表的右上角有一个“热点”,对应于接近 100%的投票率和 100%的选票支持获胜的政党。这个特别是维也纳医学大学的研究人员强调的选举舞弊的标志。这在世界各地其他有争议的选举结果中显而易见，比如 2011 年的乌干达总统选举。

### Tip

世界各地许多其他选举的区级结果可在 http://www . complex-systems . meduniwien . AC . at/elections/election . html 查阅。请访问该网站获取研究论文的链接，并下载其他数据集，在这些数据集上练习您在本章中学到的关于清理和转换真实数据的知识。

我们将在[第 3 章](ch03.html "Chapter 3. Correlation")、*相关性*中更详细地介绍相关性，届时我们将了解如何量化两个值之间的关系强度，并基于此建立预测模型。我们还将在第 10 章、*可视化*中重温这些数据，届时我们将实现一个自定义的二维直方图，以便更清楚地可视化投票率和获胜者投票比例之间的关系。



# 总结

在第一章中，我们学习了汇总统计和分布的价值。我们已经看到，即使是简单的分析也能提供潜在欺诈活动的证据。

特别是，我们遇到了中心极限定理，并看到了为什么它对解释数据科学中普遍存在的正态分布有如此大的帮助。一个合适的分布可以用很少的统计数据代表一个大的数字序列的本质，我们已经在本章中用纯 Clojure 函数实现了其中的几个。我们还介绍了 Incanter 库，并使用它来加载、转换和可视化比较几个数据集。然而，我们除了注意到两个发行版之间的奇怪差异之外，还能做的不多。

在下一章，我们将扩展我们所学的描述性统计，以涵盖推断性统计。这将允许我们量化两个或多个分布之间的测量差异，并决定差异是否具有统计显著性。我们还将了解假设检验——一种进行稳健实验的框架，允许我们从数据中得出结论。