

# 十、可视化

|   | “数字有一个重要的故事要讲。他们依靠你给他们一个清晰而令人信服的声音。” |   |
|   | - *斯蒂芬几* |

这本书的每一章都以某种方式利用了可视化，主要是使用咒语。Incanter 是一个有效的工具，可以在我们工作时生成各种各样的图表，这些图表通常是我们在试图理解数据集时首先要接触的。这个初始阶段通常被称为 **探索性数据分析**，在这个阶段，我们感兴趣的是总结统计数据，例如数字数据的分布、分类数据的计数以及数据中的属性是如何关联的。

找到一种有意义的方法来解释数据后，我们通常会想把它传达给其他人。沟通最重要的工具之一是可视化，我们可能需要向没有强大分析背景的人传达微妙或复杂的想法。在这一章中，我们将使用库 Quil——它源于为视觉艺术家开发的软件——来生成有吸引力的图形，这些图形有助于将数据变得生动。可视化和交流设计是很大很丰富的领域，我们在这里不会详细讨论。相反，本章将提供两个案例研究，展示如何将 Clojure 的数据抽象和 Quil 的绘图 API 结合使用以获得良好的效果。

我们将从头开始这一章，回到我们在[第 1 章](ch01.html "Chapter 1. Statistics")、*统计*中使用的数据。我们将通过演示如何从俄罗斯选举数据构建一个简单的二维直方图来介绍 Quil。在介绍了 Quil 中绘画的基础知识之后，我们将展示一些基本的绘画指导如何结合起来，产生一个引人注目的美国财富分布图。

# 下载代码和数据

在这一章中，我们将回到本书第一章中使用的数据:2011 年俄罗斯大选的数据。回到[第一章](ch01.html "Chapter 1. Statistics")、*统计*，我们使用了一个透明散点图来可视化选民投票率和胜利者的投票百分比之间的关系。在这一章中，我们将编写代码将数据呈现为二维直方图。

我们还将利用美国财富分配的数据。这些数据如此之小，以至于我们没有什么可下载的:我们将把这些数字直接输入源代码。

### 注意

本章的源代码可在 https://github.com/clojuredatascience/ch10-visualization 获得。

本章的示例代码包含一个脚本，用于下载我们在[第 1 章](ch01.html "Chapter 1. Statistics")、*统计*中使用的选举数据。下载源代码后，您可以通过在项目根目录下运行以下命令行来执行脚本:

```

script/download-data.sh

```

如果您之前下载了第 1 章、*统计数据*的数据，如果您愿意，您可以简单地将数据文件移到本章的数据目录中。



# 探索性数据可视化

在任何数据科学项目的开始，当您深入了解数据时，可能会有一段迭代的数据探索期。在这本书里，咒语是我们主要的可视化工具。尽管它包含了大量的图表，但有时它并不包含您想要表示的数据的理想图表。

### 注意

其他 Clojure 库正在提供探索性的数据可视化功能。关于的例子，请参见**clojurewerkz/envision**https://github.com/clojurewerkz/envision 和 Karsten Schmidt 的 **thi-ng/geom** 在[https://github.com/thi-ng/geom/tree/master/geom-viz](https://github.com/thi-ng/geom/tree/master/geom-viz)。

例如，回到[第 1 章](ch01.html "Chapter 1. Statistics")、*统计*，我们使用了一个具有 alpha 透明度的散点图来可视化投票人比例与获胜者投票比例。这不是一个理想的图表，因为我们主要对特定区域的点密度感兴趣。Alpha 透明性有助于揭示数据的结构，但它不是一种明确的表示。一些点仍然太虚假可见或太多，他们似乎是一个:

![Exploratory data visualization](graphics/7180OS_10_100.jpg)

我们可以用二维直方图解决这些问题。这种类型的绘图使用颜色在两个维度上传达高密度和低密度区域。图表被分成网格，网格中的每个单元格表示两个维度上的范围。落入网格单元的点越多，范围内的密度就越大。

## 表示二维直方图

一个直方图仅仅是一个连续分布到一系列区间的表示。直方图在[第一章](ch01.html "Chapter 1. Statistics")、*统计*中介绍，当时，我们编写了一个宁滨函数，将连续数据分成离散的箱:

```
(defn bin [n-bins xs]

  (let [min-x    (apply min xs)

        range-x  (- (apply max xs) min-x)

        max-bin  (dec n-bins)

        bin-fn   (fn [x]

                   (-> (- x min-x)

                       (/ range-x)

                       (* n-bins)

                       (int)

                       (min max-bin)))]

    (map bin-fn xs)))
```

这段代码将获取一系列连续的`xs`，并根据`n-bins`参数将它们分成不同的组。例如，将 0 和 19 之间的范围宁滨成 5 个二进制数，产生以下序列:

```
(defn ex-1-1 []

  (bin 5 (range 20)))

;;(0 0 0 0 1 1 1 1 2 2 2 2 3 3 3 3 4 4 4 4)
```

`bin` 函数返回每个数据点的容器索引，而不是计数，因此我们使用 Clojure 的`frequencies`函数来确定落入容器的点的计数:

```
(defn ex-1-2 []

  (frequencies (bin 5 (range 20))))

;;{0 4, 1 4, 2 4, 3 4, 4 4}
```

这是一维直方图的合理表示:作为要计数的箱的图。为了表示二维直方图，我们可以简单地对 *xs* 和 *ys* 执行相同的计算。我们将向量函数映射到 bin 索引上，以便将每个点转换成`[x-bin y-bin]`的表示:

```
(defn histogram-2d [xsys n-bins]

  (-> (map vector

           (bin n-bins xs)

           (bin n-bins ys))

      (frequencies)))
```

这个函数返回一个由两个值的向量作为关键字的映射。`frequencies`函数现在将计算共享一个 *x* 和一个 *y* 容器的所有点:

```
(defn ex-10-3 []

  (histogram-2d (range 20)

                (reverse (range 20)) 5))

;;{[0 4] 4, [1 3] 4, [2 2] 4, [3 1] 4, [4 0] 4}
```

我们希望用直方图绘制真实数据，所以让我们从第一章、*统计数据*加载俄罗斯数据。如果您已经将数据下载到示例代码的`data`目录中，您可以运行以下代码:

```
(defn ex-10-4 []

  (let [data (load-data :ru-victors)]

    (histogram-2d (i/$ :turnout data)

                  (i/$ :victors-share data) 5)))

;; {[4 3] 6782, [2 2] 14680, [0 0] 3, [1 0] 61, [2 3] 2593,

;;  [3 3] 8171, [1 1] 2689, [3 4] 1188, [4 2] 3084, [3 0] 64,

;;  [4 1] 1131, [1 4] 13, [1 3] 105, [0 3] 6, [2 4] 193, [0 2] 10,

;;  [2 0] 496, [0 4] 1, [3 1] 3890, [2 1] 24302, [4 4] 10771,

;;  [1 2] 1170, [3 2] 13384, [0 1] 4, [4 0] 264}
```

我们可以在柱状图箱中看到巨大的数值范围:从箱`[0 4]`中的 1 到箱`[2 1]`中的 24，302。这些计数将是我们绘制在直方图上的密度值。



# 使用 Quil 进行可视化

quil([https://github.com/quil/quil](https://github.com/quil/quil))是一个 Clojure 库，它提供了巨大的灵活性来生成定制的可视化。它包装了 Processing(【https://processing.org/】)，一个由视觉艺术家和设计师多年来积极开发的 Java 框架，旨在促进“视觉艺术中的软件素养和技术中的视觉素养”。

用 Quil 完成的任何可视化都包括创建一个*草图*。草图是 processing 的术语，指运行由绘图指令组成的程序。大多数 API 函数都可以从`quil.core`名称空间获得。我们将把它作为`q`包含在我们的代码中。不带任何参数调用`q/sketch`会弹出一个空窗口(尽管它可能会被其他窗口遮住)。

## 绘制到草图窗口

默认的窗口大小是 500 像素乘 300 像素。我们希望二维直方图是正方形的，所以让窗口在两个方向上都为 250 像素:

```
(q/sketch :size [250 250])
```

由于我们的两个轴各有 5 个面元，这意味着每个面元将由一个 50px 宽和 50px 高的正方形表示。

Quil 为绘图提供了标准的 2D 形状图元:点、线、弧、三角形、四边形、矩形和椭圆。为了绘制一个矩形，我们调用`q/rect`函数，指定位置为 *x* 和 *y* 坐标，以及宽度和高度。

让我们在原点画一个正方形，50px 宽。向 Quil 提供绘图指令有几种方法，但在本章中，我们将传递一个被称为`setup`的函数。这是一个没有参数的函数，我们传递给 sketch。我们的零参数函数简单地用位置[0，0]和宽度与高度 50 来调用`rect`:

```
(defn ex-10-5 []

  (let [setup #(q/rect 0 0 50 50)]

    (q/sketch :setup setup

              :size [250 250])))
```

该代码生成以下图像:

![Drawing to the sketch window](graphics/7180OS_10_110.jpg)

矩形可能不在你预期的位置，这取决于你对计算机图形的熟悉程度。

### 注意

也可以通过将半径作为第五个参数来绘制圆角矩形。通过将值作为参数 5 到 8 传递，可以为每个角使用不同的半径。

在我们继续之前，我们需要了解 Quil 的坐标系统。

## 奎尔坐标系

Quil 使用的坐标系与 processing 和大多数其他计算机图形程序相同。如果你不熟悉绘画，原点在显示屏的左上角可能看起来有些违背直觉。 *y* 轴在屏幕下方运行，而 *x* 轴向右运行。

显然，这不是大多数图表上的 *y* 轴的方向，这意味着在绘图时需要经常翻转 *y* 坐标。

![Quil's coordinate system](graphics/7180OS_10_120.jpg)

一种常见的方法是从草图的高度减去所需的 *y* 值(从草图底部测量)。这种变换导致零的 *y* 对应于草图底部的。较大的值 *y* 将对应于草图中较高的值。

## 绘制网格

让我们用一个简单的网格将这个付诸实践。下面的函数接受多个容器、`n-bins`和一个表示为矢量`[width height]`的`size`参数:

```
defn draw-grid [{:keys [n-bins size]}]

  (let [[width height] size

        x-scale (/ width n-bins)

        y-scale (/ height n-bins)

        setup (fn []

                (doseq [x (range n-bins)

                        y (range n-bins)

                        :let [x-pos (* x x-scale)

                              y-pos (- height

                                       (* (inc y) y-scale))]]

                  (q/rect x-pos y-pos x-scale y-scale)))]

    (q/sketch :setup setup :size size)))
```

由此，我们可以计算出`x-scale`和`y-scale`，这是一个使我们能够在 *x* 和 *y* 维度中将容器索引转换为像素偏移的因子。这些由我们的`setup`函数使用，该函数循环遍历 *x* 和 *y* 库，为每个库放置一个矩形。

### 注意

注意我们是如何在`doseq`中执行循环的。我们的绘图指令是作为副作用执行的。如果我们不这样做，Clojure 的懒惰评估将导致什么也画不出来。

前面的代码生成了下图:

![Plotting the grid](graphics/7180OS_10_130.jpg)

定义了前面的函数后，我们差不多创建了一个直方图。我们只需要用代表直方图中每个柱的适当值的颜色来给网格中的每个方块着色。为了实现这一点，我们还需要两个函数:一个从对应于 bin 的数据中获取值，另一个将这些值解释为颜色。

## 指定填充颜色

用`q/fill`功能在 Quil 中填充颜色。我们指定的任何填充都将继续使用，直到我们指定新的填充。

### 注意

Quil 中的许多函数影响当前的绘图上下文，并且是*有状态的*。例如，当我们指定一个填充值时，它将用于所有后续的绘图指令，直到填充被改变。其他示例包括填充、描边、缩放和字体。

以下代码是我们的`draw-grid`函数的改编版本。对`draw-filled-grid`的补充是`fill-fn`:在网格的某一点给矩形着色的某种方式。`fill-fn`函数应该是两个参数的函数，即容器的 *x* 和 *y* 索引。它应该返回一个 Quil 可以用作填充的表示:

```
(defn draw-filled-grid [{:keys [n-bins size fill-fn]}]

  (let [[width height] size

        x-scale (/ width n-bins)

        y-scale (/ height n-bins)

        setup (fn []

                (doseq [x (range n-bins)

                        y (range n-bins)

                        :let [x-pos (* x x-scale)

                              y-pos (- height

                                       (* (inc y) y-scale))]]

                  (q/fill (fill-fn x y))

                  (q/rect x-pos y-pos x-scale y-scale)))]

    (q/sketch :setup setup :size size)))
```

Quil 的 fill 函数接受多个算术:

*   **一个参数**:RGB 值(作为数字或`q/color`表示)
*   **两个参数**:RGB，和一个参数一样，加上一个 alpha 透明度
*   **三个参数**:颜色的红、绿、蓝分量，为 0 到 255 之间的数字
*   **四个自变量**:红色、绿色、蓝色和阿尔法分量作为数字

我们将很快看到如何使用`color`表示法，但是现在，我们将用一个简单的数字表示法来表示颜色:0 到 255 之间的一个数字。当相同的数字用于红色、绿色和蓝色时(或者当用一个或两个参数调用`fill`时)，我们得到一个灰色。0 对应黑色，255 对应白色。

如果我们将直方图中每个柱中值的频率除以最大值，我们将得到一个介于 0 和 1.0 之间的数。乘以 255 将得到一个值，Quil 会将它转换为灰色。我们在下面的`fill-fn`实现中这样做，将它传递给我们之前定义的`draw-filled-grid`函数:

```
(defn ex-10-6 []

  (let [data (load-data :ru-victors)

        n-bins 5

        hist (histogram-2d (i/$ :turnout data)

                           (i/$ :victors-share data)

                           n-bins)

        max-val (apply max (vals hist))

        fill-fn (fn [x y]

                  (-> (get hist [x y] 0)

                      (/ max-val)

                      (* 255)))]

    (draw-filled-grid {:n-bins n-bins

                       :size [250 250]

                       :fill-fn fill-fn})))
```

前面的代码生成如下图形:

![Specifying the fill color](graphics/7180OS_10_140.jpg)

图表正在做我们想要的，但它是我们数据的一个非常粗糙的表示。让我们增加仓的数量，以提高直方图的分辨率:

```
(defn ex-10-7 []

  (let [data (load-data :ru-victors)

        n-bins 25

        hist (histogram-2d (i/$ :turnout data)

                           (i/$ :victors-share data)

                           n-bins)

        max-val (apply max (vals hist))

        fill-fn (fn [x y]

                  (-> (get hist [x y] 0)

                      (/ max-val)

                      (* 255)))]

    (draw-filled-grid {:n-bins n-bins

                       :size [250 250]

                       :fill-fn fill-fn})))
```

该代码生成如下图形:

![Specifying the fill color](graphics/7180OS_10_150.jpg)

沿着每个 *x* 和 *y* 轴有 25 个矩形，我们有一个更细粒度的数据结构图。然而，一个副作用是它变得很难辨别直方图中的细节，因为大多数细胞都很暗。部分问题是右上角的值太高，以至于中心区域(以前是最亮的)现在也只是一个灰色的污点。

这个问题有两种解决方案:

*   通过绘制*z*-分数而不是实际值来减轻异常值的影响
*   通过使用更大范围的颜色使视觉队列的范围多样化

我们将在下一节中发现如何将值转换为全光谱颜色，但是首先，让我们将直方图值转换为一个 *z* 分数。绘制*z*-分数是一种分布感知的图表着色方式，这将大大减少右上角极端异常值的影响。有了一个*z*-分数，我们将绘制每个单元格偏离平均值的标准偏差数。

为了做到这一点，我们需要知道两件事:直方图中频率的均值和标准差:

```
(defn ex-10-8 []

  (let [data (load-data :ru-victors)

        n-bins 25

        hist (histogram-2d (i/$ :turnout data)

                           (i/$ :victors-share data)

                           n-bins)

        mean (s/mean (vals hist))

        sd   (s/sd   (vals hist))

        fill-fn (fn [x y]

                  (-> (get hist [x y] 0)

                      (- mean)

                      (/ sd)

                      (q/map-range -1 3 0 255)))]

    (draw-filled-grid {:n-bins n-bins

                       :size [250 250]

                       :fill-fn fill-fn})))
```

前面的代码从直方图的每个值中减去平均值，然后除以平均值。这将产生一个平均值为零的值。`1`表示平均值的一个标准差，`2`表示平均值的两个标准差，依此类推。

Quil 公开了一个有用的`map-range`函数，它将一个范围的值映射到另一个范围的值。例如，我们可以取标准偏差的期望范围(在前面的例子中为-1 到 3)，并将它们映射到范围 0 到 255。这将对应于被表示为从黑到白的全灰度范围的分布的四个标准偏差。任何超出这个范围的数据都会被简单地剪切掉。

![Specifying the fill color](graphics/7180OS_10_160.jpg)

结果是一个更加醒目的灰度数据表示。使用*z*-分数在直方图的主体中带来了更多的细节，我们可以察觉到尾部的更多变化。

然而，直方图仍然不尽如人意，因为区分不同的灰色阴影具有挑战性。当单元格彼此不相邻时，很难确定它们是否共享相同的值。

我们可以利用颜色来代表每个单元格，从而扩大我们的工作范围。这使得直方图更像热图:“冷”色如蓝色和绿色代表低值，而“热”色如橙色和红色代表热图中最密集的区域。

## 颜色和填充

为了创建二维直方图的热度映射版本，我们必须获取我们的 *z* 分数，并找到某种方法将其映射到一个颜色值。我们的热图应该有一个包含光谱中所有颜色的平滑调色板，而不是显示一个离散的调色板，比如说 5 种颜色。

### 注意

对于阅读印刷书籍或黑白书籍的人来说，你可以从 Packt Publishing 的网站[https://www . packtpub . com/sites/default/files/downloads/clo jure _ for _ Data _ Science _ color images . pdf](https://www.packtpub.com/sites/default/files/downloads/Clojure_for_Data_Science_ColorImages.pdf)下载彩色图像。

这正是 Quil 函数`q/lerp-color`所做的。给定两种颜色和一个介于零和一之间的比率，`lerp-color`将返回一个介于两者之间的新颜色。数量为 0 将返回第一种颜色，数量为 1 将返回第二种颜色，而数量为 0.5 将返回介于两种颜色之间的颜色:

```
(defn z-score->heat [z-score]

  (let [colors [(q/color 0 0 255)   ;; Blue

                (q/color 0 255 255) ;; Turquoise

                (q/color 0 255 0)   ;; Green

                (q/color 255 255 0) ;; Yellow

                (q/color 255 0 0)]  ;; Red

        offset  (-> (q/map-range z-score -1 3 0 3.999)

                    (max 0)

                    (min 3.999))]

    (q/lerp-color (nth colors offset)

                  (nth colors (inc offset))

                  (rem offset 1))))
```

这段代码让按照光谱的顺序使用一组颜色。我们使用`q/map-range`来确定我们将在哪两种颜色之间进行插值，并使用范围的浮点部分调用`q/lerp-color`。

我们已经实现了一个`draw-filled-grid`函数，它接受`fill-fn`来决定哪种颜色应该用来填充网格。现在让我们把我们的`z-score->heat`函数传递给它:

```
(defn ex-10-9 []

  (let [data (load-data :ru-victors)

        n-bins 25

        hist (histogram-2d (i/$ :turnout data)

                           (i/$ :victors-share data)

                           n-bins)

        mean (s/mean (vals hist))

        sd   (s/sd   (vals hist))

        fill-fn (fn [x y]

                  (-> (get hist [x y] 0)

                      (- mean)

                      (/ sd)

                      (z-score->heat)))]

    (draw-filled-grid {:n-bins n-bins

                       :size [250 250]

                       :fill-fn fill-fn})))
```

该代码生成如下图形:

![Color and fill](graphics/7180OS_10_170.jpg)

热图暴露了更多的数据内部结构。特别是，虽然数据的强对角线形状仍然明显，但我们现在可以看到其中更多的变化。以前难以确定的细节(要么是因为这个区域太密集，要么是因为太稀疏)变得更加明显。

## 输出图像文件

既然我们对直方图感到满意，我们想输出一个高质量的版本。通过在设置函数中添加一个对`q/save`的调用，传递一个文件名，Quil 将输出到一个文件以及屏幕上。创建的图像格式取决于文件名后缀:TIFF 文件使用`.tif`，JPEG 文件使用`.jpg`，PNG 文件使用`.png`，TARGA 文件使用`.tga`:

```
(defn draw-filled-grid [{:keys [n-bins size fill-fn]}]

  (let [[width height] size

        x-scale (/ width n-bins)

        y-scale (/ height n-bins)

        setup (fn []

                (doseq [x (range n-bins)

                        y (range n-bins)

                        :let [x-pos (* x x-scale)

                              y-pos (- height

                                       (* (inc y) y-scale))]]

                  (q/fill (fill-fn x y))

                  (q/rect x-pos y-pos x-scale y-scale))

                  (q/save "heatmap.png"))]

    (q/sketch :setup setup :size size)))
```

我们还能够输出到 PDF，我们将在下一个可视化中展示。



# 可视化交流

在我们作为数据科学家的工作过程中，我们可能会发现自己需要与各种各样的人交流。我们的亲密同事和经理也许能读懂我们的咒语图表，但他们不太可能打动首席执行官。我们可能还有一个角色需要我们与公众沟通。

无论是哪种情况，我们都应该专注于制作简单而强大的可视化，但不牺牲数据的完整性。缺乏统计训练并不妨碍我们理解微妙和微妙的论点，我们应该尊重听众的智力。作为数据科学家，我们面临的挑战是找到一种能够向他们有效传达信息的表达方式。

在本章的剩余部分，我们将致力于一种可视化，旨在以简洁和忠实的方式交流一组更复杂的数据。

### 注意

我们将要创建的可视化是 https://www.youtube.com/watch?v=QPKKQnijnsM的**美国财富不平等**在线视频中的一个图表版本。由匿名电影制作人 Politizane 制作的这个强有力的视频在 YouTube 上已经获得了超过 1600 万的点击量。

像这样的图形表示经常出现的情况一样，我们的数据将来自几个不同的来源。

## 可视化财富分布

我们将利用的第一个数据集来自加州大学圣克鲁斯分校心理学和社会学研究教授 g .威廉·多姆霍夫的一篇文章。我们接下来要引用的数字来自 http://www2.ucsc.edu/whorulesamerica/power/wealth.html 的一篇题为**财富、收入和权力**的文章。

尽管整篇文章都很值得一读，但一个特别引人注目的图表是一个饼状图，显示了 2010 年美国人的金融净资产明细:

![Visualizing wealth distribution](graphics/7180OS_10_180.jpg)

这个饼状图引人注目有几个原因。首先，超过 40%的金融财富掌握在如此小的一部分人手中的概念很难理解。第二，馅饼的每一片不仅代表了数量差异极大的财富，也代表了数量差异极大的人口:从 1%的人口到 80%的人口。众所周知，即使在最好的情况下，饼状图也很难读懂，所以这张图更具挑战性。

### 注意

饼图通常不是表示数据的好方法，即使总数在概念上表示整体的一部分。作家兼程序员史蒂夫·芬顿记录了许多原因，并在 https://www.stevefenton.co.uk/2009/04/pie-charts-are-bad/提供了适当的替代方案。

让我们看看如何重新解释这些数据，使其更容易理解。首先，让我们提取下表中列出的数字:

| 

百分位

 | 

2010 年金融财富总额

 |
| --- | --- |
| 0-79 | 5% |
| 80-89 | 11% |
| 90-95 | 13% |
| 96-99 | thirty percent |
| One hundred | 42% |

对饼图的一个小改进是用条形图表示相同的数据。虽然人们通常很难成功地解释饼图各部分的相对大小，但条形图不存在这样的问题。下一个示例简单地用以前的数字创建了一个条形图:

```
(defn ex-10-10 []

  (let [categories ["0-79" "80-89" "90-95" "96-99" "100"]

        percentage [5      11      13      30      42   ]]

    (-> (c/bar-chart categories percentage

                     :x-label "Category"

                     :y-label "% Financial Wealth")

        (i/view))))
```

这将返回以下图表:

![Visualizing wealth distribution](graphics/7180OS_10_190.jpg)

这是对饼图的改进，因为它更容易比较类别的相对大小。然而，一个重要的问题仍然存在:每个类别所代表的人数差异如此之大。左边的条形代表 80%的人口，而右边的条形代表 1%的人口。

如果我们想让这些数据更容易理解，我们可以把总数分成 100 个相等的单元，每个单元代表人口的 1 个百分点。每个条形的宽度可以根据它所代表的百分位数进行调整，同时保留其面积。由于每个百分位单位代表相同数量的人，结果图表将允许我们更容易地进行组间比较。

实现这一点的一种方法是返回一个包含 100 个元素的序列，每个元素代表人口的一个百分位。序列中每个元素的价值就是百分位占总财富的比例。我们已经知道，前 1%的人拥有 42%的总财富，但其他群体的价值会根据他们所跨越的百分点数向下调整:

```
(def wealth-distribution

  (concat (repeat 80 (/ 5  80))

          (repeat 10 (/ 11 10))

          (repeat 5  (/ 13 5))

          (repeat 4  (/ 30 4))

          (repeat 1  (/ 42 1))))

(defn ex-10-11 []

  (let [categories (range (count wealth-distribution))]

    (-> (c/bar-chart categories wealth-distribution

                     :x-label "Percentile"

                     :y-label "% Financial Wealth")

        (i/view))))
```

此示例生成以下条形图:

![Visualizing wealth distribution](graphics/7180OS_10_200.jpg)

通过应用一个简单的转换，我们能够更好地理解真正的分布。现在，每个条形代表相同比例的人口，每个条形的面积代表百分位数拥有的财富比例。

## 使用 Quil 将数据带入生活

上一节中的转换产生了一个图表，该图表几乎过于明显地显示了标尺两端之间的差异:除了最大的柱线之外，很难解释任何东西。一种解决方案是像我们在书中其他地方所做的那样，用对数标度或双对数标度来显示数字。如果这个图表的观众有统计学知识，这可能是最合适的做法，但是让我们假设我们可视化的目标观众是普通大众。

之前展示的图表的问题是，最右边的条形太大了，以至于盖过了所有其他的条形。80%的区域仅仅由几个像素表示。在下一节中，我们将利用 Quil 来生成一个可视化，它可以更好地利用空间，同时保持图表的完整性。

## 绘制不同宽度的线条

在接下来的几个部分，我们将分阶段建立一个可视化。因为我们将绘制一个 Quil 草图，我们将首先定义一些常量，这些常量将允许我们产生与草图尺寸相关的绘图指令。为了简洁起见，下一段代码中省略了一些常量:

```
(def plot-x 56)

(def plot-y 60)

(def plot-width 757)

(def plot-height 400)

(def bar-width 7)
```

有了这些，我们可以开始用一种更容易理解的方式来表示条形图。下面的代码获取财富分布，并将除最后一个条形以外的所有条形绘制成一系列矩形。计算出的*y*-刻度是为了让我们将要绘制的最大横条填满绘图的高度:

```
(defn draw-bars []

  (let [pc99    (vec (butlast wealth-distribution))

        pc1     (last wealth-distribution)

        y-max   (apply max pc99)

        y-scale (fn [x] (* (/ x y-max) plot-height))

        offset  (fn [i] (* (quot i 10) 7))]

    (dotimes [i 99] ;; Draw the 99%

      (let [bar-height (y-scale (nth pc99 i))]

        (q/rect (+ plot-x (* i bar-width) (offset i))

                (+ plot-y (- plot-height bar-height))

                bar-width bar-height)))

    (let [n-bars 5  ;; Draw the 1%

          bar-height (y-scale (/ pc1 n-bars))]

      (q/rect (+ plot-x (* 100 bar-width) (offset 100))

              (+ plot-y (- plot-height bar-height))

              (* bar-width n-bars) bar-height))))
```

到目前为止，我们画的线条代表了 99%。最后一个条形代表最后 1%的人口。因此，它符合我们设计的垂直比例，而不会从草图的顶部消失，我们将在保留其面积的同时相应地使条形变宽。因此，该条比其他条短 5 倍，但也宽 5 倍:

```
(defn ex-10-12 []

  (let [size [960 540]]

    (q/sketch :size size

              :setup draw-bars)))
```

示例输出如下图形:

![Drawing bars of differing widths](graphics/7180OS_10_210.jpg)

我们已经可以更清楚地看到最大棒线之间的关系，但还不能清楚地看出这是一个图表。在下一节中，我们将添加文本来标识图表的主题和轴的范围。

## 添加标题和轴标签

专门的可视化工具如 Incanter 的一个方便之处是可以为我们的图表自动生成轴。Quil 在这里没有为我们提供任何帮助，但是因为条形宽度是已知的，所以对我们来说实现起来并不困难。在下面的代码中，我们将利用`text`、`text-align`、`text-size`函数将文本写入我们的可视化:

```
(defn group-offset [i]

  (* (quot i 10) 7))

(defn draw-axis-labels []

  (q/fill 0)

  (q/text-align :left)

  (q/text-size 12)

  (doseq [pc (range 0 (inc 100) 10)

          :let [offset (group-offset pc)

                x      (* pc bar-width)]]

    (q/text (str pc "%") (+ plot-x x offset) label-y))

    (q/text "\"The 1%\"" pc1-label-x  pc1-label-y))
```

通过使用非专业的图表库，我们失去了什么呢？我们获得了灵活性。接下来，我们将编写一个函数来在文本上产生凸版印刷样式的浮雕:

```
(defn emboss-text [text x y]

  (q/fill 255)

  (q/text text x y)

  (q/fill 100)

  (q/text text x (- y 2)))

(defn draw-title []

  (q/text-size 35)

  (q/text-leading 35)

  (q/text-align :center :top)

  (emboss-text "ACTUAL DISTRIBUTION\nOF WEALTH IN THE US"

               title-x title-y))
```

我们使用`emboss-text`函数在图表中心绘制一个大标题。请注意，我们还指定了文本与位置的对齐方式，可选地从文本的顶部、底部、中心、左侧或右侧进行测量:

```
(defn ex-10-13 []

  (let [size [960 540]]

    (q/sketch :size size

              :setup #((draw-bars)

                       (draw-axis-labels)

     (draw-title)))))
```

前面的示例生成了下图:

![Adding a title and axis labels](graphics/7180OS_10_220.jpg)

这个图表是条形高度和面积的混合，在标准图表应用程序中很难实现定制的文本可视化。使用 Quil，我们有了一个工具箱，可以轻松自由地混合图形和数据。

## 用插图提高清晰度

我们的图表有所进展，但目前还很缺乏。增加视觉趣味的一种方法是使用图像。在示例项目的 resources 目录中，有两个 SVG 图像文件。一个是人物图标，另一个是来源于维基百科的美国地图。

### 注意

Wikipedia 包含在灵活的知识共享许可下发布的各种 SVG 地图。比如美国地图在[https://commons . wikimedia . org/wiki/Category:SVG _ maps _ of _ the _ United _ States](https://commons.wikimedia.org/wiki/Category:SVG_maps_of_the_United_States)。

我们在本章中使用的地图可从 https://commons . wikimedia . org/wiki/File:Blank _ US _ Map，_ continental _ with _ no _ States . SVG 获得，由 Lokal_Profil 根据 CC-BY-SA-2.5 许可提供。

在 Quil 中使用 SVG 图像是一个两步过程。首先，我们必须使用`q/load-shape`将图像加载到内存中。这个函数接受一个参数:要加载的 SVG 文件的路径。接下来，我们必须在屏幕上绘制图像。这是通过使用`q/shape` 函数来完成的，该函数需要图像的 *x* ， *y* 位置以及可选的宽度和高度。如果我们使用基于像素的图像，如 JPEGs 或 PNGs，我们将使用相应的`q/load-image`和`q/image`函数:

```
(defn draw-shapes []

  (let [usa    (q/load-shape "resources/us-mainland.svg")

        person (q/load-shape "resources/person.svg")

        colors [(q/color 243 195 73)

                (q/color 231 119 46)

                (q/color 77  180 180)

                (q/color 231 74  69)

                (q/color 61  76  83)]]

    (.disableStyle usa)

    (.disableStyle person)

    (q/stroke 0 50)

    (q/fill 200)

    (q/shape usa 0 0)

    (dotimes [n 99]

      (let [quintile (quot n 20)

            x (-> (* n bar-width)

                  (+ plot-x)

                  (+ (group-offset n)))]

        (q/fill (nth colors quintile))

        (q/shape person x icons-y icon-width icon-height)))

        (q/shape person

             (+ plot-x (* 100 bar-width) (group-offset 100))

             icons-y icon-width icon-height)))
```

在这段代码中，我们在`usa`和`person`形状上都调用了`.disableStyle`。这是因为 SVG 文件可能包含嵌入样式，如填充颜色、描边颜色或边框宽度信息，这些信息会影响 Quil 绘制形状的方式。我们想完全控制我们的表现，所以我们选择禁用所有的风格。

另外，请注意，我们加载了一次人物形状，并用`dotimes`绘制了多次。我们根据用户所处的`quintile` 来设置颜色:

```
(defn ex-10-14 []

  (let [size [960 540]]

    (q/sketch :size size

              :setup #((draw-shapes)

                       (draw-bars)

                       (draw-axis-labels)

                       (draw-title)))))
```

结果如下图所示:

![Improving the clarity with illustrations](graphics/7180OS_10_230.jpg)

这幅图开始看起来像是一幅我们可以展示给人们而不会脸红的图。人物图标有助于传达这样一种理念，即每个条形代表人口的百分位数。酒吧还不是很吸引人。既然每个条代表每个人的财富，那我们就把每个条代表一堆钞票。虽然这可能看起来是一个过于字面的解释，但它实际上会更清楚地表明，1%棒线实际上是其他人的 5 倍宽。

## 向条中添加文本

现在，我们可以把钞票画成一系列矩形，这应该不足为奇了:

```
(defn banknotes [x y width height]

  (q/no-stroke)

  (q/fill 80 127 64)

  (doseq [y (range (* 3 (quot y 3)) (+ y height) 3)

          x (range x (+ x width) 7)]

    (q/rect x y 6 2)))
```

前面代码中唯一稍微复杂的地方是需要将起始 *y* 位置调整为`3`的偶数倍。这将确保所有的钞票在偶数倍数后都与 *x* 轴相交，而与 *y* 轴上的杆的高度无关。这是从上到下绘制条的副作用，而不是相反。

在下面的例子中，我们将前面的函数添加到我们的草图中:

```
(defn ex-10-15 []

  (let [size [960 540]]

    (q/sketch :size size

              :setup #((draw-shapes)

                       (draw-banknotes)

                       (draw-axis-labels)

                       (draw-title)))))
```

这将生成以下图表:

![Adding text to the bars](graphics/7180OS_10_240.jpg)

这是一张相当完整的图表，代表了美国财富的实际分布。早先提供的原始 YouTube 视频链接的优点之一是，它将实际分布与其他几种分布进行了对比:人们预期的财富分布和他们更喜欢的财富分布。

## 合并附加数据

哈佛商学院教授迈克尔·诺顿和行为经济学家丹·艾瑞里对 5000 多名美国人进行了一项研究，以评估他们对财富分配的看法。当他们看到各种财富分配的例子，并被要求识别哪一个来自美国时，大多数人选择了比实际更平衡的分配。当被要求选择他们理想的财富分配时，92%的人选择了一个更加公平的。

下图显示了这项研究的结果:

![Incorporating additional data](graphics/7180OS_10_250.jpg)

上图由 Mother Jones 在[http://www . Mother Jones . com/politics/2011/02/income-inequality-in-America-chart-graph](http://www.motherjones.com/politics/2011/02/income-inequality-in-america-chart-graph)上发布，数据来源于[http://www . people . HBS . edu/MnO rton/Norton % 20 ariely % 20 in % 20 press . pdf](http://www.people.hbs.edu/mnorton/norton%20ariely%20in%20press.pdf)。

前面的图表很好地显示了 5 个五分位数中每个五分位数的人们的感知和现实之间的相对差异。我们将把这些数据转换成另一种表示形式，这样，像以前一样，我们可以把数据转换成表格表示形式。

通过阅读之前的图表并参考相关论文，我得出了以下按五分位划分的大致分类:

| 

五分之一对座

 | 

理想百分比

 | 

预期百分比

 | 

实际百分比

 |
| --- | --- | --- | --- |
| 第 100 次 | 32.0% | 58.5% | 84.5% |
| 第 80 届 | 22.0% | 20.0% | 11.5% |
| 第 60 届 | 21.5% | 12.0% | 3.7% |
| 第四十名 | 14.0% | 6.5% | 0.2% |
| 第二十；二十号 | 10.5% | 3.0% | 0.1% |

让我们看看*理想*和*预期*分布，并找到一种方法在我们现有的财富分布图上绘制它们。我们的柱状图已经代表了不同百分位数作为一个区域的相对财富。为了使这两个数据集具有可比性，我们也应该对这些数据进行同样的处理。前面的表格已经将数据表示为五个大小相等的组，因此我们不需要像对来自饼图的数据那样应用转换。

但是，让我们以此为契机，了解更多关于在 Quil 中绘制复杂形状的知识，并看看我们是否可以获得更像下图的数据表示:

![Incorporating additional data](graphics/7180OS_10_260.jpg)

该表提供了我们想要用标记为 **A** 、 **B** 和 **C** 的形状来表示的相对区域。为了绘制早期的形状，我们必须计算高度 *x* 、 *y* 和 *z* 。这将给我们坐标，我们可以在我们的图表上绘制。

区域 **A** 、 **B** 、 **C** 的宽度为 **w** 。因此， **x** 和 **w** 的乘积将等于 **A** 的面积:

![Incorporating additional data](graphics/7180OS_10_01.jpg)![Incorporating additional data](graphics/7180OS_10_02.jpg)

由此得出 *x* 的高度就是 *A* 的面积除以 *w* 。 *Y* 稍微复杂一点，但是不多。 *B* 的三角形分量的面积等于:

![Incorporating additional data](graphics/7180OS_10_03.jpg)

因此:

![Incorporating additional data](graphics/7180OS_10_04.jpg)

我们可以用同样的方法计算 *z* :

![Incorporating additional data](graphics/7180OS_10_05.jpg)

扩展我们的定义给出了下面的公式:

![Incorporating additional data](graphics/7180OS_10_06.jpg)

如果我们假设 *w* 是 1(我们所有的五分位数都是一个恒定的宽度)，那么我们得到下面的等式，并且对于任意数量的部分以此类推:

![Incorporating additional data](graphics/7180OS_10_07.jpg)![Incorporating additional data](graphics/7180OS_10_08.jpg)![Incorporating additional data](graphics/7180OS_10_09.jpg)

这可以表示为简单的递归函数。我们的第一个比例将被赋值为 *x* 。随后的值可以根据它计算如下:

```
(defn area-points [proportions]

  (let [f (fn [prev area]

            (-> (- area prev)

                (* 2)

                (+ prev)))

        sum (reduce + proportions)]

    (->> (reductions f (first proportions) proportions)

         (map #(/ % sum)))))
```

`reductions`函数的行为与`reduce`完全一样，但是保留了我们计算的中间步骤。我们将得到一系列对应于我们的 *y* 坐标高度的值，而不是一个单一的值。

## 绘制复杂形状

上一节定义的`area-points`函数将为 us 提供一系列点来绘图。然而，我们还没有介绍 Quil 中允许我们绘制它们的函数。为了画线，我们可以使用`q/line`函数。line 函数将接受起点和终点坐标，并在它们之间绘制一条直线。我们可以用这种方式构建一个面积图，但是它没有填充。线条只是简单地描述轮廓；我们不能像制作直方图时用`q/rect` 那样用它们来构建彩色形状。为了给我们的形状填充颜色，我们需要一次建立一个顶点。

要用 Quil 构建任意复杂的形状，我们首先调用`q/begin-shape`。这是一个有状态函数，它让 Quil 知道我们想要开始构建一系列顶点。对`q/vertex`的后续调用将与我们正在构建的形状相关联。最后，调用`q/end-shape`将完成这个形状。我们将使用笔画绘制它，并填充当前绘图上下文中指定的样式。

让我们通过使用上一节定义的`area-points`函数绘制一些测试图形来看看它是如何工作的:

```
(defn plot-area [proportions px py width height]

  (let [ys      (area-points proportions)

        points  (map vector (range) ys)

        x-scale (/ width (dec (count ys)))

        y-scale (/ height (apply max ys))]

    (q/stroke 0)

    (q/fill 200)

    (q/begin-shape)

    (doseq [[x y] points]

      (q/vertex (+ px (* x x-scale))

                (- py (* y y-scale))))

      (q/end-shape)))

(defn ex-10-16 []

  (let [expected [3 6.5 12 20 58.5]

        width  640

        height 480

        setup (fn []

                (q/background 255)

                (plot-area expected 0 height width height))]

    (q/sketch :setup setup :size [width height])))
```

本例使用之前定义的面积点函数绘制了`[3 6.5 12 20 58.5]`系列。这是美国财富分布`expected`数据表中列出的一系列百分比值。`plot-area`函数调用`begin-shape`，迭代`area-points`返回的 *ys* 序列，并调用`end-shape`。结果如下:

![Drawing complex shapes](graphics/7180OS_10_270.jpg)

这不完全是我们想要的。虽然我们要求填充形状，但我们并没有描述要填充的整个形状。Quil 不知道我们要如何封闭形状，所以它只是简单地从最后一个点画一条边回到第一个点，穿过图表的对角线。幸运的是，通过确保图表的两个底角都有点，这个问题可以很容易地解决:

```
(defn plot-full-area [proportions px py width height]

  (let [ys      (area-points proportions)

        points  (map vector (range) ys)

        x-scale (/ width (dec (count ys)))

        y-scale (/ height (apply max ys))]

    (q/stroke 0)

    (q/fill 200)

    (q/begin-shape)

    (q/vertex 0 height)

    (doseq [[x y] points]

      (q/vertex (+ px (* x x-scale))

                (- py (* y y-scale))))

    (q/vertex width height)

    (q/end-shape)))

(defn ex-10-17 []

  (let [expected [3 6.5 12 20 58.5]

        width  640

        height 480

        setup (fn []

                (q/background 255)

                (plot-full-area expected 0 height width height))]

    (q/sketch :setup setup :size [width height])))
```

`plot-full-area`函数在迭代 *ys* 的序列之前和之后增加一个对`vertex`的额外调用。指定的点确保了在调用`end-shape`之前形状被完全描述。结果如下图所示:

![Drawing complex shapes](graphics/7180OS_10_280.jpg)

这是更好的，它开始看起来像一个面积图。在下一节中，我们将介绍如何使用曲线来描述更复杂的形状。虽然我们的面积图不需要曲线，但它将有助于使结果更有吸引力。

## 绘制曲线

面积图开始看起来不错，但是我们可以通过使用 Quil 的样条曲线去掉那些尖角。我们可以调用`q/curve-vertex`来平滑边之间的连接，而不是通过添加顶点来构建形状。

`q/curve-vertex`函数实现了一种称为 Catmull-Rom 样条的曲线绘制方法。要绘制一条曲线，我们必须指定至少四个顶点:第一个和最后一个顶点将被视为控制点，曲线将在中间两个顶点之间绘制。

我们在下图中可视化 Catmull-Rom 样条如何工作，该图显示了由点 *a* 、 *b* 、 *c* 和 *d* 指定的路径:

![Drawing curves](graphics/7180OS_10_290.jpg)

曲线在点 **c** 的切线平行于 **X** :点 **a** 和 **b** 之间的直线；曲线在 **b** 处的切线与 **Y** 平行:由点 **c** 和 **d** 描绘的直线。因此，要画一条曲线，我们需要确保在直线的起点和终点添加这些额外的控制点。每个控制点都添加了`curve-vertex`，我们在迭代我们的点之前调用一次，然后在最后再调用一次:

```
(defn smooth-curve [xs ys]

  (let [points (map vector xs ys)]

    (apply q/curve-vertex (first points))

    (doseq [point points]

      (apply q/curve-vertex point))

    (apply q/curve-vertex (last points))))
```

现在我们已经定义了一个`smooth-curve`函数，我们将在下面的两个函数中使用它，`smooth-stroke`和`smooth-area`:

```
(defn smooth-stroke [xs ys]

  (q/begin-shape)

  (q/vertex (first xs) (first ys))

  (smooth-curve (rest xs) (rest ys))

  (q/end-shape))

(defn smooth-area [xs ys]

  (q/begin-shape)

  (q/vertex (first xs) (first ys))

  (smooth-curve (rest xs) (rest ys))

  (q/vertex (last xs) (first ys))

  (q/end-shape))
```

`smooth-stroke`函数将通过为 *xs* 和 *ys* 创建顶点来绘制它们定义的形状。`smooth-area`函数扩展了这一点，它封闭了形状，避免了我们之前看到的对角填充的情况。将这两个函数结合在一起的是`plot-curve`，这个函数接受要绘制的 *xs* 和 *ys* ，以及要使用的填充颜色、描边颜色和描边粗细:

```
(defn plot-curve [xs ys fill-color

                  stroke-color stroke-weight]

  (let [points (map vector xs ys)]

    (q/no-stroke)

    (q/fill fill-color)

    (smooth-area xs ys)

    (q/no-fill)

    (q/stroke stroke-color)

    (q/stroke-weight stroke-weight)

    (smooth-stroke xs ys)))
```

让我们对之前绘制的预期值序列调用`plot-curve`函数，并比较不同之处:

```
(defn plot-smooth-area [proportions px py width height]

  (let [ys      (cons 0 (area-points proportions))

        points  (map vector (range) ys)

        x-scale (/ width (dec (count ys)))

        y-scale (/ height (apply max ys) -1)]

    (plot-curve (map (point->px px x-scale) (range (count ys)))

                (map (point->px py y-scale) ys)

                (q/color 200)

                (q/color 0) 2)))

(defn ex-10-18 []

  (let [expected [3 6.5 12 20 58.5]

        width  640

        height 480

        setup (fn []

                (q/background 255)

                (plot-smooth-area expected 0 height

                                  width height))]

    (q/sketch :setup setup :size [width height])))
```

这个例子生成了下面的图像:

![Drawing curves](graphics/7180OS_10_300.jpg)

曲线的效果是微妙的，但它为我们的图表提供了一个否则将会缺乏的润色。上图显示了诺顿和艾瑞里研究得出的*预期*财富分布。在我们将此与我们之前创建的*实际*财富分布图结合之前，让我们看看它如何与来自同一项研究的*理想*财富分布相结合。

## 绘制复合图表

前面的描述展示了如何创建一个适合一个区域的曲线图形。正如我们所定义的，`plot-smooth-area`函数将填充我们为每个绘制区域指定的高度。从绘图的角度来看，这是有意义的，但在试图绘制两个可比较的图表时，这是没有意义的:我们需要确保它们使用相同的比例。

在下一个代码块中，我们将根据两个图形中较大的一个来计算一个比例，然后使用这个比例来绘制两个图形。这确保了我们绘制的所有系列都可以相互比较。组合图表将填充我们分配给它的宽度和高度:

```
(defn plot-areas [series px py width height]

  (let [series-ys (map area-points series)

        n-points  (count (first series-ys))

        x-scale   (point->px px (/ width (dec n-points)))

        xs        (map x-scale (range n-points))

        y-max     (apply max (apply concat series-ys))

        y-scale   (point->px py (/ height y-max -1))]

    (doseq [ys series-ys]

      (plot-curve (cons (first xs) xs)

                  (map y-scale (cons 0 ys))

                  (q/color 255 100)

                  (q/color 255 200) 3))))

(defn ex-10-19 []

  (let [expected [3 6.5 12 20 58.5]

        ideal    [10.5 14 21.5 22 32]

        width  640

        height 480

        setup (fn []

                (q/background 100)

                (plot-areas [expected ideal] 0 height

                            width height))]

    (q/sketch :setup setup :size [width height])))
```

我们用`plot-areas`功能绘制了`expected`和`ideal`系列，用`background`功能给草图设置了较暗的背景。在我们对`plot-curve`的调用中，我们指定半透明白色作为要使用的填充。下图显示了结果:

![Plotting compound charts](graphics/7180OS_10_310.jpg)

要将这个图表与之前创建的显示实际值的图表结合起来，我们只需要调整它的比例。该图右上角的最高点对应于 5%的概率密度。我们实际地块上的第 96-99 个^(百分位数代表总数的 7.5%，每个百分位数都在它们的地块上。这意味着，为了使坐标轴具有可比性，我们需要在我们已有图表高度的 2/3 处绘制之前的图表。让我们现在就这样做，并添加一系列标签到两个新的系列，而我们在这一点上:)

```
(defn draw-expected-ideal []

  (let [expected [3 6.5 12 20 58.5]

        ideal    [10.5 14 21.5 22 32]]

    (plot-areas [expected ideal]

                plot-x

                (+ plot-y plot-height)

                plot-width

                (* (/ plot-height 0.075) 0.05))

    (q/text-size 20)

    (emboss-text "EXPECTED" 400 430)

    (emboss-text "IDEAL" 250 430)))
```

最后，我们调用草图中的`draw-expected-ideal`函数以及之前定义的其他函数:

```
(defn ex-10-20 []

  (let [size [960 540]]

    (q/sketch :size size

              :setup #((draw-shapes)

                       (draw-expected-ideal)

                       (draw-banknotes)

                       (draw-axis-labels)

                       (draw-title)))))
```

完成的结果如下图所示:

![Plotting compound charts](graphics/7180OS_10_320.jpg)

希望你会同意完成的图表既有吸引力又能提供信息。最重要的是，我们通过从实际数据中提取指令来生成图表。如果图表是手工制作的，那么最终结果的完整性将更难确定。

## 输出到 PDF

所有的元素组合在一起产生了一个可能最终会被印刷出来的图形。我们提供的绘图指令是基于矢量的，而不是基于像素的，因此它可以缩放到任何所需的分辨率，而不会损失质量。

我们不像处理直方图那样使用`save`输出到基于像素的格式，而是输出到 PDF。PDF 格式将保持我们的作品的可扩展性，并允许我们以任何所需的分辨率输出。为此，我们通过传递关键字`:pdf`和一个`:output-file`路径来配置草图以使用 PDF 渲染器。

```
(defn ex-10-21 []

  (let [size [960 540]]

    (q/sketch :size size

              :setup #((draw-shapes)

                       (draw-expected-ideal)

                       (draw-banknotes)

                       (draw-axis-labels)

                       (draw-title))

              :renderer :pdf

              :output-file "wealth-distribution.pdf")))
```

最后一个示例将我们完成的 PDF 文件输出到项目目录的根目录。



# 总结

在这一章中，我们看到了非常简单的可视化——只使用彩色矩形——如何从数据中获得有用的洞察力，以及 Clojure 核心函数和 Quil 绘图 API 的结合如何使我们能够生成传达信息的强大图形。

我们使用 Quil 库实现了所有这些。Quil 比我们在这里展示的要多得多:它支持交互式动画，支持 web 的 ClojureScript 输出，还可以生成 3D 渲染。可视化也是一个很大的话题，我们不能指望提供更多的例子来激起你对这一章的兴趣。通过展示使用矩形、曲线和 SVG 的基本绘图指令如何组合成强大的图形，我们希望能够启发您创建自己的自定义可视化。

这是数据科学 Clojure 的最后一章。请务必访问这本书在 http://clojuredatascience.com[的网站，获取更多关于主题的信息和进一步阅读的链接。我们打算为一般的数据科学家，尤其是 Clojure 程序员提供一个持续的资源。](http://clojuredatascience.com)

使用一种库也在快速发展的语言来表达数据科学这样一个多样化且快速发展的领域的广度和深度是一项雄心勃勃的任务。尽管如此，我们希望 *Clojure for Data Science* 已经让你对统计学、机器学习和大数据处理的一些基本概念有所了解。这个概念基础应该很好地为您服务，即使技术选项——甚至可能是您选择的编程语言——在未来继续发展。