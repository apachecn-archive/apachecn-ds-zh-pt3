# 3.使用数据和数据集

在这一章中，我们将回顾如何使用 Map 和 apply 将函数应用于列表的基础知识；如何定义用户功能，可以在整个笔记本上使用；以及如何用 Wolfram 语言中一种强大的语法编写代码，这种语法称为纯函数。自然地，我们会将如何将键与值相关联传递给关联，并理解它们对于在 Wolfram 语言中正确构建数据集是非常重要的。我们最后概述了关联是如何对分层数据进行抽象构造的。

## 列表操作

现在我们将看到如何在列表上和列表之间执行操作，这很重要，因为在大多数情况下，Mathematica 中的结果可以被视为列表。我们将看到如何执行算术运算，加法，减法，乘法，除法和标量乘法。我们将详细介绍如何使用 Map 和 apply 将函数应用于列表，因为这些工具在处理线性列表和嵌套列表时非常有用，因为它们允许我们指定函数应用的深度级别。我们将看到如何实现用户自定义函数、语法和术语分组的方法；如何接团；以及如何像其他函数一样应用该函数。我们还将回顾 Wolfram 语言的一个重要概念，即纯函数，因为这些对于能够执行强大的任务和活动以及以紧凑的方式编写代码非常重要。

### 对列表的算术运算

在这一节中，我们将看到列表是如何支持数字之间和列表之间不同的算术运算的。我们可以对列表进行基本的算术运算，如加、减、乘、除。

#### 加法和减法

```py
In[1]:= List[1,2,3,4,5,6]+1
Out[1]= {2,3,4,5,6,7}

In[2]:= List[1,2,3,4,5,6]-5
Out[2]= {-4,-3,-2,-1,0,1}

```

#### 除法和乘法

`In[3]:= List[1,2,3,4,5,6] /` π

`Out[3]=` ![$$ \left\{\frac{1}{\uppi},\frac{2}{\uppi},\frac{3}{\uppi},\frac{4}{\uppi},\frac{5}{\uppi},\frac{6}{\uppi}\;\right\} $$](img/500903_1_En_3_Chapter/500903_1_En_3_Chapter_TeX_IEq1.png)

执行标量乘法:

```py
In[4]:= List[1,2,3,4,5,6]*2
Out[4]= {2,4,6,8,10,12}

```

#### 指数运算

```py
In[5]:= List[1,2,3,4,5,6]^3
Out[5]= {1,8,27,64,125,216}

```

列表还可以支持列表之间的基本算术运算。

```py
In[6]:= List[1,2,4,5]-List[2,3,5,6]
Out[6]= {-1,-1,-1,-1}

```

我们也可以使用数学符号来执行运算。

`In[7]:=` ![$$ \frac{\left\{{}^{"}{\mathrm{Dog}}^{"},2\right\}}{\left\{2,1\right\}} $$](img/500903_1_En_3_Chapter/500903_1_En_3_Chapter_TeX_IEq2.png)

`Out[7]=` ![$$ \left\{\frac{\mathrm{Dog}}{2},2\right\} $$](img/500903_1_En_3_Chapter/500903_1_En_3_Chapter_TeX_IEq3.png)

为了在列表之间执行计算，列表的长度必须相同；否则 Mathematica 将返回一个错误，指出列表没有相同的维度，如下例所示。

```py
In[8]:= {1,3,-1}+{-1}
During evaluation of In[8]:= Thread::tdlen: Objects of unequal length in {1,3,-1}+{-1} cannot be combined.
Out[8]= {-1}+{1,3,-1}

```

#### 加入列表

要将一个列表与另一个列表连接起来，也就是说，要将两个列表连接起来，需要使用 Union 命令，该命令将列表的元素连接起来，并显示为一个新的列表。

```py
In[9]:= Union[List["1","v","c"],{13,4,32},List["adfs",3,1,"no"]]
Out[9]= {1,3,4,13,32,1,adfs,c,no,v}

```

除了 Union 命令之外，还有 Intersection 命令，它的功能类似于它在集合论中所代表的功能。这个命令允许我们观察列表中的公共元素。

```py
In[10]:= Intersection[{7,4,6,8,4,7,32,2},{123,34,6,8,5445,8}]
Out[10]= {6,8}

```

正如所看到的，这些列表只有数字 6 和 8 是相同的。

### 将函数应用于列表

功能可以应用于简洁和自动化的列表。最常用的功能是映射和应用。一个简短的符号是用符号@代替方括号[]；f @“expr”等价于 f[expr]。

```py
In[11]:= Max @ {1,245.2,2,5,3,5,6.0,35.3}
Out[11]= 245.2

```

Map 具有以下形式，Map[f，" expr "]；另一种表示方法是用符号@表示简写符号。f /@ "expr "和 Map[f，" expr"]是等价的。嵌套列表也包括在内。

```py
In[12]:= Factorial/@List[1,2,3,4,5,6]
Out[12]= {1,2,6,24,120,720}

```

映射可以应用于嵌套列表。

```py
In[13]:= Map[Sqrt,{{1,2},{3,4}}]
Out[13]= {{1,Sqrt[2]},{Sqrt[3],2}}

```

通过使用映射，该函数被应用于列表的每个元素。Map 也可以处理嵌套列表，就像前面的例子一样。在下一个例子中，我们将使用 Table 创建一个包含 10 个元素的列表。那些元素将是 0 到 1 之间的随机数，然后我们将映射一个函数将它们转换成字符串表达式。

```py
In[14]:= Data=Range[RandomReal[{0,1}],10];(*List *)
ToString/@Data (* mapping a to convert to string*)
Head/@ % (*Checking the data type of every element *)
Out[14]= {0.539347,1.53935,2.53935,3.53935,4.53935,5.53935,6.53935,7.53935,8.53935,9.53935}
Out[14]= {String,String,String,String,String,String,String,String,String,String}

```

我们将看到如何将一个函数应用到一个带有附加函数的列表中。Apply 的形式为 Apply [f，" expr"]，简写符号为 f @@ "expr "。

```py
In[15]:= Apply[Plus,Data](*It gives the sum of the elements of Data*)
Out[15]= 50.3935

In[16]:= Plus@@Data
Out[16]= 50.3935

```

此外，命令可以应用于同一行代码中的列表。这在处理大型列表时会很有帮助；例如，如果我们想知道元素是否满足某个条件，那么可以将元素聚集到一个列表中，然后针对指定的条件进行测试，而不是遍历每个值。

```py
In[17]:= Primelist=Range[100];Map[PrimeQ,Primelist]
Out[17]= {False,True,True,False,True,False,True,False,False,False,True,False,True, False,False,False,True,False,True,False,False,False,True,False,False,False,False,False,True,False,True,False,False,False,False,False,True,False,False,False,True,False,True,False,False,False,True,False,False,False,False,False,True,False,False,False,False,False,True,False,True,False,False,False,False,False,True,False,False,False,True,False,True,False,False,False,False,False,True,False,False,False,True,False,False,False,False,False,True,False,False,False,False,False,False,False,True,False,False,False}

```

在后一个例子中，我们创建一个从 1 到 100 的列表，然后用函数 PrimeQ 测试哪个数字满足素数的条件。其他函数可用于测试数字和字符串的不同条件。此外，可以使用一个更具体的函数来测试列表中的逻辑关系(MemberQ，SubsetQ)。

### 定义自己的功能

可以编写用户函数来执行重复的任务，并减小程序的大小。将代码分割成函数允许您创建执行特定任务的代码片段。当通过参数调用时，函数可以从外部接收数据，并返回固定的结果。

可以用 set 或 set delayed 符号定义一个函数，但是记住使用 set 符号会将结果赋给定义。要定义一个函数，我们首先要写名字或符号，然后是引用变量和下划线。正如我们在案例中看到的，下划线告诉 Mathematica 我们正在处理一个虚拟变量。作为警告，定义的函数在字母之间不能有空格。函数也可以接收多个参数。

```py
In[18]:= MyF[z_]:=12+2+z;MyF2[x_,z_]:=z/x

```

现在我们可以用不同的 z 值调用我们的函数。

`In[19]:= List[MyF[1],MyF[324],MyF[5432],MyF2[154,1],MyF2[14,4],MyF2[6,9]]`

`Out[19]=` ![$$ \left\{15,338,5446,\frac{1}{154},\frac{2}{7},\frac{3}{2}\right\} $$](img/500903_1_En_3_Chapter/500903_1_En_3_Chapter_TeX_IEq4.png)

另外，编写函数的另一种方法是编写复合函数。这个概念类似于复合表达式；不同类的表达式都写在定义内。每个计算都可以或不可以用分号结束。下面的例子将展示这个概念。

```py
In[20]:= StatsFun[myList_]:=
{
Max@myList,
Min@myList,
Mean@myList,
Median@myList,
Quantile@@{myList,1}(* 25 percent *)(* to write a function with multiple arguments with shorthand notation use curly braces*)
}

```

我们可以发送一个列表作为参数。

```py
In[21]:= myList=Table[m-2,{m,-2,10}];
StatsFun[myList]
Out[21]= {8,-4,2,2,8}

```

我们可以在一个函数中有多个操作，可以选择创建参数满足的条件。为了编写条件，我们使用符号破折号和分号(/；)当条件为真时，对函数求值；否则，如果条件不为真，函数将不求值。复合功能需要分组；否则 Mathematica 会将它们视为在整个函数体之外。

在下一个例子中，我们将创建一个函数来告诉我们一个任意的字符串是否是一个回文，也就是当这个单词反向书写时。

```py
In[22]:= PalindromeWord[string_/; StringQ @ string==True]:= (*we can check if the input is really a string*)

(
ReverseWord=StringJoin[Reverse[Characters[string]]]; (*here we separate the characters, reverse the list and join them into a string*)

ReverseWord ==string (* then we test if the word is a palindrome, the output of the whole function will be True or False*)
)

```

我们测试我们的新功能。

```py
In[23]:= PalindromeWord/@{"hello","room","jhon","kayak","civic","radar"}
Out[23]= {False,False,False,True,True,True}

```

当我们对一个或多个复合函数进行局部赋值时，所使用的符号仍然会被赋值，所以如果在函数外部调用这些符号，可能会导致编码错误。需要考虑的一点是，当函数不再被使用时，我们可以清除我们的函数和局部符号。仅清除函数名不会删除局部赋值。另一个解决方案是在模块内部声明变量，因为变量只会被局部处理。这可以从下面的表格中看出。

```py
In[24]:=
MyFunction[a0_,b0_]:=
Module[{m=a0,n=b0},(*local variables*)
m+n (*body of the module*)
](*end of module*)

In[25]:= Clear[MyF,MyF2,StatsFun,PalindromeWord,ReverseWord] (*To remove tag names of the functions and local symbols *)

```

### 纯函数

纯函数是 Wolfram 语言的一个非常强大的功能。有可能在不引用名字的情况下执行一个函数，并且必须显式地为我们想要执行的操作指定一个名字函数。参数用标签(#)标记。要标记一个特定的参数，符号后面会跟一个数字；所以，#1，#2，#3，...，意味着论点一，二，三，...然后在定义的末尾使用一个&符号来标记 hashtag 将使用的引用。纯函数可以用 Function 以及 hashtag 和&符号的简写符号来构造。

`In[26]:=`

`Function[#^-1][z] == #^-1&[z]`

`#^-1&[z] (* both expression mean`![$$ \frac{1}{z} $$](img/500903_1_En_3_Chapter/500903_1_En_3_Chapter_TeX_IEq5.png)T1】

`Out[26]= True`

`Out[27]=` ![$$ \frac{1}{z} $$](img/500903_1_En_3_Chapter/500903_1_En_3_Chapter_TeX_IEq6.png)

纯函数的一些例子。

```py
In[28]:= {#^-1&[77],#1+#2-#3&[x,y,z] (* we can imagine that #1,#2,#3 are the 1st, 2nd and 3rd variables*),
Power[E,#]&[3]}
Out[28]= {1/77,x+y-z, ⅇ3}

```

我们可以将纯函数与 Map 和 Apply 一起使用，将列表中的每个参数传递给特定的函数。#表示列表中的每个元素，而&表示#已被填充并测试了列表中的元素。

```py
In[29]:= N[#]&/@ {1,1,1,12,3,1}
Sqrt[#]&/@{-1,2,4,16}
Out[29]= {1.,1.,1.,12.,3.,1.}
Out[30]= {I,Sqrt[2],2,4}

```

通过使用 Apply 和 pure 函数，可以以更紧凑的形式编写代码，如下例所示；我们可以选择大于 10 的数字。

```py
In[31]:= Select@@{{1,22,41,7,62,21},#>10&}
Out[31]= {22,41,62,21}

```

## 索引表

为了提供一种快速观察和管理一组相关数据的方法，我们可以以表格的形式创建和显示结果。这让我们看到了如何用 Wolfram 语言创建表格，比如给列命名，给行命名。我们将展示一系列示例，帮助您学习使用这些表的基本知识，以便您可以正确地呈现数据。

### Wolfram 语言的表格

表格是用嵌套列表创建的，这些列表是用函数 TableForm 描绘的。

```py
In[32]:= table1={{"Dog","Wolf"},{"Cat","Leopard"},{"Pigeon","Shark"}};
TableForm[table1]
Out[32]//TableForm=
Dog          Wolf
Cat          Leopard
Pigeon       Shark

```

TableForm 的格式为[“列表”，选项]。格式选项允许您以三种方式对齐表格的列:左对齐、居中和右对齐。在下一个示例中，表格的内容居中。

```py
In[33]:= TableForm[table1,TableAlignments → Right]
Out[33]//TableForm=

   Dog    Wolf
   Cat Leopard
Pigeon   Shark

```

可以使用选项命令 TableHeadings 添加标题，并指定是同时显示行标签和列标签，还是只显示其中一个。选择“自动”选项会为行和列提供索引标签。记住在撇号之间写字符串或者使用 ToString。

```py
In[34]:= TableForm[table1,TableHeadings->{{"Row 1","Row 2","Row 3"}, {"Column 1","Column 2","Column 3"}}]
Out[34]//TableForm=

        |   Column 1         Column 2
Row 1   |   Dog              Wolf
Row 2   |   Cat              Leopard
Row 3   |   Pigeon           Shark

```

带标签的行和列可以定制为所需的名称。

```py
In[35]:= Colname={"Domestic Animals","Wild Animals"};
Rowname={"Animal 1","Animal 2","Animal 3"};
TableForm[table1,TableHeadings → {Rowname,
Colname}]
Out[35]//TableForm=

          |  Domestic Animals      Wild Animals
Animal 1  |  Dog                   Wolf
Animal 2  |  Cat                   Leopard
Animal 3  |  Pigeon                Shark

```

同样的概念也适用于通过在行或列选项上键入 None 来仅标注列或行。

```py
In[36]:= TableForm[table1,TableHeadings → {None, {"Domestic Animals","Wild Animals"}}]

Out[36]//TableForm=
Domestic Animals    Wild Animals
Dog                 Wolf
Cat                 Leopard
Pigeon              Shark

```

使用表格和范围可以创建表格的自动形式。通过在表标题中应用 Automatic 选项，我们可以为数据创建索引标签。

```py
In[37]:= TabData={Table[i,{i,10}],Table[5^i,{i,10}]};
TableForm[TabData,TableHeadings → Automatic]
Out[37]//TableForm=

     |   1        2        3        4        5        6        7
1    |   1        2        3        4        5        6        7
2    |   5        25       125      625      3125     15625    78125

```

出于展示的原因，桌子也可以调换。

```py
In[38]:= TableForm[Transpose[TabData],TableHeadings → Automatic]
Out[38]//TableForm=

   |  1         2
1  |  1         5
2  |  2         25
3  |  3         125
4  |  4         625
5  |  5         3125
6  |  6         15625
7  |  7         78125

```

另一个有用的工具是网格。使用 Grid，我们可以用表格格式显示一个列表或者一个嵌套列表。就像使用 TableForm 一样，Grid 也可以定制为更恰当地展示数据。

Note

网格适用于任何形式的表达。

```py
In[39]:= TabData2 = Table[{i,Exp[i],N @ Exp[i]},{i,7}];
Grid[TabData2]
Out[39]=
1    e      2.71828
2    e2    7.38906
3    e3    20.0855
4    e4    54.5982
5    e5    148.413
6    e6    403.429
7    e7    1096.63

```

要添加头，我们需要将它们作为字符串插入到原始列表中的位置 1。

```py
In[40]:= Grid[Insert[TabData2,{"i","Exp i","Numeric approx."},1]]
Out[40]=
i    Exp i  Numeric approx.
1    e       2.71828
2    e2     7.38906
3    e3     20.0855
4    e4     54.5982
5    e5     148.413
6    e6     403.429
7    e7     1096.63

```

我们还可以添加隔断和隔板。使用分隔线和间距，我们可以选择分隔 y 轴和 x 轴。

```py
In[41]:=Grid[Insert[TabData2,{"i","Exp i","Numeric approx."}, 1],Dividers → {All, False},Spacings → {1, 1} ]
Out[41]=

 | i | Exp i |  Numeric approx. |
 | 1 |   e   |      2.71828     |
 | 2 |   e2  |      7.38906     |
 | 3 |   e3  |      20.0855     |
 | 4 |   e4  |      54.5982     |
 | 5 |   e5  |      148.413     |
 | 6 |   e6  |      403.429     |
 | 7 |   e7  |      1096.63     |

```

可以使用背景选项添加背景。使用此选项，表格或列表格的特定部分可以着色。

```py
In[42]:=Grid[Insert[TabData2,{"i","Exp i","Numeric approx."}, 1],Dividers → {All, False},Spacings → {Automatic, 0},Background → {{LightYellow,None,LightBlue}}]
Out[42]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Figa_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Figa_HTML.jpg)

### 联合

联想是沃尔夫拉姆语发展的基础；关联用于索引列表或其他表达式，以及创建更复杂的数据结构。关联是一个更加结构化的构造，它允许我们提供一个创建键和值对的过程。关联对于查看给定键的特定值很有用。稍后我们会看到它们对于用 Wolfram 语言处理数据集是很重要的。

关联的形式为关联[ "key_1" → val_1，key_2 →val_2...]或< | “key_1”→ “val_1”, “key_2” → “val_2” ... | >；它们将一个键与一个值相关联。键和值可以是任何表达式。要构建关联，可以使用 association 命令，或者我们可以使用符号输入。

```py
In[43]:=
Associt=<|1 → "a",2 → "b",3 → "c"|> (* is the same as Association[a → "a",
b → "b",c → "c"] *)
Associt2= Association[ dog → "23","score" → π * π, 2*2 → Sin[23 Degree]]
Out[43]= <|1 → a,2 → b,3 → c|>
Out[44]= <|dog → 23,score → π2,4 → Sin[23 °] |>

```

关联中的条目是有序的，因此可以根据值的键或关联的元素来访问数据，就像列表一样。位置与值相关联，而不是与键相关联。

```py
In[45]:= Associt[1](*this is key 1 *)
Associt2[[2]] (*this is position of key 2, which is π2 *)
Out[45]= a
Out[46]= π2

```

如后一个示例所示，位置与值关联，而不是与键关联。因此，如果我们想显示部分关联，我们可以使用分号。

```py
In[47]:= Associt[[1;;2]]
Associt2[[2;;2]]
Out[47]= <|1 → a,2 → b|>
Out[48]= <|score → π2 |>

```

可以使用命令键和值提取值和键。

```py
In[49]:= Keys@Associt2
Values@Associt2
Out[49]= {dog,score,4}
Out[50]= {23, π2,Sin[23 °]}

```

如果我们在没有适当参考的情况下请求一个密钥，我们将得到一个错误。

```py
In[51]:= Associt["a"](* there is no "a" key in the association, thus the error*)
Out[51]= Missing[KeyAbsent,a]

```

关联也可以是关联。如下例所示，我们可以关联关联，从而产生一个关联的关联。这个概念是理解 Wolfram 语言中数据集如何工作的基础。

```py
In[52]:= Association[Associt,Associt2]
Out[52]= <|1 → a,2 → b,3 → c,dog → 23,score → π2,4 → Sin[23 ° ]|>

```

我们还可以使用 AssociationThread 与列表建立不同的关联。键对应于第一个参数，值对应于第二个参数。这是通过命令 AssociationThread 实现的。AssociationThread 将一个键列表线程化为一个值列表，如下所示:< | {“key_1”, “key_2”, “key_3” ...} → {“val_1”, “val_2”, “val_3” ... | >。后一种形式可以被看作是标记值列表的键列表。当我们定义了键和值的列表时，可以使用命令将列表与列表相关联。我们还可以创建一个关联列表，使得键可以作为一行和一列来读取。

```py
In[53]:= AssociationThread[{"class","age","gender","survived"},{"Economy",29,"female",True}]
Out[53]= <|class → Economy,age → 29,gender → female, survived→ True|>

```

我们可以构造我们的键和值的列表。

```py
In[54]:= keys={"class","age","gender","boarded"};
values={"Economy",29,"female",True};
AssociationThread@@{keys,values}
Out[54]= <|class → Economy,age → 29,gender → female,boarded → True|>

```

更复杂的结构可以用关联来完成。例如，下一个关联基于关于跑车的信息创建一个数据结构，包括型号名称、发动机、功率、扭矩、加速度和最高速度。

```py
In[55]:= Association@
{
"Model name" → "Koenigsegg CCX",
"Engine" → "Twin supercharged V8",
"Power" → 806 "hp",
"Torque" → 5550 "rpm",
"Acceleration 0-100 km/h" → 3.2 "sec",
"Top speed" → 395 "Km/h"
}
Out[55]= <|Model name → Koenigsegg CCX,Engine → Twin supercharged V8,Power → 806 hp,Torque → 5550 rpm,Acceleration 0-100 km/h → 3.2 sec,Top speed → 395 Km/h|>

```

我们可以看到标签及其元素是如何以分组的方式创建的。除此之外，还显示了花括号如何标记每行如何排列键/值对。

## 数据集格式

正如我们在后面的章节中所看到的，关联是构成数据结构形式的重要组成部分。正如我们将看到的，Wolfram 语言中的数据集通过提供访问数据集中数据的方法，提供了一种组织和展示分层数据的方式。我们将在本节中看到一些例子，包括如何将列表、嵌套列表和关联转换成数据集。此外，我们还将讨论如何添加值、访问数据集的值、删除值、在数据集上映射函数、处理重复数据以及按行或列应用函数。

### 构建数据集

数据集用于构建分层数据框架，其中列表、关联和嵌套列表是有序的。数据集有助于以可访问的结构化格式展示大量数据。有了数据集，我们可以用清晰的格式显示带有行标题、列标题和编号元素的封闭结构。将数据作为数据集可以让我们以多种方式查看数据。

数据集可以以四种形式构建。

1.  列表列表；没有行列面额的桌子

2.  一个关联列表，一个带有标签列的表格；具有重复关键字和不同或相同值的表

3.  列表的关联，带有标记行的表格；具有不同关键字和不同或相同值的表

4.  协会联合会；带有带标签的行和列的表格

数据集可以手动创建，也可以先创建关联。首先，在使用数据集之前，我们必须确保“动态更新”已启用；否则 Mathematica 内核将无法正确显示数据集，我们只会在计算表达式时看到一个重叠的单元格。因此，如果我们将鼠标指针悬停在数据集对象内容上，它会向我们显示一条广告，提示无法显示动态内容。要激活它，请转到菜单栏，然后选择评估，并选择启用动态更新。

创建新数据集最常见的形式是通过列表列表。使用函数数据集，我们在花括号{}内创建一个列表。每个大括号代表我们桌子的一部分。图 [3-1](#Fig1) 显示了数据集函数的输出。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig1_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig1_HTML.jpg)

图 3-1

根据输入代码创建的数据集对象

```py
In[56]:= Dataset@
{
{"Jhon",23,"male","Portugal"},
{"Mary",30,"female","USA"},
{"Peter",33,"male","France"},
{"Julia",53,"female","Netherlands"},
{"Andrea",45,"female","Brazil"},
{"Jeff",24,"male","Mexico"}
}
Out[56]=

```

将鼠标光标悬停在数据集的元素上，可以看到左下角的位置。名称法国对应于位置行 3 和列 4。数据集的符号首先是行，然后是列。在我们有标签列、行或两者的情况下，我们将看到列名和行名，而不是数字。

通过关联列表构建数据集是通过首先用重复的关键字创建关联，然后用列表封闭来执行的。首先，我们创建自己的联想；重复的键指定每个列标题。这些值表示列的内容。数据集有一个数据集的头表达式。

```py
In[57]:= Dataset@
{
<|"Name" → "Jhon", "Age" → 23, "Gender" → "male", "Country" → "Portugal" |>,
<|"Name" → "Mary", "Age" → 30, "Gender" → "female", "Country" → "USA" |>,
<|"Name" → "Peter", "Age" → 33, "Gender" → "male", "Country" → "France" |>,
<|"Name" → "Julia", "Age" → 53, "Gender" → "female", "Country" → "Netherlands"|>,
<|"Name" → "Andrea", "Age" → 45, "Gender" → "female", "Country" → "Brazil |>,
<|"Name" → "Jeff", "Age" → 24, "Gender" → "male", "Country" → "Mexico" |>
}
(*Head @ % *)

Out[57]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig2_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig2_HTML.jpg)

图 3-2

带有列标题的数据集

从图 [3-2](#Fig2) 中可以看出，Mathematica 识别出姓名、年龄、性别、国家是列标题，这也是为什么框的颜色不一样的原因。当光标经过列标签时，它们会以蓝色突出显示，这样就可以点击标签的名称，然后只产生选中的标签，而不是整个数据集，如图 [3-3](#Fig3) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig3_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig3_HTML.jpg)

图 3-3

数据集中选定的列名

发生这种情况时，列的名称也会出现；要返回整个数据集，点击左上角的电子表格图标![img/500903_1_En_3_Chapter/500903_1_En_3_Figb_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Figb_HTML.jpg)或名称 All。当我们处理大量的行和列时，这种类型的布局很实用，我们希望只关注数据集的几个部分。

列表的关联，在这种情况下，键代表行的标签，值是行的元素的列表；然后我们把整个街区联系起来。下一个代码块生成一个列表的关联。

Note

这同样适用于这里。每当您单击行的名称时，它将只显示该行。

```py
In[58]:= Dataset@
<|
"Subject A" → {"Jhon", 23, "male", "Portugal"},
"Subject B" → {"Mary", 30, "female", "USA"},
"Subject C" → {"Peter", 33, "male", "France"},
"Subject D" → {"Julia", 53, "female", "Netherlands"},
"Subject E" → {"Andrea", 45, "female", "Brazil"},
"Subject F" → {"Jeff", 24, "male", "Mexico"}
|>
Out[58]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig4_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig4_HTML.jpg)

图 3-4

带有标记行的数据集

从图 [3-4](#Fig4) 中可以看出，这些行现在已被标记。就像前面的例子一样，行标签被识别并显示在颜色框中。当选择该行的标签时，将只显示该行，如图 [3-5](#Fig5) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig5_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig5_HTML.jpg)

图 3-5

主题 E 选定的行

关联的关联，在这种形式下，关联的关联的重复键是列标签和数据集内容的值。在第二个关联中，键是行的标签，第一个关联是第二个关联的值。下一个例子阐明了这一点。

```py
In[59]:= Dataset@
<|
"Subject A" → <|"Name" → "Jhon", "Age" → 23, "Gender" → "male", "Country" → "Portugal"|>,
"Subject B" → <|"Name" → "Mary", "Age" → 30, "Gender" → "female", "Country" → "USA"|>,
"Subject C" → <|"Name" → "Peter", "Age" → 33, "Gender" → "male", "Country" → "France"|>,
"Subject D" → <|"Name" → "Julia", "Age" → 53, "Gender" → "female", "Country" → "Netherlands"|>,
"Subject E" → <|"Name" → "Andrea", "Age" → 45, "Gender" → "female", "Country" → "Brazil" |>,
"Subject F" → <|"Name" → "Jeff", "Age" → 24, "Gender" → "male", "Country" → "Mexico"|>
|>
Out[59]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig6_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig6_HTML.jpg)

图 3-6

在行和列中有名称的数据集

如图 [3-6](#Fig6) 所示，行和列现在已被标记。就像前面的例子一样，列和行标签被识别并显示在颜色框中。当选择行或列的标签时，将只显示该行或列，如图 [3-7](#Fig7) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig7_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig7_HTML.jpg)

图 3-7

仅选择一行

如果我们只选择了一个特定的值，那么该值将单独显示。图 [3-8](#Fig8) 显示其形状。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig8_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig8_HTML.jpg)

图 3-8

主题 F 的名称

从关联的关联中创建数据集最适合于压缩数据集，因为有时候试图提取值和键会变得很麻烦。然而，最好的方法是最适合你的方法。

### 访问数据集中的数据

Mathematica 给每个元素一个唯一的索引，所以如果我们想从数据集中选择数据，我们就给数据集分配一个符号，然后在下一个表单中指定每个输出。自变量的第一个位置和第二个位置代表行和列[第 n <sup>行</sup>，第 m <sup>列</sup>。因此，为了根据一个列名或一组列提取数据，我们将这些列放在括号中。我们也可以使用双括号符号。在只收到一个参数的情况下，它将只是行。

首先让我们创建数据集。

```py
In[60]:= Dst=Dataset@
{
<|"Name" → "Jhon", "Age" → 23, "Gender" → "male", "Country" → "Portugal"|>,
<|"Name" → "Mary", "Age" → 30, "Gender" → "female", "Country" → "USA"|>,
<|"Name" → "Peter", "Age" → 33, "Gender" → "male", "Country" → "France"|>,
<|"Name" → "Julia", "Age" → 53, "Gender" → "female", "Country" → "Netherlands"|>,
<|"Name" → "Andrea", "Age" → 45, "Gender" → "female", "Country" → "Brazil"|>,
<|"Name" → "Jeff", "Age" → 24, "Gender" → "male", "Country" → "Mexico"|>
};

```

符号[[ ]]的作用与双括号的特殊字符(⟦ ⟧)相同。此外，我们可以用值的特定键来选择数据。

```py
In[61]:= Dst[[1,2]](* This is for row 1, column 2*)
Dst[1](*row 1*)
Out[61]= 23
Out[62]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig9_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig9_HTML.jpg)

图 3-9

Dst 的第 1 行

如图 [3-9](#Fig9) 所示，让我们看看图 [3-10](#Fig10) 。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig10_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig10_HTML.jpg)

图 3-10

第 1 列到第 3 列的值

```py
In[63]:= Dst[1;;3](*to manipulate data of the column try Dst[1;;3, 1;;3] *)
Out[63]=

```

在本例中，我们选择了位置 1 到 3 的数据，即从约翰到彼得。这同样适用于列。输出如图 [3-10](#Fig10) 所示。

我们还可以显示特定的列，并用它们的键维护所有固定的行。当每行中有一个标签时，应用相同的过程。通过键入 All，我们指的是列或行的所有元素。输出如图 [3-11](#Fig11) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig11_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig11_HTML.jpg)

图 3-11

列名和年龄的值

```py
In[64]:= Dst[All,{"Name","Age"}] (*If more than 1 column label is added then enclosed the labels by curly braces. *)
Out[64]=

```

或者，我们可以提取一列或一行作为列表，以便在 Wolfram 语言中更好地操作它们。为此，我们需要使用函数 Normal 和值。请记住，我们处理的是关联，所以如果我们需要值，那么我们使用命令 values，然后 Normal 将其转换为普通表达式。

```py
In[65]:= Normal@Values@Dst[All,{"Name","Age"}](*values of the name and age columns*)
Out[65]= {{Jhon,23},{Mary,30},{Peter,33},{Julia,53},{Andrea,45},{Jeff,24}}

```

对于行，这是相同的想法:如果行有一个标签，那么我们可以使用行的标签。

```py
In[66]:= Normal@ Values@Dst[[1,All]]
Out[66]= {Jhon,23,male,Portugal}

```

如果我们先计算普通值，然后再计算值，结果是一样的。

```py
In[67]:= Values@Normal@Dst[[1,All]]
Out[67]= {Jhon,23,male,Portugal}

```

另一个可以使用的功能是查询；这是一个专门处理数据集的函数。查询必须应用于数据集的符号或直接应用于数据集。查询是有帮助的，因为它允许容易地选择值；我们也可以提取行或列，得到唯一的记录。

```py
In[68]:= Query[All,"Country"]@Dst
Query[3]@%
Out[68]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig12_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig12_HTML.jpg)

图 3-12

国家价值观

```py
Out[69]= France

```

如图 [3-12](#Fig12) 所示，我们可以通过查询提取列和值。

另一个更直观的函数是 Take，在这个函数中，我们可以简单地指定数据集的符号，然后指定要显示多少行和列。当我们处理大型数据集，并且只想查看数据的特定部分时，Take 就变得非常方便。

```py
In[70]:= Take[Dst,2] (*First 2 rows*)
(*Take [Dst, 3,3] First 3 rows and columns*)
Out[70]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig13_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig13_HTML.jpg)

图 3-13

数据集的前两行

如图 [3-13](#Fig13) 所示，我们可以使用 Take 作为替代。

### 添加值

既然我们已经研究了如何访问数据集的元素，现在我们可以继续研究如何向数据集添加新值。我们可以用 Append 或 Prepend 添加行，但是记住 AppendTo 和 PrependTo 也可以使用。但是，它们会将新的结果赋给已赋值的变量。Append 在最后添加，Prepend 在第一个添加。

要添加一行，我们需要像编写带有重复键的关联那样编写新行，调用数据集和函数，然后是新行，如图 [3-14](#Fig14) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig14_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig14_HTML.jpg)

图 3-14

在数据集末尾添加新行

```py
In[71]:= Dst[Append[<|"Name" → "Anya", "Age" → 19, "Gender" → "female", "Country" → "Russia"|>]]
Out[71]=

```

要在数据集顶部添加新行，请尝试使用代码:`Prepend[ Sst[Prepend[<|"Name"` → `"Anya", "Age"` → `19, "Gender"` → `"female", "Country"` → `"Russia"|> ]]]`。

添加一个只有单个值的新列可以通过简单地用键名(即列名)给数据集的列侧赋值来完成。图 [3-15](#Fig15) 显示了添加的新列。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig15_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig15_HTML.jpg)

图 3-15

添加了 ID 列

```py
In[72]:= Dst[All,Prepend["ID number" → 1]]
Out[72]=

```

要将值列表添加为列，我们首先需要一个值列表，然后使用 AssociationThread 将每个值关联到同一个键，为重复的键创建一个值关联。然后，我们使用函数 Join 创建新关联和连接对象的数据集，即整个数据集和新数据集。Join 可以聚集同一个头像的表情。

```py
In[73]:= Id={1,2,3,4,5,6};(* our list of values *)
ID=AssociationThread["ID" → #]&/@Id (* the process is threaded in the list *)
Out[73]=
{<|ID → 1|>,<|ID → 2|>,<|ID → 3|>,<|ID → 4|>,<|ID → 5|>,<|ID → 6|>}

```

对于后面的块，每个元素需要逐个关联，因为 AssociationThread 禁止重复的键，所以我们将只有一个关联，并且我们需要有一个重复的键标记到不同的值。

现在，我们继续使用图 [3-16](#Fig16) 中所示的相同键创建新的数据集。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig16_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig16_HTML.jpg)

图 3-16

ID 列数据集

```py
In[74]:= Dataset[ID]
Out[74]=

```

最后我们加入相同的对象；这里我们使用规范级别为 2 的 Join，因为新数据集是深度为 2 的子列表。如果我们要添加左侧的列，新列先走，然后是数据集；对于右侧来说，则相反。图 [3-17](#Fig17) 显示了输出数据集。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig17_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig17_HTML.jpg)

图 3-17

添加了 ID 列

```py
In[75]:= Join[%,Dst,2]
Out[75]=

```

在前面的案例中，我们使用了来自关联列表的数据集；从某种意义上说，我们只处理带标记的行或带标记的行和列，添加行或列的过程通过向数据集添加相同的结构来保留。因此，向列表关联中添加新行将采用形式< | “key” → {elem, ... } | >；对于列，这将是创建数据集并连接它们的过程。在列表的列表的情况下，添加一行将是相同的方法，但是没有一个键。对于关联的关联的情况，添加一行将是 | >，对于列，它将和以前一样，是一个与值关联的键。然而，对于如何容纳数据没有限制。

最后，为了更改唯一值，我们选择项目并赋予它新的内容。在我们在行和列上都有标签的情况下，原始形式仍然保留，{"rows "，" columns"}。因此，如果我们想要替换 Jhon 的年龄，我们使用函数 ReplacePart，方法是调用数据集的符号，然后指定列标记，然后使用新值 50。如果我们只使用行标签或列标签，过程将是相同的，但使用行或列标签，然后是元素的位置数。

```py
In[76]:= ReplacePart[Dst, {1, "Age"} → 50](*Also using the index will produce the same output, that would be {1,2} → 50 *)
Out[76]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig18_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig18_HTML.jpg)

图 3-18

约翰年龄值改为 50

如图 [3-18](#Fig18) 所示，新值为 50。

### 丢弃值

我们可以删除一行或一列的内容，而不用删除整个表结构。为此，我们使用 Drop 或 Delete 函数。当使用 Drop 时，我们用{ }将行号或列号括起来，以删除一个唯一的行或列(图 [3-19](#Fig19) )。

```py
In[77]:= Drop[Dst,{1}](*in the instance we want to delete more than one then we write m through n dropped {m,n} *)
Out[77]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig19_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig19_HTML.jpg)

图 3-19

删除第 1 行

如图 [3-19](#Fig19) 所示，我们已经删除了第一行。我们也可以同时删除行和列。图 [3-20](#Fig20) 显示第二行和最后一列被放下。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig20_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig20_HTML.jpg)

图 3-20

删除第 2 行和第 4 列后的新数据集

```py
In[78]:=Drop[Dst,{2},{4}]
Out[78]=

```

另一种方法，但是通过使用行或列的键的标签，是使用 Delete，如图 [3-21](#Fig21) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig21_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig21_HTML.jpg)

图 3-21

年龄栏已删除

```py
In[79]:= Dst[All,Delete["Age"]] (*to delete a row use ["label of row",All] *)
Out[79]=

```

### 过滤值

将数据作为数据集可以让我们以多种方式查看数据。现在让我们使用标记数据集来更好地展示过滤值的工作方式。首先，我们将使用图 [3-22](#Fig22) 中所示的标签数据集。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig22_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig22_HTML.jpg)

图 3-22

标记数据集

```py
In[80]:= Clear[Dst];(* Let's clear the symbol "Dst" of previous assignments *)
Dst=Dataset@
<|
"Subject A" → <|"Name" → "Jhon", "Age" → 23, "Gender" → "male", "Country" → "Portugal"|>,
"Subject B" → <|"Name" → "Mary", "Age" → 30, "Gender" → "female", "Country" → "USA"|>,
"Subject C" → <|"Name" → "Peter", "Age" → 33, "Gender" → "male", "Country" → "France"|>,
"Subject D" → <|"Name" → "Julia", "Age" → 53, "Gender" → "female", "Country" → "Netherlands"|>,
"Subject E" → <|"Name" → "Andrea", "Age" → 45, "Gender" → "female", "Country" → "Brazil"|>,
"Subject F" → <|"Name" → "Jeff", "Age" → 24, "Gender" → "male", "Country" → "Mexico"|>
|>
Out[80]=

```

就像列表一样，我们可以创建一个或多个过滤条件；例如，我们可以选择大于 30 的年龄。并且我们会得到一个 dataset 对象(图 [3-23](#Fig23) )。

```py
In[81]:= Cases[Dst[All,"Age"],x_/;x>30](*also we can select data that matches exactly 30 with the == sign*)
Out[81]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig23_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig23_HTML.jpg)

图 3-23

年龄列中的筛选数据

图 [3-23](#Fig23) 显示了过滤后的数据。可以根据真或假的结果来选择数据。为此，我们可以使用 Select 函数。

```py
In[82]:= Select[Dst[All,"Age"],EvenQ]
Out[82]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig24_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig24_HTML.jpg)

图 3-24

选定的主题

图 [3-24](#Fig24) 显示了所选择的主题。也可以使用纯函数。记住#Age 类似于 Age 列中的元素，如图 [3-25](#Fig25) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig25_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig25_HTML.jpg)

图 3-25

使用纯函数语法选择的值

```py
In[83]:= Dst[Select[#Age>30&]]
Out[83]=

```

同样，我们可以计算分类数据的值，如图 [3-26](#Fig26) 所示。当我们想确定我们的数据中有多少类时，这是很有帮助的。例如，我们可以计算数据集中有多少女性和男性。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig26_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig26_HTML.jpg)

图 3-26

男性和女性班级的统计数据

```py
In[84]:= Counts[Dst[All,"Gender"]] (*alternative form:Dst[Counts,"Gender"] *)
Out[84]=

```

更复杂的组可以基于一个类来创建；例如，我们可以按性别对数据集进行分组，如图 [3-27](#Fig27) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig27_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig27_HTML.jpg)

图 3-27

按阶级和年龄排列的数据

```py
In[85]:= Dst[GroupBy["Gender"],Counts,"Age"]
Out[85]=

```

作为一个好的实践，当符号不再被使用时，让我们清除它们。

```py
In[86]:= Clear[Dst]

```

### 应用函数

函数可以应用于我们的数据集。这类函数尤其可以是统计数据，用于了解数据的维度或转换数据。函数可以应用于数据结构中的单个列或唯一元素。

首先，让我们创建一个包含 10 项的数据集，这些列将是 1 到 10 的阶乘，1 到 0 的随机实数，以及 1 到 10 的自然对数。图 [3-28](#Fig28) 显示了新的数据集。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig28_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig28_HTML.jpg)

图 3-28

数值数据集

```py
In[87]:= DataNumbr=Dataset@Table[<|"Factorial" → Factorial[i], "Random number" → RandomReal[{0,1}], "Natural Logarithm" → Log[E,i]|>,{i,1,10}]
Out[87]=

```

现在我们可以对我们的数据进行基本运算，比如获取阶乘和随机数的平均值，如图 [3-29](#Fig29) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig29_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig29_HTML.jpg)

图 3-29

阶乘和随机数列中值的平均值

```py
In[88]:= DataNumbr[Mean,{"Factorial","Random number"}] //N
Out[88]=

```

通过使用@ *(组合)符号，圆括号和函数组合也可用于关联应用于数据的操作。图 [3-30](#Fig30) 显示了从少到多排序的随机数数据。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig30_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig30_HTML.jpg)

图 3-30

按规范顺序排序的数据

```py
In[89]:= DataNumbr[All,"Random number"]@(Sort@*N)
Out[89]=

```

我们可以对数据应用不同的函数。从图 [3-31](#Fig31) 中可以看出，数据集以十进制形式显示数字，因为否则它将无法放入方形框中。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig31_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig31_HTML.jpg)

图 3-31

自然对数列的合计值、最大值和最小值

```py
In[90]:= DataNumbr[{Total,Max,Min},"Natural Logarithm"]
Out[90]=

```

我们也可以应用我们自己创建的函数；让我们使用一个先前构造的函数。

```py
In[91]:= StatsFun[myList_]:=
{
Max@myList,
Min@myList,
Mean@myList,
Median@myList,
Quantile@@{myList,1}(* 25 percent *) (* to write a function with multiple arguments with shorthand notation use curly braces*)
}

```

图 [3-32](#Fig32) 显示了我们之前创建的应用于数据集列的函数。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig32_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig32_HTML.jpg)

图 3-32

StatsFun 应用于自然对数列

```py
In[92]:= DataNumbr[{StatsFun},"Natural Logarithm"]
Out[92]=

```

也可以应用重构数据集的函数，如 Reverse，如图 [3-33](#Fig33) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig33_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig33_HTML.jpg)

图 3-33

数据集的反转元素

```py
In[93]:= DataNumbr[Reverse,All]
Out[93]=

```

Map 也可以用来应用函数，就像我们在前面章节中看到的列表一样。在下一个例子中，我们将把一个函数直接映射到我们的数据集中，如图 [3-34](#Fig34) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig34_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig34_HTML.jpg)

图 3-34

数据集中映射的平方根函数

```py
In[94]:= Map[Sqrt,DataNumbr]
Out[94]=

```

转置是一种将列转换为行，将行转换为列的操作，有时可以帮助我们以不同的方式观察数据。为了获得数据集的转置，我们使用应用于数据集的转置函数。正如我们在图 [3-35](#Fig35) 中看到的，所有列现在都是行，并且以紧凑的方式显示，因为这是一个大的行。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig35_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig35_HTML.jpg)

图 3-35

由于内容庞大，Mathematica 的数据集值

```py
In[95]:= DataNumbr//Transpose
Out[95]=

```

如果我们点击行，我们应该得到相应行的值。

### 按列或行的函数

另一种方法是将一个函数直接应用于我们的列值，我们可以指定一个转换规则。例如，我们可以舍入到大于或等于自然对数列上所有值的最小整数。图 [3-36](#Fig36) 显示了输出。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig36_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig36_HTML.jpg)

图 3-36

作为规则应用的上限函数

```py
In[96]:= DataNumbr[All,{"Natural Logarithm" → Ceiling}](*The same can be done using the index number of the columns, DataNumbr *)
Out[96]=

```

我们可以将平方根应用于第一行。Map 还可以用于将函数应用于行。图 [3-37](#Fig37) 显示了生成的输出。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig37_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig37_HTML.jpg)

图 3-37

从早期代码生成的输出

```py
In[97]:= DataNumbr[1,Sqrt] (* Map[Sqrt,DataNumbr[1;;2,All]] can also do the work for the first 2 rows*)
Out[97]=

```

当我们想要将一个函数应用到一个定义的级别时，我们可以使用 MapAt。MapAt 的形式为 MapAt[f，" expr "，{i，j，...}]，其中{i，j}表示该位置的级别，如图 [3-38](#Fig38) 。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig38_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig38_HTML.jpg)

图 3-38

仅使用 MapAt 对第一行求幂

```py
In[98]:=MapAt[Exp,DataNumbr,{1}](*for first position of row 1 only*)(*Double semi-colon can be used to define from row to row, try using 4 ;; 6\. Caution you might get big numbers *)
Out[98]=

```

偶尔我们可能会遇到重复的数据，这可能会使我们很难理解我们的数据，尤其是在出错的情况下。一种方法是删除一整行或一整列，正如我们在前面章节中看到的；但是作为替代，我们可以使用内置函数来完成这项工作。函数 DeleteDuplicates 是最常见的。也可以使用 DeleteCases，但是与 DeleteDuplicates 相反，它删除与模式匹配的数据。让我们为我们的示例创建一个数据集。

```py
In[99]:= Sales=Dataset@
{
<|"Id" → 1, "Product" → "PC", "Price" → "800 €", "Sale Month" → "January"|>,
<|"Id" → 2, "Product" → "Smart phone", "Price" → "255 €", "Sale Month" → "January"|>,
<|"Id" → 3, "Product" → "Anti-Virus", "Price" → "100 €", "Sale Month" → "March"|>,
<|"Id" → 4, "Product" → "Earphones", "Price" → "78 €", "Sale Month" → "February"|>,
<|"Id" → 5, "Product" → "PC", "Price" → "809 €", "Sale Month" → "March"|>,
<|"Id" → 5, "Product" → "PC", "Price" → "809 €", "Sale Month" → "March"|>,
<|"Id" → 6, "Product" → "Radio", "Price" → "60 €", "Sale Month" → "January"|>,
<|"Id" → 7, "Product" → "PC", "Price" → "700 €", "Sale Month" → "February"|>,
<|"Id" → 8, "Product" → "Mouse", "Price" → "100 €", "Sale Month" → "March"|>,
<|"Id" → 9, "Product" → "Keyborad", "Price" → "125 €", "Sale Month" → "January"|>,
<|"Id" → 10, "Product" → "USB 64gb", "Price" → "90 €", "Sale Month" → "March"|>,
<|"Id" → 11, "Product" → "LED Screen", "Price" → "900 €", "Sale Month" → "February"|>,
<|"Id" → 11, "Product" → "LED Screen", "Price" → "900 €", "Sale Month" → "February"|→>
}
Out[99]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig39_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig39_HTML.jpg)

图 3-39

重复数据的数据集示例

如图 [3-39](#Fig39) 所示，在数据集中我们有两个重复的行，ID 号为 5 和 11 的行。DuplicateFreeQ 函数可以检测数据集是否有重复项。当有重复数据时，函数返回 False，没有重复数据时，函数返回 True。它可以直接应用于数据集，或者我们可以检测看起来重复的行。

让我们检查一下从第 1 行到第 7 行是否有重复项。

```py
In[100]:= DuplicateFreeQ[Sales[1;;7,All]]
Out[100]= False

```

我们通过编程发现数据集中有重复的数据。

另一种形式是按列检查我们是否有重复。

```py
In[101]:= Sales[All,{"Id"}]@DuplicateFreeQ
Out[101]= False

```

现在，我们已经发现我们确实有重复，删除重复使用 DeletDuplicates 函数。它可以作为函数应用于数据集或特定的列或行。产生的输出如图 [3-40](#Fig40) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig40_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig40_HTML.jpg)

图 3-40

没有重复的数据集

```py
In[102]:= DeleteDuplicates[Sales] (*Datas[All,{"ID"}]@DuplicateFreeQ*)
Out[102]=

```

另一种方法是使用 GroupBy 来识别数据集中哪些数据是重复的。

```py
In[103]:= GroupBy[Sales,"Id"]
Out[103]=

```

注意在图 [3-41](#Fig41) 中，重复的数据堆叠在一起。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig41_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig41_HTML.jpg)

图 3-41

按重复项分组的数据集

### 自定义数据集

数据集可以根据我们希望显示数据的方式进行定制，使用数据集可以根据偏好进行个性化。为了在下一个模块中探索这一点，我们将从 Wolfram 参考服务器加载示例数据，以发现您可以根据需要个性化数据的方法。当从服务器加载数据时，根据您的互联网连接，它可能会弹出一个试图访问 Wolfram 服务器的加载帧。

让我们通过使用 ExampleData 加载数据，然后选择动物体重的统计数据，并将列表转换为数据集。通过使用选项 MaxItem，我们可以显示数据集的行数或列数。我们选择显示前四行和前三列。显示数据集时，滚动条将出现在左侧和顶部。使用它们在数据集上移动。或者，我们可以选择将内容靠左、靠中或靠右对齐。在图 [3-42](#Fig42) 中，只有左边的滚动条出现。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig42_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig42_HTML.jpg)

图 3-42

动物数据集

```py
In[104]:= AnimalData= ExampleData[{"Statistics","AnimalWeights"}];
Dataset[ AnimalData, MaxItems → {4,3}, Alignment → Center] (*To align a sole column, Alignment → {"Col_name" → Left} *)
Out[104]=

```

为了给数据集的内容着色，使用了 Background 选项，并且保留了符号{row，col}的颜色。要绘制整个数据，只需输入颜色。要按行或列绘画，请以嵌套列表的形式输入颜色，即{{"color_row1 "，" color_row2 "，...}、{“颜色 _ 列 1”、“颜色 _ 列 2”，...} }.也可以通过嵌套颜色来混合颜色。对于特定的值，需要输入值的位置。在下一个示例中，我们将只对前两列进行着色，如图 [3-43](#Fig43) 所示。

对于特定的值，需要输入值的位置。另一个选项是项目的大小，这由 ItemSize 选项控制。如果我们想要编辑相同的选项，但是带有标题，我们可以使用 HeaderAlignment 将文本放在左边、中间或右边；HeaderSize 适合标题的大小；HeaderStyle 表示字体的样式。

```py
In[105]:= Dataset[AnimalData,MaxItems → {4,3}, Background → {{None}, {LightBlue,LightYellow}},ItemSize → {12}]
Out[105]=

```

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig43_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig43_HTML.jpg)

图 3-43

第 1 列和第 2 列是彩色的

对于特定的值，需要输入值的位置。另一个选项是项目的大小，这由 ItemSize 选项控制。如果我们想要编辑相同的选项，但是带有标题，我们可以使用 HeaderAlignment 将文本放在左边、中间或右边；HeaderSize 适合标题的大小；ItemStyle 表示项目的字体样式。图 [3-44](#Fig44) 以粗体显示了数据集。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig44_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig44_HTML.jpg)

图 3-44

具有粗体样式的数据集

```py
In[106]:= Dataset[AnimalData,MaxItems → {4,3}, Background → {{4,3} → Yellow}, ItemSize → {12}, ItemStyle → Bold]
Out[106]=

```

另一个有用的选项是 HiddenItems，它隐藏不想显示的项目。因此，隐藏第 1 行和第 1 列将是。可以使用相关联的标签隐藏列，例如:HiddenItems → {"row # "，" col #"}。图 [3-45](#Fig45) 显示了数据集中隐藏的行和列的形式。对于特定值，则嵌套值的位置 try HiddenItems → {{2，3}}。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig45_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig45_HTML.jpg)

图 3-45

隐藏第 1 列和第 1 行

```py
In[107]:= Dataset[AnimalData,MaxItems → {4,3}, HiddenItems → {1,1}]
Out[107]=

```

我们可以使用 Query 命令向新数据集中的每一列添加标题。要重命名列，应用相同的过程；新名称将被归入旧名称，即“新名称”→“动物名称”，如图 [3-46](#Fig46) 所示。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig46_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig46_HTML.jpg)

图 3-46

添加了列标题的动物数据集

```py
In[108]:= Query[All,<|"Animal Name" → 1, "Body Weight" → 2, "Brain Weight" → 3|>]@Dataset[AnimalData]
(* for display motives we put row 7 to 9, use All for the whole data set *) (* or "symbol_of_the_dataset" [All,<|"Animal Name" → 1, "Body Weight" → 2, "Brain Weight" → 3|>] *)
Out[108]=

```

### 哈希表的一般化

哈希表是一种关联数据结构，它允许存储数据，反过来，允许从称为键的对象中快速检索元素(值)。哈希表可以在数组内部实现，数组的主要组成部分是键和值。在数组中搜索元素的方法是使用哈希函数，该函数将键映射到值对，从而给出它在数组中的位置(索引)。换句话说，哈希函数搜索某个键，计算该键，并返回一个索引。这个过程被称为哈希。图 [3-47](#Fig47) 显示了散列表的典型模式。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig47_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig47_HTML.jpg)

图 3-47

哈希表的图形表示

在哈希表中，键和值的数量可以不断增加。这是哈希表非常有用的原因之一，因为它可以存储大量的信息。在 Wolfram 语言中，关联可以表示哈希表。这主要是因为关联是一种抽象的数据结构，它的基础组件是键和值，就像哈希表一样。这结合了关联数组和索引列表的结构，更像一个散列数组的嵌套。关联具有不可变的重要属性，这意味着每个关联类型的对象都是唯一的，并且一个关联的引用与另一个关联没有链接，即使它们引用的是同一个符号。

就像我们看到的带有关联的普通命令一样，更多的特殊命令是可用的。我们先创建一个关联。嵌套关联被定义为内部具有关联的关联，换句话说，一个键指向一个值桶，该值桶对应于内部具有其他值的键(图 [3-48](#Fig48) )。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig48_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig48_HTML.jpg)

图 3-48

数据集格式的嵌套关联

```py
In[109]:=
Asc=<|"User"->
       <|"Edgar"->
              <|"id"-> 01,
                   "Parameters"->
                    <|"Active"-> True,"Region"-> "LA","Internet Traffic"->
"1 GB"|>|>,
       <|"Anya"->
             <|"id"-> 02,
                    "Parameters"->
                    <|"Active"-> False,"Region"-> "MX","Internet Traffic"->
"3 GB"|>|>|>|>|>;
Dataset[%]
Out[109]=

```

与键和值相关联的命令支持执行访问项目、更新值和删除等操作。请记住，Keys 返回关联的键，并对这些值进行赋值。在嵌套关联中，键只在表面级别起作用。这将在下一段代码中看到。

```py
In[110]:= Keys[Asc]
Out[110]= {User}

```

应用密钥只会返回密钥用户。要查看嵌套关联中的键，需要将命令键应用到更深的级别。这可以通过映射和指定子层来实现。

```py
In[111]:= Map[Keys,Asc,#]&/@{{0},{1},{2}}//Column
Out[111]= {User}
<|User->{Edgar,Anya}|>
<|User-><|Edgar->{id,Parameters},Anya->{id,Parameters}|>|>

```

如表面层(0)所示，关键是用户。下一个子层有键 Edgar 和 Anya，最后一层有键 Edgar 和 Anya 的键 ID 和参数。使用 MapIndexed，我们可以查看整个关联，并将关键字应用到子级别，以显示关键字的前身。

```py
In[112]:=
Print["Level 0: "<>ToString@MapIndexed[Keys,Asc,{0}]]
Print["Level 1: "<>ToString@MapIndexed[Keys,Asc,{1}]]
Print["Level 2: "<>ToString@MapIndexed[Keys,Asc,{2}]]
Level 0: {{}[User]}
Level 1: <|User -> {{Key[User]}[Edgar], {Key[User]}[Anya]}|>
Level 2: <|User -> <|Edgar -> {{Key[User], Key[Edgar]}[id], {Key[User],
Key[Edgar]}[Parameters]}, Anya -> {{Key[User], Key[Anya]}[id], {Key[User],
Key[Anya]}[Parameters]}|>|>

```

在级别 0，只存在关键用户，前任是{}。在级别 1 上，前任用户和键 Edgar 和 Anya 是键 User 的值，而在级别 2 上，前任键是键 ID 和参数的 Edgar/Anya 和 User。换句话说，表达式{Key[User]，Key[Anya]}[id]，意味着 id 对应于 key Anya，Anya 对应于 key User，以此类推。这也很有用，因为这意味着对键的一个或多个值的访问是通过应用于指定键的关联的操作符形式来完成的。

```py
In[113]:=
Asc["User"]["Edgar"]["id"](*{Key[User],Key[Anya]}[id],*)
Out[113]=0

```

如图所示，我们获得了与 Edgar inside User 中的密钥 ID 相对应的值。要查看前面表达式的图形表示，我们可以使用 MapIndexed 来标记应用的键和数据集的位置，例如，在子层 4 中(图 [3-49](#Fig49) )

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig49_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig49_HTML.jpg)

图 3-49

标记嵌套关联中的键的数据集表示形式

```py
In[114]:= Dataset@MapIndexed[Framed[Labeled[#2,#1],FrameMargins->0,RoundingRadius->5]&,Asc,{4}](*Try changin the number to see how the expression changes*)
Out[114]=

```

每个框都包含前任键的值。这就是为什么 1 GB 对应{ Key[用户]，Key[埃德加]，Key[参数]，Key[上网流量]}。看完整个表达式，规范的级别是无穷大(图 [3-50](#Fig50) )。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig50_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig50_HTML.jpg)

图 3-50

嵌套关联中键的框架级别

```py
In[115]:=
MapIndexed[Framed[Labeled[#2,#1,ImageMargins->0,Spacings->0],FrameMargins->0,RoundingRadius->5]&,Asc,Infinity]
Out[115]=

```

值使用与键相同的方法。若要测试密钥是否存在，请使用 KeyExistQ 如果键存在，则返回 true。否则就是假的。要测试更深层次的内容，请使用 Map。

```py
In[116]:= {KeyExistsQ[Asc,"User"],Map[KeyExistsQ["Anya"],Asc,{1}],Map[KeyExistsQ["Anya"],Asc,{2}]}
Out[116]= {True,<|User->True|>,<|User-><|Edgar->False,Anya->False|>|>}

```

测试特定形式的键是否存在于关联中的另一种方法是使用 key memberq——例如，如果有一个字符串模式键。

```py
In[117]:= KeyMemberQ[Asc["User"]["Anya"],_String]
Out[117]= True

```

要测试给定的键是否存在值，请使用 Lookup。

```py
In[118]:= Lookup[Asc["User"]["Anya"],"Parameters"]
Out[118]= <|Active->False,Region->MX,Internet Traffic->3 GB|>

```

要根据标准选择键，请使用 KeySelect。

```py
In[119]:= KeySelect[Asc["User"]["Anya"],StringQ]
Out[119]= <|id->2,Parameters-><|Active->False,Region->MX,Internet Traffic->3 GB|>|>

```

或者简单地使用 KeyTake 来抓取特定的键。

```py
In[120]:= KeyTake[Asc["User"]["Anya"]["Parameters"],{"Region","Internet Traffic"}]
Out[120]= <|Region->MX,Internet Traffic->3 GB|>

```

若要移除某个键，请使用 KeyDrop。

```py
In[121]:= KeyDrop[Asc["User"],"Edgar"]
Out[121]= <|Anya-><|id->2,Parameters-><|Active->False,Region->MX,Internet Traffic->3 GB|>|>|>

```

要分配新值，与该键相关联的值将被分配新值。

```py
In[122]:= Asc["User"]["Edgar"]["Parameters"]["Region"]="CZ"
Out[122]= CZ

```

将它传递到数据集中，我们可以寻找新的赋值(图 [3-51](#Fig51) )。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig51_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig51_HTML.jpg)

图 3-51

区域值更改为 CZ 的数据集

```py
In[123]:= Dataset[Asc]
Out[123]=

```

要向关联中添加一个键和值，我们可以通过指定位置来插入新的表达式，以将其与键一起插入(图 [3-52](#Fig52) )。

![img/500903_1_En_3_Chapter/500903_1_En_3_Fig52_HTML.jpg](img/500903_1_En_3_Chapter/500903_1_En_3_Fig52_HTML.jpg)

图 3-52

由关键位置添加的新行

```py
In[124]:= Insert[Asc["User"],"Alexandra"-><|"id"->0,"Parameters"-><|"Active"->False,"Region"->"RS","Internet Traffic"->"12 GB"|>|>,Key["Edgar"]]//Dataset
Out[124]=

```