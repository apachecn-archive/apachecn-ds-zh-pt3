# 七、数据探索

在本章中，我们将通过 Wolfram Data Repository 在线平台了解数据管理的基础知识。我们将回顾这个网站是如何建立的，以便通过 Wolfram 语言更好地理解它在 Mathematica 中的用途。将举例说明如何通过使用 Wolfram 语言及其在表格数据集中的数据表示以及使用查询命令从该平台下载数据。我们还将了解如何在数据集中查看数据，如何在格式化数据集中应用用户函数和命令。

## Wolfram 数据仓库

Wolfram 数据仓库是一个网站，它又是一个数据仓库，位于云中。这个数据库包含不同类别的信息，如计算机科学、气象学、农业、体育、文本和文学教育等等。虽然这个知识库属于 Wolfram Research，但它的特点是属于公共领域。在 Wolfram 数据存储库中，包含的信息是经过选择、结构化和固化的可计算数据，可直接用于执行数值计算、估计、分析、统计或演示等。该存储库中托管的内容是来自许多来源的数据、全球已知的数据集和发布数据。所有这些信息都被设计成任何个人都可以在全球范围内访问。Wolfram 数据存储系统提供了一个数据源，反过来也可以存储新信息。存储在存储库中的信息是为直接实现 Wolfram 语言而设计的。

正如我们在数据导入部分看到的，我们通过接收 HTTP 类型的响应来了解网站是否处于活动状态，如图 [7-1](#Fig1) 所示。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig1_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig1_HTML.jpg)

图 7-1

Wolfram 数据仓库的 Http 响应对象。正如我们所看到的，我们已经收到了成功的回应

```py
In[1]:= URLRead["https://datarepository.wolframcloud.com/"]
Out[1]=

```

### Wolfram 数据库网站

要访问该网站，请在您最喜爱的浏览器中输入以下 URL 地址: [`https://datarepository.wolframcloud.com`](https://datarepository.wolframcloud.com) 。图 [7-2](#Fig2) 显示了 Wolfram 数据仓库的欢迎页面。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig2_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig2_HTML.jpg)

图 7-2

Wolfram 数据库网站

Note

出现的图像是重定向到与该图像关联的数据集的链接。

一旦站点被加载，我们将看到存储库标题；在它下面，有一个选项菜单，可以通过类别或数据类型来导航站点。在该菜单中，您会发现存在不同的类别和不同类型的数据，可以是文本、数字数据、图像等。在菜单选项中，您还可以找到联系人选项、自定义搜索和提交新数据。后者是重定向到另一个页面的选项，该页面显示向该存储库发布和上传新数据的指令。如果我们向下滚动，我们还会看到存在的类别和数据类型。如果是这样，可以通过单击“浏览所有资源”链接来浏览所有资源。要浏览类别，我们可以从菜单中选择类别，或者在初始站点单击类别的名称。图 [7-3](#Fig3) 显示了一旦我们选择了一个类别——在本例中是生命科学——网站的样子。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig3_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig3_HTML.jpg)

图 7-3

Wolfram 数据库的生命科学类别

Note

同样的过程也适用于按数据类型导航的情况。

### 选择类别

每个类别显示标题、该类别中包含的元素数量，以及按数据类型过滤该类别内容的选项。关于内容，每个示例数据类型都显示有其标题、其包含的数据的简短描述以及与该示例数据相关联的不同标签。例如，图像显示了费希尔的虹膜已知数据集。一旦我们选择了一个样本数据集，它将把我们带到包含关于该数据集的相关信息的站点，如图 [7-4](#Fig4) 所示，在那里选择了费希尔虹膜数据集。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig4_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig4_HTML.jpg)

图 7-4

费希尔虹膜数据集

选择样本数据集后，将显示数据集的简要描述，以及可以进行的不同计算和下载数据或笔记本的不同格式。除此之外，它还包括相关信息，如书目引用、数据资源历史和数据来源。在某些情况下，数据可以下载为不同类型的格式，如逗号分隔值(CSV)、制表符分隔值(TSV)、JavaScript 对象表示法(JSON)等。在开始从 Wolfram 数据库下载数据之前，需要有一个 Wolfram ID。这个 ID 是一个帐户，它使我们能够访问 Wolfram 数据库的内容，以及其他好处，如 Wolfram One 和 Wolfram Alpha。要从 Mathematica 登录，进入帮助➤登录菜单，会出现一个如图 [7-5](#Fig5) 所示的窗口。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig5_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig5_HTML.jpg)

图 7-5

Wolfram 云登录提示

在新窗口中，您将输入您的电子邮件和密码，以便能够从 Mathematica 访问 Wolfram 数据库的内容。

## 从 Wolfram 数据库中提取数据

让我们先来看看费希尔数据集的信息和属性；为此，我们必须通过 ResourceObject 检索信息。使用 ResourceObject(图 [7-6](#Fig6) )我们现在可以通过单击加号图标来查看已发布数据的不同属性。将显示关于数据的详细信息，如样品名称、类型、版本、数据大小等。

```py
In[2]:= ResourceObject["Sample Data: Fisher's Irises"]
Out[2]=

```

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig6_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig6_HTML.jpg)

图 7-6

费希尔虹膜的资源对象

如果我们想查看资源对象的属性，请输入以下代码。这将为我们提供一个可以访问并且与数据样本相关的属性列表。

```py
In[3]:= ResourceObject["Sample Data: Fisher's Irises"]["Properties"]
Out[3]= {AllVersions,AutoUpdate,Categories,ContentElementLocations,ContentElements,ContentSize,ContentTypes,ContributorInformation,DefaultContentElement,Description,Details,Documentation,DocumentationLink,DOI,DownloadedVersion,ExampleNotebook,ExampleNotebookObject,Format,InformationElements,Keywords,LatestUpdate,Name,Originator,Properties,ReleaseDate,RepositoryLocation,ResourceLocations,ResourceType,SeeAlso,ShortName,SourceMetadata,UUID,Version,VersionInformation,VersionsAvailable,WolframLanguageVersionRequired}

```

已经知道了与信息相关的属性列表，我们现在可以从 Mathematica 下载数据样本的练习笔记本。

```py
In[4]:= ResourceObject["Sample Data: Fisher's Irises"]["ExampleNotebook"]
Out[4]= NotebookObject[Sample-Data-Fishers-Irises_examples.nb]

```

一旦你完成评估代码，它将自动打开新的笔记本。如果我们想从云中操作笔记本，我们可以输入 NotebookObject。这将返回给我们一个云，就像与超链接相关联的对象。

```py
In[5]:= ResourceObject["Sample Data: Fisher's Irises"]["ExampleNotebookObject"]
Out[5]= CloudObject[https://www.wolframcloud.com/obj/5e59b79e-d95e-4f6f-a7c8-f1276ba17be2]

```

如果我们按下新笔记本的链接，它将打开互联网浏览器，并向我们显示它在 Wolfram 云中。图 [7-7](#Fig7) 显示了这一点。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig7_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig7_HTML.jpg)

图 7-7

从 Wolfram 云打开的 Fisher 虹膜数据样本

要从 Mathematica 访问原始示例数据站点，我们进入文档，这将提供一个 URL 对象，您可以通过单击双 v 形图标进入该站点。

```py
In[6]:= ResourceObject["Sample Data: Fisher's Irises"]["Documentation"]
Out[6]= URL[https://datarepository.wolframcloud.com/resources/Sample-Data-Fishers-Irises]

```

### 在 Mathematica 中访问数据

使用 ResourceData 将数据下载到对象资源也是同样的方法。通过 ResourceData，我们可以访问指定资源的内容；在这种情况下，它是费希尔虹膜数据样本(图 [7-8](#Fig8) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig8_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig8_HTML.jpg)

图 7-8

费希尔虹膜数据集对象

```py
In[7]:= ResourceData[ResourceObject["Sample Data: Fisher's Irises"]]
Out[7]=

```

如图 [7-8](#Fig8) 所示，返回的对象是一个与数据集头一起使用的 ResourceData。通过对数据样本进行可视化检查，我们发现它是一个包含 150 个值的数据集，包含五列:Species、SepalLength、SepalWidth、PetalLength 和 PetalWidth。如果我们注意的话，我们可以看到列 SepalLength、SepalWidth、PetalLength 和 PetalWidth 的值是如何表示数量的。进一步向下移动整个数据集，我们发现这些物种分为三类:刚毛藻、杂色藻和海滨藻。如果我们想要访问与数据集相关的信息，我们必须通过 resource 对象来完成，并通过 ResourceData 表单来检索它，如下所示。

```py
In[8]:= ResourceObject["Sample Data: Fisher's Irises"]["ContentElements"]
Out[8]={ColumnDescriptions,ColumnTypes,Content,DataType,Dimensions,ObservationCount,RawData,Source,TrainingData,TestData}

```

使用 ContentElements 属性，我们可以访问数据样本的元素，这些元素出现在资源对象中。ContentElements 向我们展示了与样本数据相关的信息，比如列信息、数据源、训练数据、测试数据等。—不要与创建的资源对象的属性相混淆，因为这是不同的，因为您可以为另一个关联名称构造资源对象。要从 ContentElements 中检索信息，我们必须使用 ResourceData。这个命令将使我们能够访问数据样本的内容——在本例中，是 Fisher 的虹膜。现在让我们获取列的数据类型。

```py
In[9]:= ResourceData[ResourceObject["Sample Data: Fisher's Irises"],"ColumnTypes"]
Out[9]= {Numeric,Numeric,Numeric,Numeric,Categorical}

```

ResourceData 命令的第二个参数是我们正在寻找的元素。运行上述代码向我们展示了四种数据类型，三种数值型和一种分类型。使用一个纯函数，我们可以在一个表达式中获得信息。如果我们添加 Column 命令，可以更好地查看信息。

```py
In[10]:= Column[ResourceData[ResourceObject["Sample Data: Fisher's Irises"],#]&/@{"ColumnDescriptions","Dimensions","Source"}]
Out[10]= {Sepal length in cm.,Sepal width in cm.,Petal length in cm.,Petal width in cm.,Species of iris}
{150,4}
Fisher,R.A. "The use of multiple measurements in taxonomic problems" Annual Eugenics, 7, Part II, 179-188 (1936); also in "Contributions to Mathematical Statistics" (John Wiley, NY, 1950).

```

通过这种方式，我们可以了解列中的信息类型，比如维度(每 4 列 150 行)和数据源。

### 数据观察

在这一部分中，我们将了解如何观察数据集中的数据。我们将使用 Iris 数据集，它是从 Wolfram 数据库中提取的。我们先把数据样本命名为 Fisher 该变量将包含包含数量的数据集。

```py
In[11]:= Fisher=ResourceData[ResourceObject["Sample Data: Fisher's Irises"]];

```

如果我们看数据集，我们会注意到这些数字有它们的单位和数量级。有了数据集，我们就可以执行无止境的过程，比如按照物种类型的类别变量对内容进行分组。需要强调的是，我将访问包含在费希尔变量中的数据集。让我们看看包含按物种分组的每一列的数据类型(图 [7-9](#Fig9) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig9_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig9_HTML.jpg)

图 7-9

按物种分组的虹膜数据

```py
In[12]:= Fisher[GroupBy["Species"]]
Out[12]=

```

让我们注意一下数据是如何分成三类的:setosa、versicolor 和 virginica。如果我们注意细节，我们会注意到每个类别的物种栏末尾都有一个数字 50。这意味着除了显示的行之外还有 50 行，每个类别总共有 50 行，即总共 150 行，这与我们查看样本数据的维度时的 150 行相匹配。同时，如果我们单击其中一个类别，它将只显示该类别的列，如图 [7-10](#Fig10) 所示。如果我们在一个类别中选择特定的列，也会发生同样的情况——它将只显示该类别的那个列；试试看会发生什么。也有可能点击任何列，这将只显示我们选择的列，但三个类别。我的意思是，例如，如果我们选择 SepalLength，我们将会看到这一栏中三个物种的内容，如图 [7-10](#Fig10) 所示

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig10_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig10_HTML.jpg)

图 7-10

选定的 SepalLength 列

可以按种类分组，只选择包含数值的列。例如，如果我们想要对数据集进行目视检查，这很有帮助(图 [7-11](#Fig11) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig11_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig11_HTML.jpg)

图 7-11

隐藏了物种列的数据集

```py
In[13]:= Query[GroupBy[Key["Species"]→KeyTake[{"SepalLength","SepalWidth","PetalLength","PetalWidth"}]]][Fisher]
Out[13]=

```

在后一段代码中，我们使用 Key 命令来访问 species 列的键。一旦访问了这些键，我们就编写一个转换规则，以便为每个提取的键分配从列(SepalLength，SepalWidth，PetalLength，PetalWidth)提取的关联(KeyTake)，然后分组并应用到 Fisher 的数据集。

如果我们想要计算 Fisher 数据集中的数据元素，我们可以添加一个 ID 列作为标签(图 [7-12](#Fig12) )来列出它包含的数据。为了实现这一点，首先我们创建一个从 1 到数据集长度的键和值的关联。然后，这条指令被应用于数据集对象 Fisher 的，它添加 ID 作为行的标签。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig12_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig12_HTML.jpg)

图 7-12

ID 被添加到 Fisher 的数据集中

```py
In[14]:= Query[AssociationThread[Range[Length@#]→Range[Length@#]]][Fisher]&[Fisher]
Out[14]=

```

如果我们向下拖动该条，我们会看到计数器达到 150 个元素。

如果您不想添加一个枚举列来对元素进行计数，我们可以使用 Counts 命令(图 [7-13](#Fig13) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig13_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig13_HTML.jpg)

图 7-13

数据集上的计数元素

```py
In[15]:= Fisher[Counts,"Species"]
Out[15]=

```

这产生了属于 setosa、versicolor 和 virginica 的 50 个数据。如果我们把它们加起来，我们得到 150。你也可以使用查询命令，`Query[Counts, "Species"][Fisher].`

现在让我们看看如何获得每一列的三个类别的平均值。如图 [7-15](#Fig15) 所示，如果我们知道 setosa、versicolor 和 virginica 物种的萼片长度、萼片宽度、花瓣长度和花瓣宽度的平均值，这是可能的。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig14_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig14_HTML.jpg)

图 7-14

四列的平均值，按物种划分

```py
In[16]:= Query[GroupBy[Key["Species"]→KeyTake[{"SepalLength","SepalWidth","PetalLength","PetalWidth"}]],Mean][Fisher]
Out[16]=

```

但是，如果我们想要获得所有类别的列的平均值，一种方法是将 Mean 作为查询应用于整个数据集中的列数(图 [7-15](#Fig15) )。

```py
In[17]:= Query[Mean][Fisher[[All,2;;5]]]
Out[17]=

```

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig15_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig15_HTML.jpg)

图 7-15

所有物种的四列平均值

Note

Mean 命令处理数量并返回平均值作为数量。

## 描述统计学

在这一部分中，我们将了解如何在数据集的格式内执行虹膜数据和计算的描述性统计，以及如何创建自定义的网格格式。让我们获得一些关于这个数据集的描述性统计数据。让我们创建一个名为 Stats 的函数。让我们从构建计算最大值、最小值、平均值、中间值、第一个和第三个四分位数的函数开始。

```py
In[18]:= Stats[data_]:=
{
{#[{"Max:",Max@data}]},
{#[{"Min:",Min@data}]},
{#[{"Mean:",Mean@data}]},
{#[{"Median:",Median@data}]},
{#[{"1st quartile:",Quantile[data,0.25]}]},
{#[{"3rd quartile:",Quantile[data,0.75]}]}
}&[Row]

```

现在将创建的函数应用于每一列。这是为了获得 SepalLength、SepalWidth、PetalLength 和 PetalWidth 的总体统计数据(图 [7-16](#Fig16) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig16_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig16_HTML.jpg)

图 7-16

应用于每列的函数统计信息

```py
In[19]:= {{#1,#2,#3,#4},{Fisher[Stats,#1],Fisher[Stats,#2],Fisher[Stats,#3],Fisher[Stats,#4]}}&["SepalLength","SepalWidth","PetalLength","PetalWidth"]//Grid
Out[19]=

```

这也可以通过 TabView 以选项卡格式的紧凑形式显示(图 [7-17](#Fig17) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig17_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig17_HTML.jpg)

图 7-17

Tabview 格式

```py
In[20]:= TabView[{#1→Fisher[Stats,#1],#2→Fisher[Stats,#2],#3→Fisher[Stats,#3],#4→Fisher[Stats,#4]},ControlPlacement→Left]&["SepalLength","SepalWidth","PetalLength","PetalWidth"]
Out[20]=

```

使用 TabView，我们用每列的名称创建三个选项卡，其中显示最大值、最小值、平均值、中值、第一个和第三个四分位数；这些列是 SepalLength、SepalWidth、PetalLength 和 PetalWidth。

### 表格和网格格式

另一种方法是为每个物种创建一个表。这样，我们将创建一个更好的数据表示，从而能够正确地读取它。我们通过应用 Nest 命令提取数据。使用这个命令，我们可以指定一个命令或函数将被应用的次数；在这种情况下，我们将应用它两次。

```py
In[21]:= Short[Values[Nest[Normal,Fisher,2]]]
{SLall,SWall,PLall,PWall}=%[[All,#]]&/@{2,3,4,5};
Out[21]//Short= {{setosa,5.1cm,3.5cm,1.4cm,0.2cm},{setosa,4.9cm,3.cm,1.4cm,0.2cm},<<146>>,{virginica,6.2cm,3.4cm,5.4cm,2.3cm},{virginica,5.9cm,3.cm,5.1cm,1.8cm}}

```

所有物种的值由列分隔，我们将继续创建一个列表而不是一个函数，其中统计数据将根据每一列显示，并添加方差、标准差、偏斜度和峰度等计算。然后，我们将在变量 DescriptiveStats 中分配计算。

```py
In[22]:= {Max[#],Min[#],Median[#],Mean[#],Variance[#],StandardDeviation[#],Skewness[#],Kurtosis[#],Quantile[#,0.25],Quantile[#,.75]}&/@{SLall,SWall,PLall,PWall};
DescriptiveStats=%;

```

通过这些计算并添加行和列标题，可以创建一个表格(图 [7-18](#Fig18) )。

```py
In[23]:=
TableHeads={
Style["Sepal Length",#1,ColorData["HTML"]["Maroon"],#2,#3],
Style["Sepal Width",#1,ColorData["HTML"]["YellowGreen"],#2,#3],
Style["Petal Length",#1,ColorData["HTML"]["SteelBlue"],#2,#3],
Style["Petal Width",#1,ColorData["HTML"]["Orange"],#2,#3]
}&["Title",Italic,20];
TableRows={
Style["Max",#1,#2],Style["Min",#1,#2],
Style["Median",#1,#2],Style["Mean",#1,#2],Style["Variance",#1,#2],
Style["Standard\n Deviation",#1,#2],
Style["Skewness",#1,#2],
Style["Kurtosis",#1,#2],
Style["1st quartile",#1,#2],
Style["3rd quartile",#1,#2]
}&["Text",Italic];
TableForm[DescriptiveStats,TableHeadings→{TableHeads,TableRows}]
Out[23]=

```

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig18_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig18_HTML.jpg)

图 7-18

按四个特征显示描述性统计数据的表格

请注意，除了偏度和峰度之外，统计数据是用它们的单位计算的，因为根据定义，它们是无量纲的。但是，我们可以从 Grid 创建一个更好的结构，因为可以像电子表格格式一样添加分隔线。为此，我们将向数据添加表行，然后应用转置，以便每个计算的统计数据都有其各自的名称。随后，我们将添加列标题。

```py
In[24]:= Transpose[Prepend[DescriptiveStats,TableRows]];
{" ",Style["Sepal Length",#1,ColorData["HTML"]["Maroon"],#2,#3],Style["Sepal Width",#1,ColorData["HTML"]["YellowGreen"],#2,#3],Style["Petal Length",#1,ColorData["HTML"]["SteelBlue"],#2,#3],
Style["Petal Width",#1,ColorData["HTML"]["Orange"],#2,#3]
}&["Title",Italic,20];
NewTable=Prepend[%%,%];

```

我们继续以电子表格的形式创建表格(图 [7-19](#Fig19) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig19_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig19_HTML.jpg)

图 7-19

描述性统计数据的网格视图

```py
In[25]:= Grid[NewTable,
ItemSize→{{None, Scaled[0.11], Scaled[0.11],Scaled[0.11]}},Background→{{LightGray},None},Dividers→{{False},{1,2,3,4,5,6,7,8,9,10,11→True,-2→Blue}},Alignment→Center]
Out[25]:=

```

如果我们想要为每个物种构建表，我们必须首先使用 Cases 命令按物种分离数据集。我们使用案例，因为它给了我们使用模式的自由。首先编写提取原始数据的代码，我们将使用 Shallow 来隐藏 150 个值，而不是使用 Short。

```py
In[26]:= Shallow[Values[Nest[Normal,Fisher,2]],1]
Out[26]//Shallow= {<<150>>}

```

我们将为 versicolor 物种创建表，然后提取 versicolor 的值，并将列的值存储在变量 SLVersi、SWVersi、PLVersi 和 PWVersi 中。

```py
In[27]:= Shallow[Cases[%,{"versicolor",__}],1]
{SLVersi,SWVersi,PLVersi,PWVersi}=%[[All,#]]&/@{2,3,4,5};
Out[27]//Shallow= {<<50>>}

```

对于统计数据的计算，我们做了与以前相同的构造。但是我们添加了名称 versicolor 来区分该表属于 versicolor 种，而不是空白。

```py
In[28]:= TableRows;
{Max[#],Min[#],Median[#],Mean[#],Variance[#],StandardDeviation[#],Skewness[#],Kurtosis[#],Quantile[#,0.25],Quantile[#,.75]}&/@{SLVersi,SWVersi,PLVersi,PWVersi};
DescriptiveStats2=Prepend[%,%%];
Transpose[DescriptiveStats2];
{
Style["Versicolor","Text",Red,Italic,20],Style["Sepal Length",#1,ColorData["HTML"]["Maroon"],#2,#3],
Style["Sepal Width",#1,ColorData["HTML"]["YellowGreen"],#2,#3],
Style["Petal Length",#1,ColorData["HTML"]["SteelBlue"],#2,#3],
Style["Petal Width",#1,ColorData["HTML"]["Orange"],#2,#3]
}&["Title",Italic,20];
NewTable2=Prepend[%%,%];

```

现在我们为物种云芝建立表格(图 [7-20](#Fig20) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig20_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig20_HTML.jpg)

图 7-20

云芝物种的描述性统计

```py
In[29]:= Grid[NewTable2,ItemSize→{{None, Scaled[0.11], Scaled[0.11],Scaled[0.11]}},Background→{{LightGray},None},Dividers→{{False},{1,2,3,4,5,6,7,8,9,10,11→True,-2→Blue}},Alignment→Center]
Out[29]=

```

我们这样做只是为了云芝的物种；如果需要，将对每个物种执行相同的过程。例如，如果选择其他物种的案例，我们会将文本更改为相应的物种。

## 数据集可视化

鉴于 Wolfram 语言在数据集内执行描述性统计的能力，统计图可以在数据集格式内实现，正如我们将在这个片段中看到的。

我们可以从图形中获得更好的视角，我们将使用数据集格式(图 [7-21](#Fig21) )来按种类显示图形。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig21_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig21_HTML.jpg)

图 7-21

分布图

```py
In[30]:= Fisher[GroupBy["Species"],DistributionChart[#,PlotTheme→"Classic",PlotLabel→"PetalLength cm", GridLines→Automatic]&,"]&,"PetalLength"]
Out[30]=

```

我们可以执行相同的过程，但是对于盒须图(图 [7-22](#Fig22) )，但是选择另一列。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig22_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig22_HTML.jpg)

图 7-22

盒须图

```py
In[31]:= Fisher[GroupBy["Species"],BoxWhiskerChart[#,"Outliers",PlotTheme→"Detailed",ChartLabels→Placed[{"SepalLength cm"},Above],BarOrigin→Right,ChartStyle→Blue]&,"SepalLength"]
Out[31]=

```

如果点击硬币，它会放大图形(图 [7-23](#Fig23) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig23_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig23_HTML.jpg)

图 7-23

海滨锦鸡儿种盒须图

这同样适用于直方图。当图形非常大时，它在数据集中看起来是隐藏的，但是我们仍然可以选择它，如图 [7-24](#Fig24) 所示。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig24_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig24_HTML.jpg)

图 7-24

杂色直方图

```py
In[32]:=Fisher[GroupBy["Species"],
 Labeled[Histogram[#,
    ColorFunction → (Hue[3/5, 2/3, #] &)], {Rotate["Frequency",
     90 Degree], "SepalWidth cm"}, {Left, Bottom}] &, "SepalWidth"]
Out[32]=

```

这里我们展示了每个物种的萼片长度(x)对萼片宽度(y)的三维散点图(图 [7-25](#Fig25) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig25_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig25_HTML.jpg)

图 7-25

2D 散点图

```py
In[33]:=Fisher[GroupBy["Species"],
 Labeled[ListPlot[{#, #}], {Rotate["Sepal width cm", 90 Degree], "Sepal length cm"}, {Left, Bottom}] &, {"SepalLength","SepalWidth"}]
Out[33]=

```

要返回到完整的数据集，请像对待其他数据集一样单击数据集图标。

### 数据集格式之外的数据

事实是，也有可能粗略地提取数据，如下所示。我们这样做是为了更好地处理数据。由于列表很长，我们将使用 Short 命令。

```py
In[34]:= Short[ResourceData[ResourceObject["Sample Data: Fisher's Irises"],"RawData"]]
Out[34]//Short= {<<1>>}

```

有了已经提取的数据，我们就可以用 values 函数获取值，并将它们转换成标准表达式。

```py
In[35]:= Short[Normal[Values[%]]]
Out[35]//Short= {{setosa,5.1cm,3.5cm,1.4cm,0.2cm},{setosa,4.9cm,3.cm,1.4cm,0.2cm},<<146>>,{virginica,6.2cm,3.4cm,5.4cm,2.3cm},{virginica,5.9cm,3.cm,5.1cm,1.8cm}}

```

在 MapAt 的帮助下，我们可以提取数量的大小。MapAt 命令让我们可以自由选择在哪里应用数量函数。我们选择用 all 将它应用于所有行，但是只从第 2 列到第 4 列，这是数量所在的位置。

```py
In[36]:= Short[Iris=MapAt[QuantityMagnitude,%,{All,2;;5}]]
Out[36]//Short= {{setosa,5.1,3.5,1.4,0.2},<<148>>,{virginica,5.9,3.,5.1,1.8}}

```

这里值得问一个问题:如果可以用它们进行计算，我们为什么要去掉这些单位？我们提取所有量的大小，因为它们具有相同的数量级(cm ),所以每次计算都将使用相同的单位，除非我们对数据进行了转换或变换。

### 2D 和 3D 绘图

另一方面，用 Wolfram 语言操作列表更容易。有了列表形式的数据，我们现在将继续绘制箱线图和分布图中的三列(图 [7-26](#Fig26) )。我们将只选择三列数据。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig26_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig26_HTML.jpg)

图 7-26

所有物种的盒须图和分布图

```py
In[37]:= Row[
{BoxWhiskerChart[{Iris[[All,#1]],Iris[[All,#2]],Iris[[All,#3]],Iris[[All,#4]]},"Outliers",PlotRange→Automatic,FrameTicks→True,ChartStyle→"SandyTerrain",PlotLabel→"All Species",GridLines→Automatic,ChartLegends→Placed[{"SepalLength","SepalWidth","PetalLength","PetalWidth"},Bottom],ImageSize→Small],DistributionChart[{Iris[[All,#1]],Iris[[All,#2]],Iris[[All,#3]],Iris[[All,#4]]},PlotRange→Automatic,FrameTicks→True,ChartStyle→"SouthwestColors",PlotLabel→"All Species",ChartLegends→Placed[{"SepalLength","SepalWidth","PetalLength","PetalWidth"},Bottom],PlotTheme→"Detailed",GridLines→Automatic,ImageSize→Small]
}]&[2,3,4,5]
Out[37]=

```

为了改进这一点，让我们为每个物种绘制图表。我们将用案例将列表与它们各自的种类分开(图 [7-27](#Fig27) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig27_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig27_HTML.jpg)

图 7-27

每个物种的盒须图具有四个特征

```py
In[38]:= Short[Setosa=Cases[Iris,{"setosa",__}]]
Short[Versi=Cases[Iris,{"versicolor",__}]]
Short[Virgin=Cases[Iris,{"virginica",__}]]
Out[38]//Short= {{setosa,5.1,3.5,1.4,0.2},<<48>>,{setosa,5.,3.3,1.4,0.2}}
Out[38]//Short= {{versicolor,7.,3.2,4.7,1.4},<<48>>,{versicolor,5.7,2.8,4.1,1.3}}
Out[38]//Short= {{virginica,6.3,3.3,6.,2.5},<<48>>,{virginica,5.9,3.,5.1,1.8}}
In[39]:= Column@{
BoxWhiskerChart[{Setosa[[All,#1]],Setosa[[All,#2]],Setosa[[All,#3]],Setosa[[All,#4]]},"Outliers",PlotRange→Automatic,FrameTicks→True,ChartStyle→"Rainbow",PlotLabel→"Setosa",ChartLegends→Placed[{"SepalLength","SepalWidth","PetalLength","PetalWidth"},Bottom],GridLines→Automatic],BoxWhiskerChart[{Versi[[All,#1]],Versi[[All,#2]],Versi[[All,#3]],Versi[[All,#4]]},"Outliers",PlotRange→Automatic,FrameTicks→True,ChartStyle→"Rainbow",PlotLabel→"Versicolor",ChartLegends→Placed[{"SepalLength","SepalWidth","PetalLength","PetalWidth"},Bottom],GridLines→Automatic],BoxWhiskerChart[{Virgin[[All,#1]],Virgin[[All,#2]],Virgin[[All,#3]],Virgin[[All,#4]]},"Outliers",PlotRange→Automatic,FrameTicks→True,ChartStyle→"Rainbow",PlotLabel→"Virginica",ChartLegends→Placed[{"SepalLength","SepalWidth","PetalLength","PetalWidth"},Bottom],GridLines→Automatic]
}&[2,3,4,5]
Out[39]=

```

此外，我们可以将所有物种的萼片宽度与萼片长度的散点图连接起来(图 [7-28](#Fig28) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig28_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig28_HTML.jpg)

图 7-28

2D 散点图为所有物种的前两个特征

```py
In[40]:= ListPlot[{Setosa[[All,{2,3}]],Versi[[All,{2,3}]],Virgin[[All,{2,3}]]},FrameTicks→All,Frame→True,AspectRatio→1,PlotStyle→{Blue,Red,Green},FrameLabel→{"Sepal length cm","Sepal width cm"},PlotLegends→{"Setosa","Versicolor","Virginica"}]
Out[40]=

```

或者我们可以用三个特征做一个 3D 散点图(图 [7-29](#Fig29) )。

![img/500903_1_En_7_Chapter/500903_1_En_7_Fig29_HTML.jpg](img/500903_1_En_7_Chapter/500903_1_En_7_Fig29_HTML.jpg)

图 7-29

每个物种的三个特征的 3D 散点图

```py
In[41]:= ListPointPlot3D[{Setosa[[All,{2,3,4}]],Versi[[All,{2,3,4}]],Virgin[[All,{2,3,4}]]},Ticks→All,AspectRatio→1,PlotStyle→{Blue,Red,Green},AxesLabel→{"Sepal length cm","Sepal width cm","Petal Length cm"},PlotLegends→{"Setosa","Versicolor","Virginica"},PlotTheme→"Detailed",ViewPoint→{0, -3, 3}]
Out[41]=

```

现在，当我们处理完资源对象后，我们需要删除它，以便正确地删除资源的本地缓存。

```py
In[42]:=Clear[Fisher]
DeleteObject[ResourceObject["Sample Data: Fisher's Irises"]]

```